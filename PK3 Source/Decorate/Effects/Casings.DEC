/* Based on Zombie Torrent's casings code (I don't know who made the code for those casings)
** Casing sprites by Captain J. Edited by Chronos "phantombeta" Ouroboros
** Usage:
**  Weapons:  A_FireCustomMissile ("<Casing spawner actor>", 0, 0, <int xPos>, <float yPos>)
**  Monsters: A_CustomMissile ("<Casing spawner actor>", <float yPos/zPos>, <int xPos>, 0.0, CMF_AbsolutePitch | CMF_AimDirection, 0.0)
*/

actor S7_CasingSmoke : S7_ShotSmokeTrail {
    scale 0.05
    alpha 0.35
}

actor S7_CasingSpawnerBase {
    speed 20
    projectile

    +noClip +clientsideOnly
}

actor S7_CasingBase {
    height 2
    radius 2
    speed 6
    bounceType "doom"

    +windThrust +clientsideOnly +ghost +canBounceWater
    +moveWithSector +missile +noBlockmap +noTeleport
    +forceXYBillboard
    -dropOff -noGravity

    states {
    Delaying:
        "----" A    0 A_JumpIf (callACS ("S7_GetEffectsStayTime") == 1, "Delay.Shorter")
        "----" A    0 A_JumpIf (callACS ("S7_GetEffectsStayTime") == 2, "Delay.EvenShorter")
        "----" A    0 A_JumpIf (callACS ("S7_GetEffectsStayTime") == 3, "Delay.ShortAsFuck")
        "----" A    0 A_JumpIf (callACS ("S7_GetEffectsStayTime") == 4, "Delay.DearLordDoYouHaveAFuckingToaster")
        "----" A    0 A_JumpIf (callACS ("S7_GetEffectsStayTime") == 5, "Delay.MeinFuckingGott")
        "----" A    0 A_JumpIf (callACS ("S7_GetEffectsStayTime") == 6, "Delay.DearLordGoBuyANewComputerRightMotherfuckingNow")
        "----" A 2500
        goto Fade
    Delay.Shorter:
        "----" A 1250
        goto Fade
    Delay.EvenShorter:
        "----" A 625
        goto Fade
    Delay.ShortAsFuck:
        "----" A 313
        goto Fade
    Delay.DearLordDoYouHaveAFuckingToaster:
        "----" A 157
        goto Fade
    Delay.MeinFuckingGott:
        "----" A 70
        goto Fade
    Delay.DearLordGoBuyANewComputerRightMotherfuckingNow:
        "----" A 35
        goto Fade

    Fade:
    Fade.Looper:
        "----" A 0 A_JumpIf (callACS ("S7_GetEffectsFadeSpeed") == 1, "Fade.Faster")
        "----" A 0 A_JumpIf (callACS ("S7_GetEffectsFadeSpeed") == 2, "Fade.EvenFaster")
        "----" A 0 A_JumpIf (callACS ("S7_GetEffectsFadeSpeed") == 3, "Fade.FastAsFuck")
        "----" A 0 A_JumpIf (callACS ("S7_GetEffectsFadeSpeed") == 4, "Fade.TheFlash")
        "----" A 0 A_JumpIf (callACS ("S7_GetEffectsFadeSpeed") == 5, "Fade.InTheTimeItTakesForYourComputerToProcessATicThisWouldAlreadyBeGoneInANormalComputer")
        "----" A 0 A_JumpIf (callACS ("S7_GetEffectsFadeSpeed") == 6, "Fade.DearLordGoBuyANewComputerRightMotherfuckingNow")
        goto Fade.Normal

    Fade.Normal:
        "----" A 3 A_FadeOut (0.03)
        goto Fade.Looper
    Fade.Faster:
        "----" A 3 A_FadeOut (0.06)
        goto Fade.Looper
    Fade.EvenFaster:
        "----" A 2 A_FadeOut (0.09)
        goto Fade.Looper
    Fade.Fasterer:
        "----" A 1 A_FadeOut (0.12)
        goto Fade.Looper
    Fade.TheFlash:
        "----" A 1 A_FadeOut (0.15)
        goto Fade.Looper
    Fade.InTheTimeItTakesForYourComputerToProcessATicThisWouldAlreadyBeGoneInANormalComputer:
        "----" A 1 A_FadeOut (0.18)
        goto Fade.Looper
    Fade.DearLordGoBuyANewComputerRightMotherfuckingNow:
        "----" A 1 A_FadeOut (2.0)
        goto Fade.Looper

    Rest1:
        "####" A 0
        "----" A 0 A_Jump (256, "Delaying")
        wait
    Rest2:
        "####" B 0
        "----" A 0 A_Jump (256, "Delaying")
        wait
    Rest3:
        "####" C 0
        "----" A 0 A_Jump (256, "Delaying")
        wait
    Rest4:
        "####" D 0
        "----" A 0 A_Jump (256, "Delaying")
        wait
    Rest5:
        "####" E 0
        "----" A 0 A_Jump (256, "Delaying")
        wait
    Rest6:
        "####" F 0
        "----" A 0 A_Jump (256, "Delaying")
        wait
    Rest7:
        "####" G 0
        "----" A 0 A_Jump (256, "Delaying")
        wait
    Rest8:
        "####" H 0
        "----" A 0 A_Jump (256, "Delaying")
        wait


    Death:
        "----" A 0 A_Jump (256, "Rest1", "Rest3", "Rest5", "Rest7")
        goto Rest1
    }
}

//-------------------------------------------------------------------------------------------
//
// 20ga Shotgun Shell
//
//-------------------------------------------------------------------------------------------
actor S7_ShellCasingSpawn : S7_CasingSpawnerBase {
    states {
    Spawn:
        TNT1 A 0 noDelay A_CustomMissile ("S7_ShellCasing", 0, 0, random [sfx] (80, 100), 2, random [sfx] (-40, -60))
        stop
    }
}

actor S7_ShellCasing : S7_CasingBase {
    seeSound "Weapons/shell"
    scale .2

    states {
    Spawn:
        CAS0 ABCDEFGH 2 // A_SpawnItemEx ("S7_CasingSmoke")
        loop
    }
}

//-------------------------------------------------------------------------------------------
//
// 12ga Shotgun Shell
//
//-------------------------------------------------------------------------------------------
actor S7_12gaShellCasingSpawn : S7_CasingSpawnerBase {
    states {
    Spawn:
        TNT1 A 0 noDelay A_CustomMissile ("S7_12gaShellCasing", 0, 0, -110, 2, 50)
        stop
    }
}

actor S7_12gaShellCasing : S7_CasingBase {
    seeSound "Weapons/shell"
    scale .2

    states {
    Spawn:
        CAS4 FFGGHABCDEFGH 2
    Idle:
        CAS4 ABCDEFGH 2 // A_SpawnItemEx ("S7_CasingSmoke")
        loop
    }
}

//-------------------------------------------------------------------------------------------
//
// 9mm Casing
//
//-------------------------------------------------------------------------------------------
actor S7_9mmCasingSpawn : S7_CasingSpawnerBase {
    states {
    Spawn:
        TNT1 A 0 noDelay A_CustomMissile ("S7_9mmCasing", -5, 0, random [sfx] (-80, -100), 2, random [sfx] (45, 80))
        stop
    }
}

actor S7_9mmCasing : S7_CasingBase {
    seeSound "Weapons/casing"
    scale .2

    states {
    Spawn:
        CAS1 ABCDEFGH 2 noDelay A_SpawnItemEx ("S7_CasingSmoke")
        loop
    }
}

//-------------------------------------------------------------------------------------------
//
// .45 ACP Casing
//
//-------------------------------------------------------------------------------------------
actor S7_45ACPCasingSpawn : S7_9mmCasingSpawn {
    states {
    Spawn:
        TNT1 A 0 noDelay A_CustomMissile ("S7_45ACPCasing", -5, 0, random [sfx] (-80, -100), 2, random [sfx] (45, 80))
        stop
    }
}

actor S7_45ACPCasing : S7_9mmCasing { scale .3 }

//-------------------------------------------------------------------------------------------
//
// Revolver Casing
//
//-------------------------------------------------------------------------------------------
actor S7_RevolverCasingSpawn : S7_CasingSpawnerBase {
    states {
    Spawn:
        TNT1 A 0 noDelay A_CustomMissile ("S7_RevolverCasing", -5, 0, 0, 2, -90)
        stop
    }
}

actor S7_RevolverCasing : S7_CasingBase {
    seeSound "Weapons/casing"
    scale .2

    states {
    Spawn:
        CAS2 ABCDEFGH 2 noDelay A_SpawnItemEx ("S7_CasingSmoke")
        loop
    }
}

//-------------------------------------------------------------------------------------------
//
// Rifle Casing
//
//-------------------------------------------------------------------------------------------
actor S7_RifleCasingSpawn : S7_CasingSpawnerBase {
    states {
    Spawn:
        TNT1 A 0 noDelay A_CustomMissile ("S7_RifleCasing", -5, 0, random [sfx] (-80, -100), 2, random [sfx] (45, 80))
        stop
    }
}

actor S7_RifleCasing : S7_CasingBase {
    seeSound "Weapons/casing"
    scale .2

    states {
    Spawn:
        CAS3 ABCDEFGH 2 noDelay A_SpawnItemEx ("S7_CasingSmoke")
        loop
    }
}