/* Based on Zombie Torrent's casings code (I don't know who made the code for those casings)
** Casing sprites by Captain J. Edited by Chronos "phantombeta" Ouroboros
** Usage:
**  Weapons:  A_FireCustomMissile ("<Casing spawner actor>", 0, 0, <int xPos>, <float yPos>)
**  Monsters: A_CustomMissile ("<Casing spawner actor>", <float yPos/zPos>, <int xPos>, 0.0, CMF_AbsolutePitch | CMF_AimDirection, 0.0)
*/

actor S7_CasingSmoke : S7_ShotSmokeTrail {
    scale 0.05
    alpha 0.35

    states {
    Spawn:
        SMOK GHIJKLMNOPQ 1
        stop
    }
}

actor S7_CasingSpawnerBase {
    speed 20
    projectile

    +noClip
}

actor S7_CasingBase {
    height 2
    radius 2
    speed 6
    bounceType "doom"

    +windThrust +clientsideOnly +ghost +canBounceWater
    +moveWithSector +missile +noBlockmap +noTeleport
    +forceXYBillboard
    -dropOff -noGravity

    var int user_Timer;
    var int user_StayTime;

    states {
    Delaying:
        "----" A 0 A_JumpIf (user_Timer % 5 != 0, 2) // Only update user_StayTime every 5 tics. This way it's less performance intensive.
        "----" A 0 A_SetUserVar ("user_StayTime", callACS ("S7_GetEffectsStayTime"))
        "----" A 0 A_JumpIf ((user_StayTime == -2 && user_Timer >= 10000) ||
                             (user_StayTime == -1 && user_Timer >=  5000) ||
                             (user_StayTime ==  0 && user_Timer >=  2500) ||
                             (user_StayTime ==  1 && user_Timer >=  1250) ||
                             (user_StayTime ==  2 && user_Timer >=   625) ||
                             (user_StayTime ==  3 && user_Timer >=   313) ||
                             (user_StayTime ==  4 && user_Timer >=   157) ||
                             (user_StayTime ==  5 && user_Timer >=    70) ||
                             (user_StayTime ==  6 && user_Timer >=    35), "Fade")
        "----" A 1 A_SetUserVar ("user_Timer", user_Timer + 1)
        loop

    Fade:
    Fade.Looper:
        "----" A 0 A_JumpIf (callACS ("S7_GetEffectsFadeSpeed") == 1, "Fade.Faster")
        "----" A 0 A_JumpIf (callACS ("S7_GetEffectsFadeSpeed") == 2, "Fade.EvenFaster")
        "----" A 0 A_JumpIf (callACS ("S7_GetEffectsFadeSpeed") == 3, "Fade.FastAsFuck")
        "----" A 0 A_JumpIf (callACS ("S7_GetEffectsFadeSpeed") == 4, "Fade.TheFlash")
        "----" A 0 A_JumpIf (callACS ("S7_GetEffectsFadeSpeed") == 5, "Fade.DearLordGoBuyANewComputerRightMotherfuckingNow")
        goto Fade.Normal

    Fade.Normal:
        "----" A 3 A_FadeOut (0.03)
        goto Fade.Looper
    Fade.Faster:
        "----" A 3 A_FadeOut (0.06)
        goto Fade.Looper
    Fade.EvenFaster:
        "----" A 2 A_FadeOut (0.09)
        goto Fade.Looper
    Fade.Fasterer:
        "----" A 1 A_FadeOut (0.12)
        goto Fade.Looper
    Fade.TheFlash:
        "----" A 1 A_FadeOut (0.15)
        goto Fade.Looper
    Fade.DearLordGoBuyANewComputerRightMotherfuckingNow:
        "----" A 1 A_FadeOut (2.0)
        goto Fade.Looper

    Rest1:
        "####" A 0
        "----" A 1 A_Jump (256, "Delaying")
        wait
    Rest2:
        "####" B 0
        "----" A 1 A_Jump (256, "Delaying")
        wait
    Rest3:
        "####" C 0
        "----" A 1 A_Jump (256, "Delaying")
        wait
    Rest4:
        "####" D 0
        "----" A 1 A_Jump (256, "Delaying")
        wait
    Rest5:
        "####" E 0
        "----" A 1 A_Jump (256, "Delaying")
        wait
    Rest6:
        "####" F 0
        "----" A 1 A_Jump (256, "Delaying")
        wait
    Rest7:
        "####" G 0
        "----" A 1 A_Jump (256, "Delaying")
        wait
    Rest8:
        "####" H 0
        "----" A 1 A_Jump (256, "Delaying")
        wait


    Death:
        "----" A 1 A_Jump (256, "Rest1", "Rest3", "Rest5", "Rest7")
        goto Rest1
    }
}

//-------------------------------------------------------------------------------------------
//
// 20ga Shotgun Shell
//
//-------------------------------------------------------------------------------------------
actor S7_ShellCasingSpawn : S7_CasingSpawnerBase {
    states {
    Spawn:
        TNT1 A 0 noDelay A_CustomMissile ("S7_ShellCasing", 0, 0, fRandom [sfx] (80, 100), 2, fRandom [sfx] (-40, -60))
        stop
    }
}

actor S7_ShellCasing : S7_CasingBase {
    bounceSound "Weapons/Shell"
    scale .2

    states {
    Spawn:
        CAS0 ABCDEFGH 2 // A_SpawnItemEx ("S7_CasingSmoke")
        loop
    }
}

//-------------------------------------------------------------------------------------------
//
// 12ga Shotgun Shell
//
//-------------------------------------------------------------------------------------------
actor S7_12gaShellCasingSpawn : S7_CasingSpawnerBase {
    states {
    Spawn:
        TNT1 A 0 noDelay A_CustomMissile ("S7_12gaShellCasing", 0, 0, -110, 2, 50)
        stop
    }
}

actor S7_12gaShellCasing : S7_CasingBase {
    bounceSound "Weapons/Shell"
    scale .2

    states {
    Spawn:
        CAS4 FFGGHABCDEFGH 2
    Idle:
        CAS4 ABCDEFGH 2 // A_SpawnItemEx ("S7_CasingSmoke")
        loop
    }
}

//-------------------------------------------------------------------------------------------
//
// 9mm Casing
//
//-------------------------------------------------------------------------------------------
actor S7_9mmCasingSpawn : S7_CasingSpawnerBase {
    states {
    Spawn:
        TNT1 A 0 noDelay A_CustomMissile ("S7_9mmCasing", -5, 0, fRandom [sfx] (-80, -100), 2, fRandom [sfx] (45, 80))
        stop
    }
}

actor S7_9mmCasing : S7_CasingBase {
    bounceSound "Weapons/Brass"
    scale .2

    states {
    Spawn:
        CAS1 ABCDEFGH 2 // noDelay A_SpawnItemEx ("S7_CasingSmoke")
        loop
    }
}

//-------------------------------------------------------------------------------------------
//
// .45 ACP Casing
//
//-------------------------------------------------------------------------------------------
actor S7_45ACPCasingSpawn : S7_9mmCasingSpawn {
    states {
    Spawn:
        TNT1 A 0 noDelay A_CustomMissile ("S7_45ACPCasing", -5, 0, fRandom [sfx] (-80, -100), 2, fRandom [sfx] (45, 80))
        stop
    }
}

actor S7_45ACPCasing : S7_9mmCasing { scale .3 }

//-------------------------------------------------------------------------------------------
//
// Revolver Casing
//
//-------------------------------------------------------------------------------------------
actor S7_RevolverCasingSpawn : S7_CasingSpawnerBase {
    states {
    Spawn:
        TNT1 A 0 noDelay A_CustomMissile ("S7_RevolverCasing", -5, 0, 0, 2, -90)
        stop
    }
}

actor S7_RevolverCasing : S7_CasingBase {
    bounceSound "Weapons/Brass"
    scale .2

    states {
    Spawn:
        CAS2 ABCDEFGH 2 // noDelay A_SpawnItemEx ("S7_CasingSmoke")
        loop
    }
}

//-------------------------------------------------------------------------------------------
//
// Rifle Casing
//
//-------------------------------------------------------------------------------------------
actor S7_RifleCasingSpawn : S7_CasingSpawnerBase {
    states {
    Spawn:
        TNT1 A 0 noDelay A_CustomMissile ("S7_RifleCasing", -5, 0, fRandom [sfx] (-80, -100), 2, fRandom [sfx] (45, 80))
        stop
    }
}

actor S7_RifleCasing : S7_CasingBase {
    bounceSound "Weapons/Brass"
    scale .2

    states {
    Spawn:
        CAS3 ABCDEFGH 2 // noDelay A_SpawnItemEx ("S7_CasingSmoke")
        loop
    }
}

//-------------------------------------------------------------------------------------------
//
// .45 WW Casing
//
//-------------------------------------------------------------------------------------------
actor S7_45WWCasingSpawn : S7_CasingSpawnerBase {
    states {
    Spawn:
        TNT1 A 0 noDelay A_CustomMissile ("S7_45WWCasing", -5, 0, fRandom [sfx] (-80, -100), 2, fRandom [sfx] (45, 80))
        stop
    }
}
actor S7_45WWCasingSpawnRifle : S7_CasingSpawnerBase {
    states {
    Spawn:
        TNT1 A 0 noDelay A_CustomMissile ("S7_45WWCasing", -5, 0, fRandom [sfx] (-80, -100), 2, fRandom [sfx] (45, 55))
        stop
    }
}

actor S7_45WWCasing : S7_CasingBase {
    bounceSound "Weapons/Brass"
    scale .25

    states {
    Spawn:
        CAS5 ABCDEFGH 2 // noDelay A_SpawnItemEx ("S7_CasingSmoke")
        loop
    }
}