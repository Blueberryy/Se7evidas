//-------------------------------------------------------------------------------------------
//
// Smoke
//
//-------------------------------------------------------------------------------------------
/** White **/
// By Jekyll Grim Payne/zer0, edited by Chronos "phantombeta" Ouroboros
actor S7_WhiteSmoke {
    renderStyle add
    alpha 0.3
    scale 0.1

    +noInteraction +forceXYBillboard +clientsideOnly

    states {
    Spawn:
        SMOK ABCDEFGHIJKLMNOPQ 1
        stop
    }
}

/** Black **/
// By Jekyll Grim Payne/zer0, edited by Chronos "phantombeta" Ouroboros
actor S7_BlackSmoke : S7_WhiteSmoke {
    renderStyle translucent
    alpha 0.15

    states {
    Spawn:
        SMOK ABCDEFGHIJKLMNOPQ 1
        stop
    }
}

/** Shot smoke (white) **/
// By Jekyll Grim Payne/zer0, edited by Chronos "phantombeta" Ouroboros
actor S7_ShotSmoke {
    vSpeed 2
    renderStyle add
    alpha 0.3
    scale 0.1

    +noInteraction +forceXYBillboard +clientsideOnly

    states {
    Spawn:
        SMOK ABCDEFGHIJKLMNOPQ 1 noDelay A_SpawnItem ("S7_ShotSmokeTrail")
        stop
    }
}

actor S7_ShotSmokeTrail : S7_ShotSmoke {
    vSpeed 0
    alpha 0.15

    states {
    Spawn:
        SMOK CDEFGHIJKLMNOPQ 1
        stop
    }
}

actor S7_ShotSmoke_Spawner { // By wildweasel, edited by Chronos "phantombeta" Ouroboros
    radius 1
    height 1
    speed 10
    damage 0
    projectile

    +thruactors +clientsideOnly

    states {
    Spawn:
        TNT1 A 1
        TNT1 A 0 A_Die
        TNT1 A 1 A_Jump (256, "Death")
        wait
    Death:
        TNT1 A 0 A_CustomMissile ("S7_ShotSmoke", -2, 0, 0)
        stop
    }
}

/** Shot smoke (Black) **/
// By Jekyll Grim Payne/zer0, edited by Chronos "phantombeta" Ouroboros
actor S7_BlackShotSmoke : S7_ShotSmoke {
    renderStyle translucent
    alpha 0.15

    states {
    Spawn:
        SMOK ABCDEFGHIJKLMNOPQ 1 noDelay A_SpawnItem ("S7_BlackShotSmokeTrail")
        stop
    }
}

actor S7_BlackShotSmokeTrail : S7_ShotSmokeTrail {
    renderStyle translucent
    alpha 0.08
}

actor S7_BlackShotSmoke_Spawner : S7_ShotSmoke_Spawner {
    states {
    Death:
        TNT1 A 0 A_CustomMissile ("S7_BlackShotSmoke", -2, 0, 0)
        stop
    }
}

/** Smoking piece **/
// By Jekyll Grim Payne/zer0, edited by Chronos "phantombeta" Ouroboros
actor S7_SmokingPiece : S7_DebrisGeneral { // Edited by Chronos "phantombeta" Ouroboros
    speed 12
    gravity 0.7

    -noGravity +clientsideOnly

    states {
    Spawn:
        TNT1 A 0 noDelay A_JumpIf (waterLevel >= 1, "Water")
        TNT1 A 1
        TNT1 A 1 A_SpawnItem ("S7_SmokingPieceSmoke")
        loop
    Water:
        TNT1 A 1
        stop
    }
}

actor S7_SmokingPieceSmoke : S7_ShotSmokeTrail {
    scale 0.06
    alpha 0.5
}

/** Shot smoke 2 **/
// DECORATE by Chronos "phantombeta" Ouroboros. I dunno who made the sprites.
actor S7_ShotSmoke_2_Spawner : S7_ShotSmoke_Spawner {
    states {
    Death:
        TNT1 A 0 A_CustomMissile ("S7_ShotSmoke_2", -2, 0, 0)
        stop
    }
}

actor S7_ShotSmoke_2 {
    renderStyle add
    alpha 0.3
    scale 0.1

    +noInteraction +forceXYBillboard +clientsideOnly
    
    states {
    Spawn:
        SMKS ABCDEFGHIJKLMNOPQRSTUVW 1
        stop
    }
}

/** Heat smoke/vapour/whatever **/
// By Jekyll Grim Payne/zer0, modified (a lot) by Chronos "phantombeta" Ouroboros
actor S7_HeatSmoke : S7_ShotSmoke {
    scale 0.08
    alpha 0.15

    states {
    Spawn:
        SMOK EFGHIJKLMNOPQ 1 noDelay A_SpawnItem ("S7_HeatSmokeTrail")
        stop
    }
}

actor S7_HeatSmokeTrail : S7_HeatSmoke {
    vSpeed 0
    alpha 0.075

    states {
    Spawn:
        SMOK HIJKLMNOPQ 1
        stop
    }
}

actor S7_HeatSmokeSpawner : S7_DebrisGeneral {
    speed 20

    +noClip

    states {
    Spawn:
        TNT1 A 0 noDelay A_SpawnItem ("S7_HeatSmoke", 0, 0)
        stop
    }
}

actor S7_HeatSmokeStatic : S7_HeatSmoke {
    vSpeed 0

    states {
    Spawn:
        SMOK EFGHIJKLMNOPQ 1
        stop
    }
}