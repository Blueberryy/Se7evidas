//-------------------------------------------------------------------------------------------
//
// Shrapnel
//
//-------------------------------------------------------------------------------------------
actor S7_Shrapnel : fastProjectile { // By Chronos "phantombeta" Ouroboros. (Heavily) Based on Hideous Destructor's shrapnel actor
    radius 1
    height 1
    speed 1
    scale .5
    damage (0)
    damageType "Shrapnel"
    obituary "$OB_SHRAPNEL"

    +cannotPush +noExtremeDeath +bloodSplatter +noDamageThrust
    +forceXYBillboard

    var int user_damage;

    states {
    Spawn:
        TNT1 A 0 noDelay A_RearrangePointers (AAPTR_Null, AAPTR_Target, AAPTR_Default)
        TNT1 A 0 A_SetMass (random (100, 400))
        TNT1 A 0 A_JumpIf (z - floorZ >= 0 && z - floorZ < 8, "Spawn.OnFloor")
        TNT1 A 0 A_JumpIf (ceilingZ - z >= 0 && ceilingZ - z < 8, "Spawn.OnCeiling")
        TNT1 A 0 A_SetPitch (fRandom (-90, 90))
        goto Spawn.Rest
    Spawn.OnFloor:
        TNT1 A 0 A_SetPitch (fRandom (0, 90))
        goto Spawn.Rest
    Spawn.OnCeiling:
        TNT1 A 0 A_SetPitch (fRandom (0, -90))
        goto Spawn.Rest
    Spawn.Rest:
        TNT1 A 0 A_ChangeVelocity (cos (pitch) * mass, 0.0, sin (pitch) * mass, CVF_Relative)
        TNT1 A 0 A_SetUserVar ("user_damage", sqrt (sqrt (velX*velX + velY*velY) + velZ*velZ) >> 4)
        TNT1 AAAA 0 A_Jump (256, "SpawnA", "SpawnB", "SpawnC", "SpawnD")
        TNT1 A 1 A_Jump (256, "SpawnA", "SpawnB", "SpawnC", "SpawnD")
        wait
    SpawnA:
        FRG2 A 0
        goto Flight
    SpawnB:
        FRG2 B 0
        goto Flight
    SpawnC:
        FRG2 C 0
        goto Flight
    SpawnD:
        FRG2 D 0
        goto Flight
    Flight:
        "####" "#" 0 A_ChangeVelocity (0, 0, -1)
        "####" "#" 1 A_CustomMissile ("S7_ShrapnelSmoke", 0, 0, 0)
        "####" "#" 0 A_ChangeVelocity (velX * 0.9, velY * 0.9, velZ - 1, CVF_Replace)
        "####" "#" 1 A_CustomMissile ("S7_ShrapnelSmoke", 0, 0, 0)
        "####" "#" 0 A_ChangeVelocity (velX * 0.9, velY * 0.9, velZ - 1, CVF_Replace)
        "####" "#" 1 A_CustomMissile ("S7_ShrapnelSmoke", 0, 0, 0)
        "####" "#" 0 A_ChangeVelocity (velX * 0.9, velY * 0.9, velZ - 1, CVF_Replace)
        "####" "#" 1 A_CustomMissile ("S7_ShrapnelSmoke", 0, 0, 0)
    FlightActual:
        "####" "#" 0 A_ChangeVelocity (velX * 0.9 + fRandom (-1, 1), velY * 0.9 + fRandom (-1, 1), velZ - 1, CVF_Replace)
        "####" "#" 1 A_CustomMissile ("S7_ShrapnelSmoke", 0, 0, 0)
        "####" "#" 0 A_ChangeVelocity (velX * 0.9 + fRandom (-1, 1), velY * 0.9 + fRandom (-1, 1), velZ - 1, CVF_Replace)
        "####" "#" 1 A_CustomMissile ("S7_ShrapnelSmoke", 0, 0, 0)
        "####" "#" 0 A_SetUserVar ("user_damage", user_damage * 0.9)
        loop

    Death:
        "####" "#" 0 A_RearrangePointers (AAPTR_Master, AAPTR_Null, AAPTR_Default)
        "####" "#" 0 A_JumpIf (user_damage > 0, 2)
        "####" "#" 0 A_SetUserVar ("user_damage", random (3, 14))
        "####" "#" 1 A_Explode (user_damage, 5, XF_HURTSOURCE, FALSE, 5)
        stop
    }
}
/*
Properties:
    No direct damage
    +cannotpush flag?

Actions:
    Init {
        mass = 100~400;
        pitch = -90.0~90.0; // Ignore this so we can make the spawner set the pitch range
        velocity = x: cos (pitch) * mass, y: 0, z: sin (pitch) * mass;
        damage = sqrt (sqrt (velX*velX + velY*velY) + (velZ*velZ)) >> 4;
    }
    FlightStart {
        (1, 1x) velocity = z: -= 1;
        (1, 3x) velocity = xy: *= 0.9, z: -= 1;
    }
    FlightActual {
        (1, 2x) velocity = xy: *= 0.9 + -1.0~1.0, z: -= 1;
        (0, 1x) damage *= 0.9
    }
    Death {
        if damage <= 0:
            (0, 1x) damage = 3~14;
        (0, 1x) RadiusDamage: radius = 4, damage = damage, maxDMGRadius = 4;
    }
*/

actor S7_ShrapnelSmoke : S7_ShotSmokeTrail {
    scale 0.06
    alpha 0.5

    states {
    Spawn:
        SMOK DEFGHIJKLMNOPQ 1
        stop
    }
}

/** Spawners **/
actor S7_ShrapnelSpawner4x {
    height 1
    radius 1

    +missile +noBlockmap +noclip +nointeraction
    +thruActors

    states {
    Spawn:
        TNT1 A 0
        TNT1 AAAAA 0 A_SpawnItemEx ("S7_Shrapnel", 0.0, 0.0, 2.0, 0.0, 0.0, 0.0, fRandom (0.0, 359.0), SXF_NoCheckPosition | SXF_TransferPointers)
        stop
    }
}

actor S7_ShrapnelSpawner8x : S7_ShrapnelSpawner4x {
    states {
    Spawn:
        TNT1 A 0
        TNT1 AAAAAAAA 0 A_SpawnItemEx ("S7_Shrapnel", 0.0, 0.0, 2.0, 0.0, 0.0, 0.0, fRandom (0.0, 359.0), SXF_NoCheckPosition | SXF_TransferPointers)
        stop
    }
}

actor S7_ShrapnelSpawner10x : S7_ShrapnelSpawner4x {
    states {
    Spawn:
        TNT1 A 0
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx ("S7_Shrapnel", 0.0, 0.0, 2.0, 0.0, 0.0, 0.0, fRandom (0.0, 359.0), SXF_NoCheckPosition | SXF_TransferPointers)
        stop
    }
}

actor S7_ShrapnelSpawner16x : S7_ShrapnelSpawner4x {
    states {
    Spawn:
        TNT1 A 0
        TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("S7_Shrapnel", 0.0, 0.0, 2.0, 0.0, 0.0, 0.0, fRandom (0.0, 359.0), SXF_NoCheckPosition | SXF_TransferPointers)
        stop
    }
}

actor S7_ShrapnelSpawner50x : S7_ShrapnelSpawner4x {
    states {
    Spawn:
        TNT1 A 0
        TNT1 AAAAA 0 A_SpawnItemEx ("S7_ShrapnelSpawner10x", 0.0, 0.0, 2.0, 0.0, 0.0, 0.0, 0.0, SXF_NoCheckPosition | SXF_TransferPointers)
        stop
    }
}

actor S7_ShrapnelSpawner100x : S7_ShrapnelSpawner4x {
    states {
    Spawn:
        TNT1 A 0
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx ("S7_ShrapnelSpawner10x", 0.0, 0.0, 2.0, 0.0, 0.0, 0.0, 0.0, SXF_NoCheckPosition | SXF_TransferPointers)
        stop
    }
}