//-------------------------------------------------------------------------------------------
//
// Melee type tokens
//
//-------------------------------------------------------------------------------------------
/* Credits:
** Sprites: Apocalyptic Studio, Captain J, Chronos "phantombeta" Ouroboros
** Animation: Chronos "phantombeta" Ouroboros
** Sounds: Credits in the SNDINFO file
*/
actor S7_DaggerMelee  : boolean { }

/* Credits:
** Sprites: Id Software, ???, Chronos "phantombeta" Ouroboros
** Animation: Chronos "phantombeta" Ouroboros
** Sounds: Credits in the SNDINFO file
*/
actor S7_PunchMelee : boolean { }

/* Credits:
** Sprites: ???, Chronos "phantombeta" Ouroboros
** Animation: Chronos "phantombeta" Ouroboros
** Sounds: Credits in the SNDINFO file
*/
actor S7_KickMelee    : boolean { }

//-------------------------------------------------------------------------------------------
//
// Base weapon
//
//-------------------------------------------------------------------------------------------
actor S7_BaseWeapon : weapon {
    inventory.pickupSound "misc/gunpickup"
    weapon.bobRangeX 0.3
	weapon.bobRangeY 0.5
	weapon.bobSpeed 2.0
    weapon.bobStyle inverseSmooth
    weapon.kickback 100

    +inventory.undroppable
    +weapon.ammo_Optional +weapon.noAutoFire +weapon.noAlert

    states {
    DryFire:
        TNT1 A 0 A_PlaySound ("Weapons/DryFire", CHAN_Weapon)
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "Reload")
        TNT1 A 0 A_Jump (256, "Ready2")
        wait

    Ready:
        TNT1 A 1 A_WeaponReady
        loop
    Fire:
        TNT1 A 1
        goto Ready

    Select:
        TNT1 A 0 A_Jump (256, "TrueSelect")
        wait
    TrueSelect:
        TNT1 A 0 A_Raise
        wait
    Deselect:
        TNT1 A 0 A_TakeInventory ("S7_HoldingZoom", 0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory ("S7_ZoomHoldTime", 0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory ("S7_ReadySequence", 0x7FFFFFFF)
        TNT1 A 0 A_ZoomFactor (1.0)
        TNT1 A 0 A_JumpIfHealthLower (1, "Deselect.DEAD")
        TNT1 A 0 A_Jump (256, "TrueDeselect")
        wait
    TrueDeselect:
        TNT1 A 0 A_Lower
        wait
    Deselect.DEAD:
        TNT1 A 1 A_Lower
        wait

    QuickMelee:
        TNT1 A 0 A_TakeInventory ("S7_DoMelee", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIfInventory ("S7_DaggerMelee", 1, "DaggerMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_PunchMelee", 1, "PunchMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_KickMelee", 1, "KickMelee")
        TNT1 A 0 A_Jump (256, "KickMelee")
        wait

    DaggerMelee:
        TNT1 A 1 A_Jump (256, "DaggerMelee1", "DaggerMelee2")
        wait
    DaggerMelee1:
        DAG1 ABCD 1
        DAG1 E    1 A_CustomPunch (callACS ("S7_MeleeDamage", 3, random [weaponDamage] (1, 5)), 1, 0, "S7_DaggerPuffSilent")
        DAG1 F    1 A_CustomPunch (callACS ("S7_MeleeDamage", 3, random [weaponDamage] (1, 5)), 1, 0, "S7_DaggerPuff")
        DAG1 G    1 A_CustomPunch (callACS ("S7_MeleeDamage", 3, random [weaponDamage] (1, 5)), 1, 0, "S7_DaggerPuffSilent")
        DAG1 H    1
        TNT1 A    5
        TNT1 A    0 A_Jump (256, "MeleeDone")
        wait
    DaggerMelee2:
        DAG2 AB  1
        DAG2 C   1 A_CustomPunch (callACS ("S7_MeleeDamage", 2, random [weaponDamage] (1, 5)), 1, 0, "S7_DaggerPuffSilent")
        DAG2 D   1 A_CustomPunch (callACS ("S7_MeleeDamage", 2, random [weaponDamage] (1, 5)), 1, 0, "S7_DaggerPuff")
        DAG2 EF  1 A_CustomPunch (callACS ("S7_MeleeDamage", 2, random [weaponDamage] (1, 5)), 1, 0, "S7_DaggerPuffSilent")
        DAG2 GH  1
        TNT1 A   5
        TNT1 A   0 A_Jump (256, "MeleeDone")
        wait

    PunchMelee:
        TNT1 A 0
        TNT1 A 0 A_Jump (256, "MeleeDone")
        wait

    KickMelee:
        KICK BCD 1
        TNT1 A   0 A_Jump (256, "KickMelee.Mid")
        wait
    KickMelee.Mid:
        TNT1 A 0 A_CustomPunch (callACS ("S7_MeleeDamage", 13, random [weaponDamage] (1, 5)), 1, 0, "S7_MeleePuff")
        TNT1 A 0 A_Jump (256, "KickMelee.End")
        wait
    KickMelee.End:
        KICK HHHHIGFEDCBA 1
        TNT1 A            0 A_Jump (256, "MeleeDone")
        wait

    MeleeDone:
        TNT1 A 0 A_Jump (256, "Ready")
        wait
    }
}

//-------------------------------------------------------------------------------------------
//
// Melee puffs
//
//-------------------------------------------------------------------------------------------
actor S7_MeleePuff { // By Jekyll Grim Payne/zer0, edited by Chronos "phantombeta" Ouroboros
    damageType Kick
    attackSound "Weapons/Kick/HitWall"
    activeSound "Weapons/Kick/Swing"

    +noBlockmap +noGravity +puffOnActors +noExtremeDeath
    -allowParticles
}

actor S7_MeleePuffStrong : S7_MeleePuff { // Ditto
    decal FistScorch
    attackSound "Weapons/Kick/Strong"
    activeSound "Weapons/Kick/Swing"

    +extremeDeath
    -noExtremeDeath

    states {
    Spawn:
        TNT1 A 0 noDelay A_SpawnItem ("S7_ShotSmoke")
        stop
    }
}

actor S7_DaggerPuff : S7_MeleePuff {
    damageType S7_Blade
    attackSound "Weapons/Tridagger/HitWall"
    activeSound "Weapons/Tridagger/Swing"
}
actor S7_DaggerPuffSilent : S7_DaggerPuff {
    attackSound ""
    activeSound ""
}

//-------------------------------------------------------------------------------------------
//
// Synth-Fire stuff
//
//-------------------------------------------------------------------------------------------
actor S7_SynthFireActive         : boolean { }
actor S7_SynthFireLeft           : boolean { }
actor S7_SynthFireRight          : boolean { }
actor S7_SynthFireRightReloading : boolean { }
actor S7_SynthFireLeftReloading  : boolean { }