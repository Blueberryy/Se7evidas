//-------------------------------------------------------------------------------------------
//
// Melee type tokens
//
//-------------------------------------------------------------------------------------------
actor S7_DaggerMelee  : boolean { } // Credits: Sprites: Apocalyptic Studio, Captain J, Chronos "phantombeta" Ouroboros
actor S7_Dagger2Melee : boolean { } // Credits: Sprites: Apocalyptic Studio, Captain J, Chronos "phantombeta" Ouroboros
actor S7_KickMelee    : boolean { } // Credits: Sprites: ???, Chronos "phantombeta" Ouroboros

//-------------------------------------------------------------------------------------------
//
// Base weapon
//
//-------------------------------------------------------------------------------------------
actor S7_BaseWeapon : weapon {
    inventory.pickupSound "misc/gunpickup"
    weapon.bobRangeX 0.3
	weapon.bobRangeY 0.5
	weapon.bobSpeed 2.0
    weapon.bobStyle inverseSmooth
    weapon.kickback 100

    +inventory.undroppable
    +weapon.ammo_Optional +weapon.noAutoFire +weapon.noAlert

    states {
    DryFire:
        TNT1 A 0 A_PlaySound ("Weapons/DryFire", CHAN_Weapon)
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "Reload")
        TNT1 A 0 A_Jump (256, "Ready2")
        wait
        
    Ready:
        TNT1 A 1 A_WeaponReady
        loop
    Fire:
        TNT1 A 1
        goto Ready
    
    Select:
        TNT1 A 0 A_Jump (256, "TrueSelect")
        wait
    TrueSelect:
        TNT1 A 0 A_Raise
        wait
    Deselect:
        TNT1 A 0 A_JumpIfHealthLower (1, "Deselect.DEAD")
        TNT1 A 0 A_Jump (256, "TrueDeselect")
        wait
    TrueDeselect:
        TNT1 A 0 A_Lower
        wait
    Deselect.DEAD:
        TNT1 A 1 A_Lower
        wait

    QuickMelee:
        TNT1 A 0 A_TakeInventory ("S7_DoMelee", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_DaggerMelee", 1, "DaggerMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_Dagger2Melee", 1, "Dagger2Melee")
        TNT1 A 0 A_JumpIfInventory ("S7_KickMelee", 1, "KickMelee")
        TNT1 A 1 A_Jump (256, "KickMelee")
        wait

    DaggerMelee:
        DAG1 AB 1
    DaggerMelee.Mid:
        TNT1 A 0 A_JumpIfInventory ("PowerStrength", 1, "DaggerMelee.Berserk")
        TNT1 A 0 A_CustomPunch (random [weaponDamage] (1, 5) * 10, 1, 0, "S7_MeleePuff")
        goto DaggerMelee.End
    DaggerMelee.Berserk:
        TNT1 A 0 A_CustomPunch (3 * random [weaponDamage] (1, 5) * 10, 1, 0, "S7_MeleePuff")
        goto DaggerMelee.End
    DaggerMelee.End:
        DAG1 CDE 1
        goto MeleeDone
    
    Dagger2Melee:
        goto MeleeDone
    
    KickMelee:
        KICK BCD 1
    KickMelee.Mid:
        TNT1 A 0 A_JumpIfInventory ("PowerStrength", 1, "KickMelee.Berserk")
        TNT1 A 0 A_CustomPunch (random [weaponDamage] (1, 5) * 13, 1, 0, "S7_MeleePuff")
        goto KickMelee.End
    KickMelee.Berserk:
        TNT1 A 0 A_CustomPunch (3 * random [weaponDamage] (1, 5) * 13, 1, 0, "S7_MeleePuffStrong")
        goto KickMelee.End
    KickMelee.End:
        KICK H 4
        KICK IGFEDCBA 1
        goto MeleeDone

    MeleeDone:
        TNT1 A 1 A_Jump (256, "Ready")
        wait
    }
}

//-------------------------------------------------------------------------------------------
//
// Melee puffs
//
//-------------------------------------------------------------------------------------------
// By Jekyll Grim Payne/zer0, edited by Chronos "phantombeta" Ouroboros
actor S7_MeleePuff {
    damageType Kick
    attackSound "Weapons/Kick/HitWall"
    activeSound "Weapons/Kick/Swing"

    +noBlockmap +noGravity +puffOnActors +noExtremeDeath
    -allowParticles
}

actor S7_MeleePuffStrong : S7_MeleePuff {
    decal FistScorch
    attackSound "Weapons/Kick/Strong"
    activeSound "Weapons/Kick/Swing"

    +extremeDeath
    -noExtremeDeath
    
    states {
    Spawn:
        TNT1 A 0 noDelay A_SpawnItem ("S7_ShotSmoke")
        stop
    }
}

//-------------------------------------------------------------------------------------------
//
// Synth-Fire stuff
//
//-------------------------------------------------------------------------------------------
actor S7_SynthFireActive         : boolean { }
actor S7_SynthFireLeft           : boolean { }
actor S7_SynthFireRight          : boolean { }
actor S7_SynthFireRightReloading : boolean { }
actor S7_SynthFireLeftReloading  : boolean { }