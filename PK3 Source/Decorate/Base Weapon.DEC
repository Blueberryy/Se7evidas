Actor S7_BaseWeapon : Weapon {
	Inventory.PickupSound "misc/gunpickup"
	Weapon.Kickback 100
	Weapon.BobRangeX 0.3
	Weapon.BobRangeY 0.5
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.5
	+Weapon.Ammo_Optional +Weapon.NoAutoFire +Inventory.Undroppable
	+Weapon.NoAlert
	States {
	Kicking:
		TNT1 A 0 A_GunFlash ("KillSynthFire", GFF_NOEXTCHANGE)
		TNT1 A 0 A_ZoomFactor (1.0)
		TNT1 A 0 A_TakeInventory ("S7_Kicking", 1)
		TNT1 A 0 A_JumpIfInventory ("PowerStrength", 1, "BerserkKick")
		KICK BCD 1
		KICK H 4 A_CustomPunch (random (30, 40), 1, 0, "S7_MeleePuff")
		KICK IGFEDCBA 1
		TNT1 A 0 A_Jump (256, "Ready")
		Wait
	BerserkKick:
		KICK BCD 1
		KICK H 4 A_CustomPunch (3 * random (30, 40), 1, 0, "S7_MeleePuffStrong")
		KICK IGFEDCBA 1
		TNT1 A 0 A_Jump (256, "Ready")
		Wait
	KillSynthFire:
		TNT1 A 1
		Stop
	DryFire:
		"####" "#" 10 A_PlaySound ("Weapons/DryFire", CHAN_WEAPON)
		"####" "#" 0 A_Jump (256, "Ready2")
		Wait
	}
}

// By Jekyll Grim Payne/zer0, edited by Alex_Mercer
Actor S7_MeleePuff {
	+NoBlockmap +NoGravity +PuffOnActors -AllowParticles
	+NoExtremeDeath
	DamageType Kick
	AttackSound "Weapons/Kick/HitWall"
	ActiveSound "Weapons/Kick/Swing"
}

Actor S7_MeleePuffStrong : S7_MeleePuff {
	Decal FistScorch
	AttackSound "Weapons/Kick/Strong"
	ActiveSound "Weapons/Kick/Swing"
	-NoExtremeDeath +ExtremeDeath
	States {
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItem ("S7_ShotSmoke")
		Stop
	}
}

// SynthFire stuff
// Synth-Fire|By Xaser
Actor S7_SynthFireActive     : Boolean { }
Actor S7_SynthFireLeft       : Boolean { }
Actor S7_SynthFireRight      : Boolean { }
Actor S7_SynthFireRightReady : Boolean { }