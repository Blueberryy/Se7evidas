actor S7_BaseWeapon : weapon {
    weapon.BobRangeX 0.3
	weapon.BobRangeY 0.5
	weapon.BobStyle InverseSmooth
	weapon.BobSpeed 2.0
    inventory.PickupSound "misc/gunpickup"
    weapon.Kickback 100
    +weapon.Ammo_Optional +weapon.NoAutoFire +inventory.Undroppable
    +weapon.NoAlert
    states {
    QuickMelee:
        TNT1 A 0 a_takeInventory ("S7_DoMelee", 1)
        TNT1 A 1 acs_namedExecuteAlways ("S7_QuickMelee")
        TNT1 A 1 a_weaponReady (WRF_NoBob | WRF_NoFire)
        wait
    DryFire:
        TNT1 A 0 a_playSound ("Weapons/DryFire", CHAN_Weapon)
        TNT1 A 0 a_jumpIf (callACS ("S7_GetAutoReloading"), "Reload")
        TNT1 A 0 a_jump (256, "Ready2")
        wait
        
    Ready:
        TNT1 A 1 a_weaponReady
        loop
    Fire:
        TNT1 A 1
        goto Ready
    
    Select:
        TNT1 A 0 a_jump (256, "TrueSelect")
        wait
    TrueSelect:
        TNT1 A 0 a_raise
        wait
    Deselect:
        TNT1 A 0 a_jumpIfHealthLower (1, "Deselect.DEAD")
        TNT1 A 0 a_jump (256, "TrueDeselect")
        wait
    TrueDeselect:
        TNT1 A 0 a_lower
        wait
    Deselect.DEAD:
        TNT1 A 1 a_lower
        wait
    }
}

// By Jekyll Grim Payne/zer0, edited by Alex_Mercer
actor S7_MeleePuff {
    +noBlockmap +noGravity +puffOnActors -allowParticles
    +noExtremeDeath
    damageType Kick
    attackSound "Weapons/Kick/HitWall"
    activeSound "Weapons/Kick/Swing"
}

actor S7_MeleePuffStrong : S7_MeleePuff {
    decal FistScorch
    attackSound "Weapons/Kick/Strong"
    activeSound "Weapons/Kick/Swing"
    -noExtremeDeath +extremeDeath
    states {
    Spawn:
        TNT1 A 0
        TNT1 A 0 a_spawnItem ("S7_ShotSmoke")
        stop
    }
}

// SynthFire stuff
// Synth-Fire|By Xaser
actor S7_SynthFireActive         : boolean { }
actor S7_SynthFireLeft           : boolean { }
actor S7_SynthFireRight          : boolean { }
actor S7_SynthFireRightReloading : boolean { }
actor S7_SynthFireLeftReloading  : boolean { }