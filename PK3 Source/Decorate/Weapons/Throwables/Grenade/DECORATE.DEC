//-------------------------------------------------------------------------------------------
//
// Grenade
//
//-------------------------------------------------------------------------------------------
actor S7_GrenadeCount : ammo {
    inventory.maxAmount 15
    ammo.backpackMaxAmount 15

    +ignoreSkill
}

actor S7_GrenadeForce : counter { inventory.maxAmount 30 }
actor S7_GrenadeWeap : S7_BaseWeapon {
    tag "$GRENADEWEAP"
    weapon.ammoType "S7_GrenadeCount"
    weapon.ammoUse 0
    weapon.ammoGive 0

    states {
    Ready:
    Ready2:
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 0x7FFFFFFF)
        TNT1 A 1 A_WeaponReady
        loop

    Deselect:
        goto TrueDeselect

    Fire:
        TNT1 A      0 A_JumpIfInventory ("S7_GrenadeCount", 1, 1)
        goto Ready2
        TGRN WXY    1
        TGRN ABCDEF 1
        TGRN G      1 A_PlaySound ("Weapons/GrenadePinPull", CHAN_Weapon)
        TGRN HIJKLM 1
        TNT1 A      1
    Hold:
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "CancelThrow")
        TNT1 A 1 A_GiveInventory ("S7_GrenadeForce", 1)
        TNT1 A 1 A_Refire ("Hold")
        // Insert throw anim here
        TNT1 A 0 A_TakeInventory ("S7_GrenadeCount", 1, TIF_NoTakeInfinite)
        TNT1 A 8 A_SpawnItemEx ("S7_ThrownHandGrenade", 0, 0, height - 12,
                                cos (pitch) * cos (angle) * 4 + velX, cos (pitch) * sin (angle) * 4 + velY, -sin (pitch) * 4 + velZ, 0, SXF_AbsoluteMomentum | SXF_NoCheckPosition | SXF_TransferPitch)
        TNT1 A 0 A_ClearRefire
        goto Ready2

    CancelThrow:
        TNT1 A      0 A_TakeInventory ("S7_Reloading", 0x7FFFFFFF)
        TNT1 A      0 A_TakeInventory ("S7_GrenadeForce", 0x7FFFFFFF)
        TNT1 A      1 A_ClearRefire
        TGRN MLKJIH 1
        TGRN G      1 A_PlaySound ("Weapons/GrenadePinPull", CHAN_Weapon)
        TGRN FEDCBA 1
        TGRN YXW    1
        goto Ready2
    }
}

//-------------------------------------------------------------------------------------------
//
// Thrown grenade
//
//-------------------------------------------------------------------------------------------
actor S7_ThrownHandGrenade {
    radius 5 height 5
    speed 5 mass 30
    damage (0)
    bounceType "Doom"
    pushFactor 3
    wallBounceFactor 0.1
    bounceFactor 0.1
    scale 0.3

    projectile
    +canBounceWater +noExplodeFloor +activateMCross +forceXYBillboard
    +skyExplode +noBlood +useBounceState
    -noTeleport -noGravity -floorClip

    var int user_Fuse;
    var int user_Frame;

    states {
    Spawn:
        TNT1 A 0 noDelay A_SetUserVar ("user_Fuse", 35 * 3.5)
    Spawn2:
        TNT1 A 0 A_JumpIfInTargetInventory ("S7_GrenadeForce", 1, 1)
        goto Flight
        TNT1 A 0 A_ChangeVelocity (cos (pitch), 0, -sin (pitch), CVF_Relative)
        TNT1 A 0 A_TakeFromTarget ("S7_GrenadeForce", 1)
        loop
    Flight:
        TNT1 A 0 A_JumpIf (user_Frame == 7, "FlightH")
        TNT1 A 0 A_JumpIf (user_Frame == 6, "FlightG")
        TNT1 A 0 A_JumpIf (user_Frame == 5, "FlightF")
        TNT1 A 0 A_JumpIf (user_Frame == 4, "FlightE")
        TNT1 A 0 A_JumpIf (user_Frame == 3, "FlightD")
        TNT1 A 0 A_JumpIf (user_Frame == 2, "FlightC")
        TNT1 A 0 A_JumpIf (user_Frame == 1, "FlightB")
        goto FlightA

    FlightA: TGRP A 0
              goto Flight2
    FlightB: TGRP B 0
              goto Flight2
    FlightC: TGRP C 0
              goto Flight2
    FlightD: TGRP D 0
              goto Flight2
    FlightE: TGRP E 0
              goto Flight2
    FlightF: TGRP F 0
              goto Flight2
    FlightG: TGRP G 0
              goto Flight2
    FlightH: TGRP H 0 A_SetUserVar ("user_Frame", 0)
              goto Flight2

    Flight2:
        "----" A 0 A_JumpIf (!user_Fuse, "Death_BurnFuse")
        "----" A 2 A_SetUserVar ("user_Fuse", user_Fuse - 2)
        "----" A 0 A_SetUserVar ("user_Frame", user_Frame + 1)
        "----" A 0 A_JumpIf (abs (velx) + abs (vely) < 1, "Death_BurnFuse")
        goto Flight

    Bounce:
    Death:
        "----" A 0 A_ChangeFlag ("noBlockmap", FALSE)
        "----" A 0 A_ChangeFlag ("missile", FALSE)
        "----" A 0 A_ChangeFlag ("pushable", TRUE)
        "----" A 0 A_ChangeFlag ("solid", TRUE)
        "----" A 0 A_JumpIf (z - floorZ > 1, "Flight2")
        "----" A 0 A_ChangeVelocity (velx * 5, vely * 5, velz, CVF_Replace)
        goto Flight2

    Death_BurnFuse:
        "----" A 0 A_JumpIf (!user_Fuse, "KABOOM")
        "----" A 1 A_SetUserVar ("user_Fuse", user_Fuse - 1)
        loop
    KABOOM:
        TNT1 AAAA 0 A_Stop
        TNT1 A 0 A_ChangeFlag ("solid", FALSE)
        TNT1 A 0 A_ChangeFlag ("pushable", FALSE)
        TNT1 A 0 A_ChangeFlag ("noBlockmap", TRUE)
        TNT1 A 0 A_ChangeFlag ("missile", TRUE)
        TNT1 A 0 A_SetScale (1.0)
        TNT1 A 0 A_SpawnItemEx ("S7_ShrapnelSpawner100x", 0.0, 0.0, 2.0, 0.0, 0.0, 0.0, 0, SXF_NoCheckPosition | SXF_TransferPointers)
        TNT1 A 0 A_PlaySound ("Explosions/Explosion1")
    KABOOM.Anim:
        TNT1 A    0 A_SetDamageType ("ExplosionSplashDMG")
        XPL1 A    3 bright A_Explode (fRandom [weaponDamage] (192.0, 256.0), fRandom [weaponSpread] (192.0, 256.0))
        XPL1 BCDE 3 bright
        stop
    }
}