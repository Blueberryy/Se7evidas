//-------------------------------------------------------------------------------------------
//
// Avtomat Kalashnikova 47
//
//-------------------------------------------------------------------------------------------
actor S7_AK47Clip : ammo {
    inventory.maxAmount 30
    ammo.backpackMaxAmount 30

    +ignoreSkill
}

actor S7_AK47SemiAuto : boolean { }
actor S7_AK47SemiOnly : boolean { }
actor S7_AK47HalfTic  : boolean { }
actor S7_AK47ReloadChamber : boolean { }
actor S7_AK47 : S7_BaseWeapon 30012 {
    tag "$AK47"
    inventory.pickupMessage "$AK47"
    weapon.ammoType1 "S7_AK47Clip"
    weapon.ammoType2 "S7_762x39Cartridges"
    weapon.ammoUse 1
    weapon.ammoGive 0

    states {
    Spawn:
        AK4P A -1
        loop

    Ready:
        //AK47 VWXYZA 1
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_ChangeFireMode", 1, "ChangeMode")
        AK47 A 1 A_WeaponReady
        loop

    ChangeMode:
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIfInventory ("S7_AK47SemiOnly", 1, "Ready2")
        TNT1 A 0 A_PlaySound ("Weapons/ModeChange", CHAN_7)
        TNT1 A 0 A_JumpIfInventory ("S7_AK47SemiAuto", 1, "ToFullAuto")
        AK47 A 5 A_GiveInventory ("S7_AK47SemiAuto", 1)
        goto Ready2
    ToFullAuto:
        AK47 A 5 A_TakeInventory ("S7_AK47SemiAuto", 0x7FFFFFFF)
        goto Ready2

    Deselect:
        //AK47 A     1
        //AK47 ZYXWV 1
        goto Super::Deselect

    Fire:
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")

        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash ("Flash")
        TNT1 A 0 A_FireCustomMissile ("S7_AK47_Tracer", fRandom [weaponSpread] (-1.5, 1.5), 1, 5, -0.5, 0, fRandom [weaponSpread] (-1.687, 1.687))
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 4)
        TNT1 A 0 A_FireCustomMissile ("S7_RifleCasingSpawn", 0, 0, 11, -12)

        TNT1 A 0 A_JumpIfInventory ("S7_AK47HalfTic", 1, "Fire2.Sound")
        TNT1 A 0 A_PlaySound ("Weapons/AK47/Fire", CHAN_Weapon)
    Fire.Continue:
        AK47 AC 1 A_SetPitch (pitch - 0.7, SPF_Interpolate)
        AK47 B  1
        TNT1 A  0 A_JumpIfInventory ("S7_AK47HalfTic", 1, "Fire2.Sound")
        AK47 CA 1

        TNT1 A 0 A_JumpIfInventory ("S7_AK47SemiAuto", 1, "Fire.Semi")
        TNT1 A 0 A_JumpIfInventory ("S7_AK47SemiOnly", 1, "Fire.Semi")
        AK47 A 1 A_Refire ("Fire")
        goto Ready2
    Fire2.Sound:
        TNT1 A 0 A_PlaySound ("Weapons/AK47/Fire", CHAN_5)
        goto Fire.Continue
    Fire2:
        AK47 C 1 A_TakeInventory ("S7_AK47HalfTic", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIfInventory ("S7_AK47SemiAuto", 1, "Fire.Semi")
        TNT1 A 0 A_JumpIfInventory ("S7_AK47SemiOnly", 1, "Fire.Semi")
        AK47 A 1 A_Refire ("Fire")
        goto Ready2
    Fire.Semi:
        TNT1 A   0 A_TakeInventory ("S7_AK47HalfTic", 0x7FFFFFFF)
        AK47 AAA 1 A_WeaponReady (WRF_NoSecondary | WRF_NoSwitch | WRF_NoBob)
        goto Ready2

    DryFire:
        AK47 A 3 A_PlaySound ("Weapons/DryFireRifle", CHAN_7)
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "Reload")
        goto Ready2

    Reload:
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIfInventory ("S7_AK47Clip", 0, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("S7_762x39Cartridges", 1, "TrueReload.Start")
        goto Ready2
    TrueReload.Start:
        TNT1 A 0 A_TakeInventory ("S7_AK47ReloadChamber", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIfInventory ("S7_AK47Clip", 1, 2)
        TNT1 A 0 A_GiveInventory ("S7_AK47ReloadChamber", 1)
    TrueReload:
        TNT1 A 0 A_JumpIfInventory ("S7_AK47Clip", 0, "ReloadFinish")
        TNT1 A 0 A_JumpIfInventory ("S7_762x39Cartridges", 1, 1)
        goto ReloadFinish
        TNT1 A 0 A_GiveInventory ("S7_AK47Clip", 1)
        TNT1 A 0 A_TakeInventory ("S7_762x39Cartridges", 1)
        loop
    ReloadFinish:
        AK47 ADEF  2
        AK47 GHI   1
        AK47 J     3
        AK47 K     1 A_PlaySound ("Weapons/AK47/Out", CHAN_6)
        AK47 LM    1
        AK47 N    15
        AK47 O     1 A_PlaySound ("Weapons/AK47/In", CHAN_7)
        AK47 PQRS  1
        AK47 J     4
        AK47 TUVWA 1

        TNT1 A 0 A_JumpIfInventory ("S7_AK47ReloadChamber", 1, "ReloadFinish.Chamber")
        goto Reload.End
    ReloadFinish.Chamber:
        AK47 A     2
        AK42 ABCDE 2
        AK42 F     2 A_PlaySound ("Weapons/AK47/Bolt", CHAN_6)
        AK42 GH    2
        AK42 IJKLM 1
        AK47 A     2
        goto Reload.End
    Reload.End:
        AK47 A 2 A_TakeInventory ("S7_AK47ReloadChamber", 0x7FFFFFFF)
        goto Ready2

    // Muzzle flashes
    Flash:
        AK4F AB 1 bright
        stop
    }
}