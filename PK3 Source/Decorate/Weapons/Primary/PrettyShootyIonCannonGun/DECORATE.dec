actor S7_PrettyShootyIonCannonGun : S7_BaseWeapon 30001 {
    tag "$IONCANNON"
    inventory.pickupMessage "$IONCANNON"
    //weapon.ammoType1 "S7_PrettyShootyIonCannonGunMag"
    //weapon.ammoType2 "S7_Cells"
    weapon.ammoUse 0
    weapon.ammoGive 0

    states {
    Spawn:
        PIC1 Z -1
        stop
    
    Ready:
    Ready2:
        //TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 1)
        PIC1 A 1 A_WeaponReady
        loop
    
    Fire:
        TNT1 A 0 A_FireCustomMissile ("S7_IonCannonProjectile")
        TNT1 AAA 0 A_FireCustomMissile ("S7_IonCannonRail", fRandom [weaponSpread] (-3.0, 3.0), 0, 1, -5, 0, fRandom [weaponSpread] (-3.0, 3.0))
        PIC1 DDEEF 1
        PIC1 GHIJKLMNGHIJKLMNGHIJKLMNG 2
        PIC1 FEEDD 1
        goto Ready2
    }
}

actor S7_IonCannonProjectile : fastProjectile {
    speed 25
    projectile
    renderStyle add
    scale 0.25
    damage (random [weaponDamage] (1, 3) * 7)
    missileType "S7_IonCannonProjectileTrail"
    missileHeight 8
    
    +seekerMissile +screenSeeker +forceXYBillboard +ripper
    
    states {
    Spawn:
        TNT1 AA 0 A_ScaleVelocity (1.0 / (25 * 1.0))
        TNT1 A 0 A_ScaleVelocity (35 * 1.0)
    Idle:
        PICF A 1 bright
        loop
    Death:
        TNT1 A 0 A_Explode (256, 128)
        stop
    }
}

actor S7_IonCannonProjectileTrail {
    renderStyle add
    scale 0.25
    
    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +forceXYBillboard +clientsideOnly

    states {
    Spawn:
        PICF A 1 bright
        TNT1 A 0 A_ChangeVelocity (fRandom [sfx] (-3.0, 3.0), fRandom [sfx] (-3.0, 3.0), fRandom [sfx] (-3.0, 3.0), CVF_Replace | CVF_Relative)
    Loople:
        TNT1 A 0 A_FadeOut (0.08)
        //TNT1 A 0 A_SetScale (scaleX - 0.035)
        PICF A 1 bright A_JumpIf (scaleX <= 0, "Death")
        loop
    Death:
        TNT1 A 0
        stop
    }
}

actor S7_IonCannonRail : fastProjectile {
    radius 2
    height 2
    speed 25
    health 125
    projectile
    renderStyle add
    scale 0.05
    damage (random [weaponDamage] (1, 5) * 16)
    missileType "S7_IonCannonRailEffectSpawner"
    decal Bullet
    alpha 0.5

    +noDamageThrust +forceXYBillboard

    states {
    Spawn:
        TNT1 AA 0 A_ScaleVelocity (1.0 / (25 * 1.0))
        TNT1 A 0 A_ScaleVelocity (health * 1.0)
    Idle:
        PICF A 1 bright
        wait
    }
}

actor S7_IonCannonRailEffectSpawner {
    +noBlockmap +noGravity +noTeleport

    states {
    Spawn:
        TNT1 A 0 noDelay A_SpawnItemEx ("S7_IonCannonRailEffect", 0.0 + fRandom [sfx] (-2.5, 2.5), 0.0 + fRandom [sfx] (-2.5, 2.5), 8.0 + fRandom [sfx] (-2.5, 2.5), 0.0, 0.0, 0.0, 0, SXF_Clientside)
        Stop
    }
}

actor S7_IonCannonRailEffect {
    renderStyle add
    scale 0.05
    
    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +forceXYBillboard +clientsideOnly

    states {
    Spawn:
        PICF A 2 bright
        TNT1 A 0 A_ChangeVelocity (fRandom [sfx] (-3.0, 3.0), fRandom [sfx] (-3.0, 3.0), fRandom [sfx] (-3.0, 3.0), CVF_Replace | CVF_Relative)
        PICF A 1 bright A_FadeOut (0.05)
        wait
    }
}