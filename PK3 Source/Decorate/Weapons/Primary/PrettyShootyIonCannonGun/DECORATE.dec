actor S7_PrettyShootyIonCannonGun : S7_BaseWeapon 30001 {
    inventory.PickupMessage "$IONCANNON"
    tag "$IONCANNON"
    //weapon.AmmoType1 "S7_PrettyShootyIonCannonGunMag"
    //weapon.AmmoType2 "S7_Cells"
    weapon.AmmoUse 0
    weapon.AmmoGive 0
    states {
    Spawn:
        PIC1 Z -1
        stop
    
    Ready:
    Ready2:
        TNT1 A 0 a_jumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 a_jumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 a_takeInventory ("S7_ChangeFireMode", 1)
        PIC1 A 1 a_weaponReady
        loop
    
    /*Select:
        TNT1 A 0 a_raise
        wait
    
    Deselect:
        TNT1 A 0 a_jumpIfHealthLower (1, "Deselect.DEAD")
        TNT1 A 0 a_lower
        loop*/
    
    Fire:
        TNT1 A 0 a_fireCustomMissile ("S7_IonCannonProjectile")
        TNT1 AAA 0 a_fireCustomMissile ("S7_IonCannonRail", fRandom (-3.0, 3.0), 0, 1, -5, 0, fRandom (-3.0, 3.0))
        PIC1 DDEEF 1
        PIC1 GHIJKLMNGHIJKLMNGHIJKLMNG 2
        PIC1 FEEDD 1
        goto Ready2
    }
}

actor S7_IonCannonProjectile : fastProjectile {
    speed 35
    projectile
    renderStyle add
    scale 0.25
    damage (450 / random (1, 8))
    
    +SeekerMissile +ScreenSeeker
    
    states {
    Spawn:
        PICF AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 bright a_seekerMissile (5, 9, SMF_Look | SMF_Precise, 192, 5)
    Idle:
        PICF AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 bright a_seekerMissile (5, 9, SMF_Precise, 192, 5)
        goto Death
    Death:
        TNT1 A 0 a_explode (256, 128)
        stop
    }
}

actor S7_IonCannonRail : fastProjectile {
    radius 2
    height 2
    speed 25
    health 125
    projectile
    renderStyle add
    +noDamageThrust
    scale 0.05
    damage (random (1, 5) * 16)
    missileType "S7_IonCannonRailEffectSpawner"
    decal Bullet
    alpha 0.5
    states {
    Spawn:
        TNT1 AA 0 a_scaleVelocity (1.0 / (25 * 1.0))
        TNT1 A 0 a_scaleVelocity (health * 1.0)
    Idle:
        PICF A 1 bright
        wait
    }
}

actor S7_IonCannonRailEffectSpawner {
    +noBlockmap +noGravity +noTeleport
    states {
    Spawn:
        TNT1 A 0
        TNT1 A 1 a_spawnItemEx ("S7_IonCannonRailEffect", 0.0 + fRandom (-2.5, 2.5), 0.0 + fRandom (-2.5, 2.5), 8.0 + fRandom (-2.5, 2.5), 0.0, 0.0, 0.0, 0, SXF_Clientside)
        Stop
    }
}

actor S7_IonCannonRailEffect {
    renderStyle add
    scale 0.05
    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +clientsideOnly
    states {
    Spawn:
        PICF A 2 bright
        TNT1 A 0 a_changeVelocity (fRandom (-3.0, 3.0), fRandom (-3.0, 3.0), fRandom (-3.0, 3.0), CVF_Replace | CVF_Relative)
        PICF A 1 bright a_fadeOut (0.05)
        wait
    }
}