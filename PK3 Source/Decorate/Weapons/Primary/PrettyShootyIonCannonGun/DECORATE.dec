//-------------------------------------------------------------------------------------------
//
// GTX-2 "Mjolnir" Ion Cannon Mk V
//
//-------------------------------------------------------------------------------------------
actor S7_PSICG_ReadyFrame         : counter { inventory.maxAmount 5 }
actor S7_PSICG_CantFire           : boolean { }
actor S7_PrettyShootyIonCannonGun : S7_BaseWeapon 30001 {
    tag "$IONCANNON"
    inventory.pickupMessage "$IONCANNON"
    //weapon.ammoType1 "S7_PrettyShootyIonCannonGunMag"
    //weapon.ammoType2 "S7_Cells"
    weapon.ammoUse 0
    weapon.ammoGive 0

    states {
    Spawn:
        PIC1 Z -1
        stop
    
    Ready:
        TNT1 A 0 A_TakeInventory ("S7_PSICG_ReadyFrame", 0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory ("S7_PSICG_CantFire",   0x7FFFFFFF)
        goto Ready2.ChooseFrame
    Ready2:
        "----" A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        "----" A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        "----" A 0 A_TakeInventory ("S7_ChangeFireMode", 1)
        "----" A 1 A_WeaponReady
        goto Ready2.ChooseFrame

    Ready2.ChooseFrame:
        TNT1 A 0 A_JumpIfInventory ("S7_PSICG_ReadyFrame", 5, "Ready2.FrameF")
        TNT1 A 0 A_JumpIfInventory ("S7_PSICG_ReadyFrame", 4, "Ready2.FrameE")
        TNT1 A 0 A_JumpIfInventory ("S7_PSICG_ReadyFrame", 3, "Ready2.FrameD")
        TNT1 A 0 A_JumpIfInventory ("S7_PSICG_ReadyFrame", 2, "Ready2.FrameC")
        TNT1 A 0 A_JumpIfInventory ("S7_PSICG_ReadyFrame", 1, "Ready2.FrameB")
        goto Ready2.FrameA
    Ready2.FrameA:
        PIC1 A 0
        goto Ready2
    Ready2.FrameB:
        PIC1 B 0
        goto Ready2
    Ready2.FrameC:
        PIC1 C 0
        goto Ready2
    Ready2.FrameD:
        PIC1 D 0
        goto Ready2
    Ready2.FrameE:
        PIC1 E 0
        goto Ready2
    Ready2.FrameF:
        PIC1 F 0
        goto Ready2

    Reload:
        PIC1 A 5 A_TakeInventory ("S7_Reloading", 1)
        goto Ready2
    
    Fire:
        // Distance check
        TNT1 A        0 A_FireBullets (0.0, 0.0, 1, 0, "S7_PSICG_FireTesterPuff", FBF_NoRandom | FBF_NoFlash | FBF_NoRandomPuffZ, 192)
        TNT1 A        0 A_JumpIfInventory ("S7_PSICG_CantFire", 1, "CantFire")
        TNT1 A        0 A_TakeInventory ("S7_PSICG_CantFire", 0x7FFFFFFF)
        // Space check
        PIC1 A        2 A_FireCustomMissile ("S7_PSICG_FireTester", 0.0, 0, 1, 5.0)
        TNT1 A        0 A_JumpIfInventory ("S7_PSICG_CantFire", 1, "CantFire")
        TNT1 A        0 A_TakeInventory ("S7_PSICG_CantFire", 0x7FFFFFFF)
        // Fire
        TNT1 A        0 A_FireCustomMissile ("S7_IonCannonProjectile", 0.0, 0, 1, 5.0)
        TNT1 AAA      0 A_FireCustomMissile ("S7_IonCannonRail", fRandom [weaponSpread] (-3.0, 3.0), 0, 1, 5.0, 0, fRandom [weaponSpread] (-3.0, 3.0))
        TNT1 A        0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 4, 0)
        PIC1 A        1 offset (8, 40)
        PIC1 B        1 offset (6, 38)
        PIC1 C        1 offset (4, 36)
        PIC1 D        1 offset (2, 34)
        TNT1 A        0 A_WeaponReady (WRF_NoSwitch | WRF_DisableSwitch | WRF_NoFire)
        PIC1 EF       1
        PIC1 FFFFFFFF 2 A_FireCustomMissile ("S7_HeatSmokeSpawner", 0, 0, random [sfx] (4, 10), fRandom [sfx] (-8.0, -9.2))
        PIC1 FFFFFFFF 2 A_FireCustomMissile ("S7_HeatSmokeSpawner", 0, 0, random [sfx] (4, 10), fRandom [sfx] (-8.0, -9.2))
        PIC1 F        4
        PIC1 EDCBA    2
        PIC1 A        5
        goto Ready2

    CantFire:
        TNT1 A        0 A_TakeInventory ("S7_PSICG_CantFire", 0x7FFFFFFF)
        PIC1 A        1 A_PlaySound ("Weapons/PSICG/Error", CHAN_Weapon)
        goto Ready2
    }
}

//-------------------------------------------------------------------------------------------
//
// Firing checks
//
//-------------------------------------------------------------------------------------------
actor S7_PSICG_FireTesterPuff : S7_DistanceTestPuff {
    states {
    RunStuff:
        TNT1 A 2 A_GiveToTarget ("S7_PSICG_CantFire", 1)
        stop
    }
}

actor S7_PSICG_FireTester : S7_DistanceTestProjectile {
    states {
    Spawn:
        TNT1 A 1
        TNT1 A 2 A_TakeFromTarget ("S7_PSICG_CantFire", 0x7FFFFFFF)
        stop
    Death:
        TNT1 A 1 A_GiveToTarget ("S7_PSICG_CantFire", 1)
        stop
    }
}

//-------------------------------------------------------------------------------------------
//
// Main projectile
//
//-------------------------------------------------------------------------------------------
actor S7_IonCannonProjectile : fastProjectile {
    speed 20
    projectile
    renderStyle add
    scale 0.25
    damage (random [weaponDamage] (2, 5) * 86)
    missileType "S7_IonCannonProjectileTrail"
    missileHeight 8

    decal S7_IonCannon
    
    +seekerMissile +screenSeeker +forceXYBillboard +forceRadiusDMG
    +bloodlessImpact +noDamageThrust
    
    states {
    Spawn:
        TNT1 AA 0 A_ScaleVelocity (1.0 / (20 * 1.0))
        TNT1 A  0 A_ScaleVelocity (35 * 1.0)
    Idle:
        PICF A 1 bright A_Explode (random [weaponDamage] (1, 3) * 7, 96, 0, FALSE, 96)
        loop
    Death:
        TNT1 A 0 A_ChangeFlag ("forceRadiusDMG", FALSE)
        TNT1 A 0 A_Explode (256, 128)
        stop
    }
}

actor S7_IonCannonProjectileTrail {
    renderStyle add
    scale 0.25
    
    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +forceXYBillboard +clientsideOnly

    states {
    Spawn:
        PICF A 1 bright
        TNT1 A 0 A_ChangeVelocity (fRandom [sfx] (-3.0, 3.0), fRandom [sfx] (-3.0, 3.0), fRandom [sfx] (-3.0, 3.0), CVF_Replace | CVF_Relative)
    Loople:
        TNT1 A 0 A_FadeOut (0.1)
        PICF A 1 bright
        loop
    Death:
        TNT1 A 0
        stop
    }
}

//-------------------------------------------------------------------------------------------
//
// Rails
//
//-------------------------------------------------------------------------------------------
actor S7_IonCannonRail : fastProjectile {
    radius 4
    height 4
    speed 25
    health 125
    projectile
    renderStyle add
    scale 0.08
    damage (random [weaponDamage] (1, 5) * 16)
    missileType "S7_IonCannonRailEffectSpawner"
    decal S7_IonCannonRail
    alpha 1.0

    +noDamageThrust +forceXYBillboard +ripper +forceRadiusDMG
    +bloodlessImpact

    states {
    Spawn:
        TNT1 AA 0 A_ScaleVelocity (1.0 / (25 * 1.0))
        TNT1 A  0 A_ScaleVelocity (health * 1.0)
    Idle:
        PICF A 1 bright
        wait
    }
}

actor S7_IonCannonRailEffectSpawner {
    +noBlockmap +noGravity +noTeleport

    states {
    Spawn:
        TNT1 A 0 noDelay A_SpawnItemEx ("S7_IonCannonRailEffect", 0.0 + fRandom [sfx] (-2.5, 2.5), 0.0 + fRandom [sfx] (-2.5, 2.5), 8.0 + fRandom [sfx] (-2.5, 2.5), 0.0, 0.0, 0.0, 0, SXF_Clientside)
        stop
    }
}

actor S7_IonCannonRailEffect {
    renderStyle add
    scale 0.1
    
    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +forceXYBillboard +clientsideOnly

    states {
    Spawn:
        PICF A 2 bright
        TNT1 A 0 A_ChangeVelocity (fRandom [sfx] (-3.0, 3.0), fRandom [sfx] (-3.0, 3.0), fRandom [sfx] (-3.0, 3.0), CVF_Replace | CVF_Relative)
        PICF A 1 bright A_FadeOut (0.05)
        wait
    }
}