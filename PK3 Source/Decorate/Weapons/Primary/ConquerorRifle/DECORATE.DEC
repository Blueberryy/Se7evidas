//-------------------------------------------------------------------------------------------
//
// MK17 "Conqueror" Anti-Materiel Sniper Rifle
//
//-------------------------------------------------------------------------------------------
actor S7_ConquerorRifleClip : ammo {
    inventory.maxAmount 15
    ammo.backpackMaxAmount 15

    +ignoreSkill
}

actor S7_ConquerorRifleZoomed : counter { inventory.maxAmount 3 }
actor S7_ConquerorRifleJammed : boolean { }
actor S7_ConquerorRifleLoaded : boolean { }
actor S7_ConquerorRifleNoPump : boolean { }
actor S7_ConquerorRifleReloadPump : boolean { }
actor S7_ConquerorRifle : S7_BaseWeapon 30010 {
    tag "$CONQUERORSRIFLE"
    inventory.pickupMessage "$CONQUERORSRIFLE"
    weapon.ammoType1 "S7_ConquerorRifleClip"
    weapon.ammoType2 "S7_45WWCartridges"
    weapon.ammoUse 1
    weapon.ammoGive 0

    states {
    Spawn:
        CQRP A -1
        loop

    /** Normal mode **/
    Ready:
        CQR1 VWXYZA 1
    Ready2:
        CQR1 A 0 A_JumpIfInventory ("S7_ConquerorRifleZoomed", 1, "Ready2.Zoom")
        CQR1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        CQR1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        CQR1 A 0 A_JumpIfInventory ("S7_ChangeFireMode", 1, "ChangeMode")
        TNT1 A 0 A_GiveInventory ("S7_WeapReady", 1)
        CQR1 A 1 A_WeaponReady (WRF_AllowZoom)
        CQR1 A 0 A_TakeInventory ("S7_HoldingZoom", 0x7FFFFFFF)
        CQR1 A 0 A_TakeInventory ("S7_ZoomHoldTime", 0x7FFFFFFF)
        loop
    Ready2.Zoom:
        SCP1 B 0 A_JumpIfInventory ("S7_ConquerorRifleZoomed", 1, 1)
        goto Ready2
        SCP1 B 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        SCP1 B 0 A_TakeInventory ("S7_DoMelee", 0x7FFFFFFF)
        SCP1 B 0 A_JumpIfInventory ("S7_ChangeFireMode", 1, "ChangeMode")
        TNT1 A 0 A_GiveInventory ("S7_WeapReady", 1)
        SCP1 B 1 A_WeaponReady (WRF_AllowZoom | WRF_NoBob)
        SCP1 B 0 A_TakeInventory ("S7_HoldingZoom", 0x7FFFFFFF)
        SCP1 B 0 A_TakeInventory ("S7_ZoomHoldTime", 0x7FFFFFFF)
        goto Ready2

    ChangeMode:
        "----" A 0 A_PlaySound ("Weapons/ModeChange", CHAN_6)
        "----" A 0 A_TakeInventory ("S7_ChangeFireMode", 0x7FFFFFFF)
        "----" A 0 A_JumpIfInventory ("S7_ConquerorRifleNoPump", 1, "ToPumpAction")
        "----" A 5 A_GiveInventory ("S7_ConquerorRifleNoPump", 1)
        goto Ready2
    ToPumpAction:
        "----" A 5 A_TakeInventory ("S7_ConquerorRifleNoPump", 0x7FFFFFFF)
        goto Ready2

    Deselect:
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleZoomed", 1, "Deselect.Zoom")

        CQR1 A     1 A_SetCrosshair (0)
        CQR1 ZYXWV 1
        goto Super::Deselect
    Deselect.Zoom:
        TNT1 A 0 A_TakeInventory ("S7_ConquerorRifleZoomed", 0x7FFFFFFF)
        TNT1 A 0 A_SetCrosshair (0)
        TNT1 A 0 A_ZoomFactor (1.0, ZOOM_Instant)
        CQR1 A 8 A_SetBlend ("00 00 00", 1.0, 8)
        goto Ready2

    Fire:
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleZoomed", 1, "Fire.Zoomed")
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleLoaded", 1, 1)
        goto Fire.Pump
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleJammed", 1, "Fire.Jammed")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound ("Weapons/ConquerorRifle/Fire", CHAN_Weapon)
        TNT1 A 0 A_GunFlash ("Flash")
        TNT1 A 0 A_FireCustomMissile ("S7_ConquerorRifle_Tracer", fRandom [weaponSpread] (-0.05, 0.05), 1, 6, -2, 0, fRandom [weaponSpread] (-0.06, 0.06))
        TNT1 A 0 A_TakeInventory ("S7_ConquerorRifleLoaded", 0x7FFFFFFF)

        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 4.5)
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleNoPump", 1, 1)
        goto Fire.Anim
        TNT1 A 0 A_FireCustomMissile ("S7_45WWCasingSpawnRifle", 0, 0, 10, -12)
    Fire.Anim:
        CQR1 A 1 A_SetPitch (pitch - 1.5)
        CQR1 C 1 A_SetPitch (pitch - 1.75)
        CQR1 B 1 A_SetPitch (pitch + 0.15)
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleNoPump", 1, "Fire.SemiAuto")
        goto Fire.AnimEnd
    Fire.SemiAuto:
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleLoaded", 1)
        TNT1 A 0 A_Jump (2, 1)
        goto Fire.AnimEnd
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleJammed", 1)
        goto Fire.AnimEnd
    Fire.AnimEnd:
        CQR1 C  1 A_SetPitch (pitch + 0.15)
        CQR1 C  1 A_WeaponReady (WRF_NoSecondary | WRF_DisableSwitch | WRF_NoBob)
        CQR1 DD 1 A_WeaponReady (WRF_NoSecondary | WRF_DisableSwitch | WRF_NoBob)
        CQR1 AA 1 A_WeaponReady (WRF_NoSecondary | WRF_DisableSwitch | WRF_NoBob)
        goto Ready2
    Fire.Pump:
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleLoaded", 1)
        TNT1 A 0 A_Jump (2, 1)
        goto Fire.Pump.Anim
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleJammed", 1)
    Fire.Pump.Anim:
        CQR1 A   1 A_PlaySound ("Weapons/ConquerorRifle/Pump1", CHAN_5)
        CQR1 EFG 1
        CQR1 H   6 A_FireCustomMissile ("S7_45WWCasingSpawnRifle", 0, 0, 12, -14)
        CQR1 H   1 A_PlaySound ("Weapons/ConquerorRifle/Pump2", CHAN_5)
        CQR1 GFE 1
        goto Ready2

    Fire.Zoomed:
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleZoomed", 1, 1)
        goto Fire
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire.Zoomed")
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleLoaded", 1, 1)
        goto Fire.Zoomed.Pump
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleJammed", 1, "Fire.Zoomed.Jammed")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash ("Flash.Zoomed")
        TNT1 A 0 A_PlaySound ("Weapons/ConquerorRifle/Fire", CHAN_Weapon)
        TNT1 A 0 A_FireCustomMissile ("S7_ConquerorRifle_Tracer", fRandom [weaponSpread] (-0.005, 0.005), 1, 0, -1, 0, fRandom [weaponSpread] (-0.005, 0.005))
        TNT1 A 0 A_TakeInventory ("S7_ConquerorRifleLoaded", 0x7FFFFFFF)

        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 0, 3.5)
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleNoPump", 1, 1)
        goto Fire.Zoomed.Anim
        TNT1 A 0 A_FireCustomMissile ("S7_45WWCasingSpawnRifle", 0, 0, 8, -8)
        goto Fire.Zoomed.Anim
    Fire.Zoomed.Anim:
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleZoomed", 3, "Fire.Zoomed.Anim.Level3")
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleZoomed", 2, "Fire.Zoomed.Anim.Level2")
        TNT1 A 0 A_ZoomFactor (5.8)
        SCP1 B 1 A_SetPitch (pitch - 1.5)
        SCP1 B 1 A_SetPitch (pitch - 1.75)
        TNT1 A 0 A_ZoomFactor (5.7)
        SCP1 B 1 A_SetPitch (pitch + 0.15)
        TNT1 A 0 A_ZoomFactor (5.9)
        SCP1 B 1 A_SetPitch (pitch + 0.15)
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleNoPump", 1, "Fire.Zoomed.SemiAuto")
        goto Fire.Zoomed.AnimEnd
    Fire.Zoomed.Anim.Level2:
        TNT1 A 0 A_ZoomFactor (11.7)
        SCP1 B 1 A_SetPitch (pitch - 1.5)
        SCP1 B 1 A_SetPitch (pitch - 1.75)
        TNT1 A 0 A_ZoomFactor (11.8)
        SCP1 B 1 A_SetPitch (pitch + 0.15)
        TNT1 A 0 A_ZoomFactor (11.9)
        SCP1 B 1 A_SetPitch (pitch + 0.15)
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleNoPump", 1, "Fire.Zoomed.SemiAuto")
        goto Fire.Zoomed.AnimEnd
    Fire.Zoomed.Anim.Level3:
        TNT1 A 0 A_ZoomFactor (23.7)
        SCP1 B 1 A_SetPitch (pitch - 1.5)
        SCP1 B 1 A_SetPitch (pitch - 1.75)
        TNT1 A 0 A_ZoomFactor (23.8)
        SCP1 B 1 A_SetPitch (pitch + 0.15)
        TNT1 A 0 A_ZoomFactor (23.9)
        SCP1 B 1 A_SetPitch (pitch + 0.15)
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleNoPump", 1, "Fire.Zoomed.SemiAuto")
        goto Fire.Zoomed.AnimEnd
    Fire.Zoomed.SemiAuto:
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleLoaded", 1)
        TNT1 A 0 A_Jump (2, 1)
        goto Fire.Zoomed.AnimEnd
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleJammed", 1)
        goto Fire.Zoomed.AnimEnd

    Fire.Zoomed.AnimEnd:
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleZoomed", 3, "Fire.Zoomed.AnimEnd.Level3")
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleZoomed", 2, "Fire.Zoomed.AnimEnd.Level2")
        TNT1 A 0 A_ZoomFactor (6.0)
        goto Fire.Zoomed.AnimEnd.Anim
    Fire.Zoomed.AnimEnd.Level2:
        TNT1 A 0 A_ZoomFactor (12.0)
        goto Fire.Zoomed.AnimEnd.Anim
    Fire.Zoomed.AnimEnd.Level3:
        TNT1 A 0 A_ZoomFactor (24.0)
        goto Fire.Zoomed.AnimEnd.Anim

    Fire.Zoomed.AnimEnd.Anim:
        SCP1 B  1 A_WeaponReady (WRF_NoSecondary | WRF_DisableSwitch | WRF_NoBob)
        SCP1 BB 1 A_WeaponReady (WRF_NoSecondary | WRF_DisableSwitch | WRF_NoBob)
        SCP1 BB 1 A_WeaponReady (WRF_NoSecondary | WRF_DisableSwitch | WRF_NoBob)
        goto Ready2.Zoom

    Fire.Zoomed.Pump:
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleLoaded", 1)
        TNT1 A 0 A_Jump (2, 1)
        goto Fire.Zoomed.Pump.Anim
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleJammed", 1)
    Fire.Zoomed.Pump.Anim:
        SCP1 B   1 A_PlaySound ("Weapons/ConquerorRifle/Pump1", CHAN_5)
        SCP1 BBB 1
        SCP1 B   6 A_FireCustomMissile ("S7_45WWCasingSpawnRifle", 0, 0, 8, -8)
        SCP1 B   1 A_PlaySound ("Weapons/ConquerorRifle/Pump2", CHAN_5)
        SCP1 BBB 1
        goto Ready2.Zoom

    Fire.Jammed:
        CQR1 A 3 A_PlaySound ("Weapons/ConquerorRifle/DryFire", CHAN_7)
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleNoPump", 1, 1)
        goto AltFire
        TNT1 A 0
        goto Ready2
    Fire.Zoomed.Jammed:
        SCP1 B 3 A_PlaySound ("Weapons/ConquerorRifle/DryFire", CHAN_7)
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleNoPump", 1, 1)
        goto AltFire.Zoomed
        TNT1 A 0
        goto Ready2.Zoom

    AltFire:
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleZoomed", 1, "AltFire.Zoomed")
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleClip", 1, 1)
        goto DryFire2
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleLoaded", 1, 1)
        goto Fire.Pump
        TNT1 A 0 A_TakeInventory ("S7_ConquerorRifleClip", 1)
        TNT1 A 0 A_TakeInventory ("S7_ConquerorRifleJammed", 0x7FFFFFFF)
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleLoaded", 1)
        TNT1 A 0 A_Jump (2, 1)
        goto Fire.Pump.Anim
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleJammed", 1)
        goto Fire.Pump.Anim
    AltFire.Zoomed:
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleZoomed", 1, 1)
        goto AltFire
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleClip", 1, 1)
        goto DryFire2.Zoomed
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleLoaded", 1, 1)
        goto Fire.Zoomed.Pump
        TNT1 A 0 A_TakeInventory ("S7_ConquerorRifleClip", 1)
        TNT1 A 0 A_TakeInventory ("S7_ConquerorRifleJammed", 0x7FFFFFFF)
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleLoaded", 1)
        TNT1 A 0 A_Jump (2, 1)
        goto Fire.Zoomed.Pump.Anim
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleJammed", 1)
        goto Fire.Zoomed.Pump.Anim

    DryFire:
        CQR1 A 3 A_PlaySound ("Weapons/ConquerorRifle/DryFire", CHAN_7)
    DryFire2:
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "Reload")
        goto Ready2
    DryFire.Zoomed:
        SCP1 B 3 A_PlaySound ("Weapons/ConquerorRifle/DryFire", CHAN_7)
    DryFire2.Zoomed:
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "Reload.Zoomed")
        goto Ready2.Zoom

    Zoom:
        "----" A  0 A_JumpIfInventory ("S7_HoldingZoom", 1, "Zoom.Hold")
        "----" A  0 A_GiveInventory ("S7_HoldingZoom", 1)
        "----" A  0 A_JumpIfInventory ("S7_ConquerorRifleZoomed", 0, "Zoom.Reset")
        "----" A  0 A_JumpIfInventory ("S7_ConquerorRifleZoomed", 2, "Zoom3")
        "----" A  0 A_JumpIfInventory ("S7_ConquerorRifleZoomed", 1, "Zoom2")
        "----" A  0 A_GiveInventory ("S7_ConquerorRifleZoomed", 1)
        "----" A  0 A_SetCrosshair (99)
        "----" A  0 A_ZoomFactor (6.0)
         SCP1  B 20 A_SetBlend ("00 00 00", 1.0, 20)
        goto Ready2
    Zoom2:
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleZoomed", 1)
        TNT1 A 0 A_ZoomFactor (12.0)
        goto Zoom2.Finish
    Zoom3:
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleZoomed", 1)
        TNT1 A 0 A_ZoomFactor (24.0)
        goto Zoom2.Finish
    Zoom.Reset:
        TNT1 A 0 A_TakeInventory ("S7_ConquerorRifleZoomed", 0x7FFFFFFF)
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleZoomed", 1)
        TNT1 A 0 A_ZoomFactor (6.0)
        goto Zoom2.Finish
    Zoom2.Finish:
        SCP1 B 6
        goto Ready2

    Zoom.Hold:
        "----" A 1 A_JumpIfInventory ("S7_ConquerorRifleZoomed", 1, "Zoom.Hold2")
        goto Ready2
    Zoom.Hold2:
        "----" A 0 A_JumpIfInventory ("S7_ZoomHoldTime", 6, "Unzoom")
        "----" A 1 A_GiveInventory ("S7_ZoomHoldTime", 1)
        goto Ready2

    Unzoom:
        TNT1 A 0 A_TakeInventory ("S7_ConquerorRifleZoomed", 0x7FFFFFFF)
        TNT1 A 0 A_SetCrosshair (0)
        TNT1 A 0 A_ZoomFactor (1.0, ZOOM_Instant)
        CQR1 A 8 A_SetBlend ("00 00 00", 1.0, 8)
        goto Ready2

    Reload.Zoomed:
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleClip", 0, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("S7_45WWCartridges", 1, 1)
        goto Ready2
        TNT1 A 0 A_TakeInventory ("S7_ConquerorRifleZoomed", 0x7FFFFFFF)
        TNT1 A 0 A_SetCrosshair (0)
        TNT1 A 0 A_ZoomFactor (1.0, ZOOM_Instant)
        CQR1 A 8 A_SetBlend ("00 00 00", 1.0, 8)
        goto Reload
    Reload:
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleZoomed", 1, "Reload.Zoomed")
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleClip", 0, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("S7_45WWCartridges", 1, "TrueReload.Start")
        goto Ready2
    TrueReload.Start:
        TNT1 A 0 A_TakeInventory ("S7_ConquerorRifleReloadPump", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleClip", 1, 2)
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleReloadPump", 1)
    TrueReload:
        TNT1 A 0 A_JumpIfInventory ("S7_ConquerorRifleClip", 0, "ReloadFinish")
        TNT1 A 0 A_JumpIfInventory ("S7_45WWCartridges", 1, 1)
        goto ReloadFinish
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleClip", 1)
        TNT1 A 0 A_TakeInventory ("S7_45WWCartridges", 1)
        loop
    ReloadFinish:
        CQR1 A      2
        CQR1 IJKLMN 2
        TNT1 A      0 A_PlaySound ("Weapons/ConquerorRifle/Out", CHAN_6)
        CQR1 OPN    2
        CQR1 N     12
        TNT1 A      0 A_PlaySound ("Weapons/ConquerorRifle/In", CHAN_7)
        CQR1 QRN    2
        CQR1 NMLKJI 2
        CQR1 A      2
        TNT1 A      0 A_JumpIfInventory ("S7_ConquerorRifleLoaded", 1, 1)
        goto ReloadFinish.Pump
        TNT1 A      0 A_JumpIfInventory ("S7_ConquerorRifleReloadPump", 1, "ReloadFinish.Pump")
        goto Reload.End
    ReloadFinish.Pump:
        CQR1 A   1 A_PlaySound ("Weapons/ConquerorRifle/Pump1", CHAN_5)
        CQR1 EFG 1
        TNT1 A   0 A_JumpIfInventory ("S7_ConquerorRifleLoaded", 1, 2)
        TNT1 A   0 A_FireCustomMissile ("S7_45WWCasingSpawnRifle", 0, 0, 12, -14)
        CQR1 H   6
        CQR1 H   1 A_PlaySound ("Weapons/ConquerorRifle/Pump2", CHAN_5)
        CQR1 GFE 1
        TNT1 A 0 A_TakeInventory ("S7_ConquerorRifleJammed", 0x7FFFFFFF)
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleLoaded", 1)
        TNT1 A 0 A_Jump (2, 1)
        goto Reload.End
        TNT1 A 0 A_GiveInventory ("S7_ConquerorRifleJammed", 1)
        goto Reload.End
    Reload.End:
        TNT1 A 0 A_TakeInventory ("S7_ConquerorRifleReloadPump", 0x7FFFFFFF)
        goto Ready2

    // Muzzle flashes
    Flash:
        CQRF A 1 bright
        stop
    Flash.Zoomed:
        TNT1 A 1 bright
        stop
    }
}