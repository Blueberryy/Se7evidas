//-------------------------------------------------------------------------------------------
//
// FS/909G "Butterfly" SMG
//
//-------------------------------------------------------------------------------------------
actor S7_LaserPewPewClip : ammo {
    inventory.maxAmount 32
    ammo.backpackMaxAmount 32

    +ignoreSkill
}
actor S7_LaserPewPewClipSecond : ammo {
    inventory.maxAmount 32
    ammo.backpackMaxAmount 32

    +ignoreSkill
}

actor S7_LaserPewPewSights : boolean { }
actor S7_LaserPewPewAkimbo : boolean { }
actor S7_LaserPewPewHasTwo : boolean { }
actor S7_LaserPewPew : S7_BaseWeapon 30009 {
    tag "$BUTTFLY"
    inventory.pickupMessage "$BUTTFLY"
    weapon.ammoType1 "S7_LaserPewPewClip"
    weapon.ammoType2 "S7_FBSysCells"
    weapon.ammoUse 1
    weapon.ammoGive 0

    states {

    Spawn:
        BLSP A -1
        loop

    /** Normal mode **/
    Ready:
        TNT1 A 0 A_GiveInventory ("S7_SynthFireActive", 1)
        TNT1 A 0 ACS_NamedExecuteAlways ("S7_SynthFire", 0)
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewAkimbo", 1, "Ready.Akimbo")
        BLSG VWXYZA 1
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewAkimbo", 1, "AkimboRight.Ready")
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewSights", 1, "Ready2.Zoom")
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_ChangeFireMode", 1, "ChangeMode")
        TNT1 A 0 A_JumpIfInventory ("S7_SynthFireLeft", 1, "Fire")
        BLSG A 1 A_WeaponReady (WRF_AllowZoom | WRF_NoFire)
        TNT1 A 0 A_TakeInventory ("S7_HoldingZoom", 0x7FFFFFFF)
        loop
    Ready2.Zoom:
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewAkimbo", 1, "AkimboRight.Ready")
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewSights", 1, 1)
        goto Ready2
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_TakeInventory ("S7_DoMelee", 0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIfInventory ("S7_SynthFireLeft", 1, "Fire.Zoomed")
        BLSG K 1 A_WeaponReady (WRF_AllowZoom | WRF_NoFire)
        TNT1 A 0 A_TakeInventory ("S7_HoldingZoom", 0x7FFFFFFF)
        goto Ready2

    ChangeMode:
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewHasTwo", 1, 1)
        goto Ready2
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewAkimbo", 1, "ChangeMode.ToSingle")
        TNT1 A 0 A_GiveInventory ("S7_LaserPewPewAkimbo", 1)
        BLSG ADEF 2
        TNT1 A 0 A_GunFlash ("AkimboLeft.Select", GFF_NOEXTCHANGE)
        BLS2 AAAAAAA 1
        goto AkimboRight.Ready
    ChangeMode.ToSingle:
        TNT1 A 0 A_TakeInventory ("S7_LaserPewPewAkimbo", 0x7FFFFFFF)
        TNT1 A 0 A_GunFlash ("AkimboLeft.Deselect", GFF_NOEXTCHANGE)
        BLS2 AAAAAAA 1
        BLSG FEDA 2
        goto Ready2

    Deselect:
        TNT1 A 0 A_SetCrosshair (0)
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewSights", 1, "Deselect.Zoom")
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewAkimbo", 1, "AkimboRight.Deselect")
        BLSG AZYXWV 1
        goto TrueDeselect
    Deselect.Zoom:
        BLSG K   1 A_TakeInventory ("S7_LaserPewPewSights", 0x7FFFFFFF)
        BLSG N   1 A_ZoomFactor (1.0)
        BLSG MLA 1
        goto Deselect

    Fire:
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewAkimbo", 1, "AkimboRight.Fire")
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewSights", 1, "Fire.Zoomed")
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash ("Flash")
        TNT1 A 0 A_PlaySound ("Weapons/LaserPewPew/Fire", CHAN_Weapon)
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2.5, 3)
        TNT1 A 0 A_FireCustomMissile ("S7_LaserPewPew_Tracer", fRandom [weaponSpread] (-0.25, 0.25), 1, 6, -2, 0, fRandom [weaponSpread] (-0.3, 0.3))
        BLSG A 1 A_SetPitch (pitch - 0.38, SPF_Interpolate)
        BLSG C 1 A_SetPitch (pitch - 0.07, SPF_Interpolate)
        BLSG B 1 A_SetPitch (pitch - 0.07, SPF_Interpolate)
        BLSG A 1
        BLSG A 1 A_JumpIfInventory ("S7_SynthFireLeft", 1, "Fire")
        goto Ready2

    Fire.Zoomed:
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewAkimbo", 1, "AkimboRight.Fire")
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewSights", 1, 1)
        goto Fire
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire.Zoomed")
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash ("Flash.Zoomed")
        TNT1 A 0 A_PlaySound ("Weapons/LaserPewPew/Fire", CHAN_Weapon)
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 0, 4)
        TNT1 A 0 A_FireCustomMissile ("S7_LaserPewPew_Tracer", fRandom [weaponSpread] (-0.25, 0.25), 1, 0, -1, 0, fRandom [weaponSpread] (-0.3, 0.3))
        TNT1 A 0 A_SetPitch (pitch - 0.38, SPF_Interpolate)
        TNT1 A 0 A_SetAngle (angle - 0.5 * random (-1, 1), SPF_Interpolate)
        BLSG K 1 A_ZoomFactor (1.270)
        TNT1 A 0 A_SetPitch (pitch + 0.07, SPF_Interpolate)
        BLSG O 1 A_ZoomFactor (1.2725)
        TNT1 A 0 A_SetPitch (pitch + 0.07, SPF_Interpolate)
        BLSG P 1 A_ZoomFactor (1.275)
        BlSG K 1
        BLSG K 1 A_JumpIfInventory ("S7_SynthFireLeft", 1, "Fire.Zoomed")
        goto Ready2

    DryFire:
        BLSG A 3 A_PlaySound ("Weapons/LaserPewPew/DryFire", CHAN_7)
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "Reload")
        goto Ready2
    DryFire.Zoomed:
        BLSG K 3 A_PlaySound ("Weapons/LaserPewPew/DryFire", CHAN_7)
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "Reload.Zoomed")
        goto Ready2

    Zoom:
        TNT1 A 0 A_JumpIfInventory ("S7_HoldingZoom", 1, "Ready2")
        TNT1 A 0 A_GiveInventory ("S7_HoldingZoom", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewAkimbo", 1, "Zoom.Akimbo")
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewSights", 0, "Unzoom")
        TNT1 A 0 A_GiveInventory ("S7_LaserPewPewSights", 1)
        TNT1 A 0 A_SetCrosshair (99)
        TNT1 A 0 A_ZoomFactor (1.275)
        BLSG ALMNK 1
        goto Ready2
    Unzoom:
        TNT1 A 0 A_TakeInventory ("S7_LaserPewPewSights", 0x7FFFFFFF)
        TNT1 A 0 A_ZoomFactor (1.0)
        TNT1 A 0 A_SetCrosshair (0)
        BLSG KNMLA 1
        goto Ready2
    Zoom.Akimbo: // Nope.
        TNT1 A 0 A_TakeInventory ("S7_LaserPewPewSights", 0x7FFFFFFF)
        TNT1 A 0 A_ZoomFactor (1.0)
        TNT1 A 0 A_SetCrosshair (0)
        goto Ready2.Akimbo

    Reload.Zoomed:
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewClip", 0, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("S7_FBSysCells", 1, 1)
        goto Ready2
        TNT1 A 0 A_TakeInventory ("S7_LaserPewPewSights", 0x7FFFFFFF)
        TNT1 A 0 A_SetCrosshair (0)
        TNT1 A 0 A_ZoomFactor (1.0)
        BLSG KNMLA 1
        goto Reload
    Reload:
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewSights", 1, "Reload.Zoomed")
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewClip", 0, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewHasTwo", 1, "TrySwap")
        TNT1 A 0 A_JumpIfInventory ("S7_FBSysCells", 1, "TrueReload")
        goto Ready2
    TrueReload:
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewClip", 0, "ReloadFinish")
        TNT1 A 0 A_JumpIfInventory ("S7_FBSysCells", 1, 1)
        goto ReloadFinish
        TNT1 A 0 A_GiveInventory ("S7_LaserPewPewClip", 1)
        TNT1 A 0 A_TakeInventory ("S7_FBSysCells", 1)
        loop
    ReloadFinish:
        BLSG ADEFF   2
        BLSG F      15 A_PlaySound ("Weapons/LaserPewPew/Out", CHAN_Weapon)
        BLSG GHI     2
        BLSG I       2 A_PlaySound ("Weapons/LaserPewPew/In", CHAN_Weapon)
        BLSG IIJEDAA 2
        goto Ready2

    // Gun swapping
    TrySwap:
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewClipSecond", 13, "DoSwap")
        TNT1 A 0 A_JumpIfInventory ("S7_FBSysCells", 1, "TrueReload")
        goto Ready2
    DoSwap:
        BLSG AZYXWV 1
        TNT1 A      5 ACS_NamedExecuteAlways ("S7_PerformButterflySwap", 0)
        BLSG VWXYZA 1
        goto Ready2

    // Muzzle flashes
    Flash:
        BLSG U 1 bright
        stop
    Flash.Zoomed:
        BLSG T 1 bright
        stop


    /** Akimbo mode **/
    Ready.Akimbo:
        BLS2 ZYXWVU 1
        BLS2 U      1 A_GunFlash ("AkimboLeft.Ready", GFF_NOEXTCHANGE)
    Ready2.Akimbo:
    AkimboRight.Ready:
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewAkimbo", 1, 1)
        goto AkimboRight.Derped
        TNT1 A 0 A_JumpIf (!CallACS ("S7_SynthFireIdle"), "AkimboRight.ReadyNotIdle")
        TNT1 A 0 A_JumpIfInventory ("S7_ChangeFireMode", 1, "ChangeMode")
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "AkimboRight.Reload")
    AkimboRight.ReadyNotIdle: // Can't think of a better name :/
        TNT1 A 0 A_TakeInventory ("S7_DoMelee", 0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory ("S7_LaserPewPewSights", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIfInventory ("S7_SynthFireRight", 1, "AkimboRight.Fire")
        BLS2 A 1 A_WeaponReady (WRF_NoFire | (WRF_NoSwitch * !CallACS ("S7_SynthFireIdle")))
        goto AkimboRight.Ready

    AkimboLeft.Ready:
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewAkimbo", 1, 1)
        goto AkimboLeft.Derped
        BLS2 D 1 A_JumpIfInventory ("S7_SynthFireLeft", 1, "AkimboLeft.Fire")
        loop

    AkimboRight.Deselect:
        TNT1 A 0 A_GunFlash ("AkimboLeft.DeselectReal", GFF_NOEXTCHANGE)
        BLS2 LMNOP 1
        goto Super::Deselect

    AkimboLeft.DeselectReal:
        BLS2 GHIJK 1
        stop

    AkimboLeft.Select:
        BLS2 K 1 A_GiveInventory ("S7_SynthfireLeftBusy", 1)
        BLS2 KJIHGD 1
        BLS2 D 1 A_TakeInventory ("S7_SynthfireLeftBusy", 0x7FFFFFFF)
        goto AkimboLeft.Ready
    AkimboLeft.Deselect:
        BLS2 D 1 A_GiveInventory ("S7_SynthfireLeftBusy", 1)
        BLS2 DGHIJK 1
        BLS2 K 1 A_TakeInventory ("S7_SynthfireLeftBusy", 0x7FFFFFFF)
        stop

    AkimboRight.Fire:
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewClip", 1, 1)
        goto AkimboRight.Dryfire
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound ("Weapons/LaserPewPew/Fire", CHAN_Weapon)
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2.5, 3)
        TNT1 A 0 A_FireCustomMissile ("S7_LaserPewPew_Tracer", fRandom [weaponSpread] (-0.25, 0.25), 0, 6, -2, 0, fRandom [weaponSpread] (-0.3, 0.3))
        TNT1 A 0 A_TakeInventory ("S7_LaserPewPewClip", 1)
        BLS3 A 1 A_SetPitch (pitch - 0.38, SPF_Interpolate)
        BLS2 C 1 A_SetPitch (pitch - 0.07, SPF_Interpolate)
        BLS2 B 1 A_SetPitch (pitch - 0.07, SPF_Interpolate)
        BLS2 A 1
        BLS2 A 1 A_JumpIfInventory ("S7_SynthFireRight", 1, "AkimboRight.Fire")
        goto AkimboRight.Ready
    AkimboLeft.Fire:
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewClipSecond", 1, 1)
        goto AkimboLeft.Dryfire
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_PlaySound ("Weapons/LaserPewPew/Fire", CHAN_5)
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, -2.5, 3)
        TNT1 A 0 A_FireCustomMissile ("S7_LaserPewPew_Tracer", fRandom [weaponSpread] (-0.25, 0.25), 0, -6, -2, 0, fRandom [weaponSpread] (-0.3, 0.3))
        TNT1 A 0 A_TakeInventory ("S7_LaserPewPewClipSecond", 1)
        BLS3 B 1 A_SetPitch (pitch - 0.38, SPF_Interpolate)
        BLS2 E 1 A_SetPitch (pitch - 0.07, SPF_Interpolate)
        BLS2 F 1 A_SetPitch (pitch - 0.07, SPF_Interpolate)
        BLS2 D 1
        BLS2 D 1 A_JumpIfInventory ("S7_SynthFireLeft", 1, "AkimboLeft.Fire")
        goto AkimboLeft.Ready

    AkimboRight.Dryfire:
        TNT1 A 0 A_TakeInventory ("S7_SynthFireRight", 0x7FFFFFFF)
        BLS2 A 7 A_PlaySound ("Weapons/LaserPewPew/DryFire", CHAN_7)
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewClipSecond", 1, "AkimboRight.Ready") // Check the opposite ammo, if there's none, we can go ahead and reload
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "AkimboRight.Reload")
        goto AkimboRight.Ready
    AkimboLeft.Dryfire:
        TNT1 A 0 A_TakeInventory ("S7_SynthFireLeft", 0x7FFFFFFF)
        BLS2 D 7 A_PlaySound ("Weapons/LaserPewPew/DryFire", CHAN_6)
        TNT1 A 0 A_JumpIfInventory ("S7_LaserPewPewClip", 1, "AkimboLeft.Ready") // Same as above
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "AkimboLeft.Reload")
        goto AkimboLeft.Ready

    AkimboLeft.Reload:
        TNT1 A 0 A_GiveInventory ("S7_Reloading", 1)
        goto AkimboLeft.Ready
    AkimboRight.Reload:
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIf (CallACS ("S7_ButterflyAkimboReload"), "AkimboRight.ReloadAnim")
        goto Ready2
    AkimboRight.ReloadAnim:
        TNT1 A 0 A_GunFlash ("AkimboLeft.ReloadAnim", GFF_NOEXTCHANGE)
        BLS2 A     3 A_PlaySound ("Weapons/LaserPewPew/Out", CHAN_Weapon)
        BLS2 AAAAA 1 // Left buttfly lower
        BLS2 A     2 // Start right clip insert
        BLSG G     2 A_PlaySound ("Weapons/LaserPewPew/In", CHAN_Weapon)
        BLSG HIIJJ 2
        BLSG IHG   2 // Finish right clip insert
        BLS2 A     2
        BLS2 LMNOP 1 // Right buttfly lower
        TNT1 A     2
        TNT1 A     2
        TNT1 AAAAA 2
        TNT1 AAA   2 // Finish left clip insert
        BLS2 PONML 1
        BLS2 A     3
        goto AkimboRight.Ready

    AkimboLeft.ReloadAnim:
        BLS2 D     3 A_PlaySound ("Weapons/LaserPewPew/Out", CHAN_5)
        BLS2 GHIJK 1 // Left buttfly lower
        TNT1 A     2 // Start right clip insert
        TNT1 A     2
        TNT1 AAAAA 2
        TNT1 AAA   2 // Finish right clip insert
        TNT1 A     2
        BLS2 KJIHG 1 // Right buttfly lower
        BLS2 D     2 // Start left clip insert
        BLS2 Q     2 A_PlaySound ("Weapons/LaserPewPew/In", CHAN_5)
        BLS2 RSSTT 2
        BLS2 SRQ   2 // Finish left clip insert
        BLS2 DDDDD 1
        BLS2 D     3
        goto AkimboLeft.Ready

    AkimboRight.Derped:
        BLS2 A 8
        BLSG FEDA 2
        goto Ready2

    AkimboLeft.Derped:
        BLS2 GHIJK 1
        TNT1 A 1
        stop
    }
}

//-------------------------------------------------------------------------------------------
//
// Tracer
//
//-------------------------------------------------------------------------------------------
actor S7_LaserPewPew_Tracer : S7_BaseTracer {
    damage (random [weaponDamage] (1, 5) * 8)
    health 155
    damageType LaserTracer
    scale 0.0125
    missileType "S7_LaserPewPew_Tracer_Trail"
    decal S7_LaserPewPewDecal

    -bloodSplatter

    states {
    Spawn:
        TNT1 AA 0 A_ScaleVelocity (1.0 / 25)
        TNT1 A  0 A_ScaleVelocity (abs (health))
    Idle:
        BLSF A 1 bright
        wait

    Death:
    Crash:
        TNT1 A 0 A_FaceTarget
        TNT1 A 3 //A_SpawnItemEx ("S7_LaserPewPew_Puff")
        stop
    XDeath:
        TNT1 A 3
        stop
    }
}

actor S7_LaserPewPew_Tracer_Trail : S7_TracerEffectsBase {
    scale 0.0125

    states {
    Spawn:
        BLSF A 1 bright
    Loople:
        "----" A 1 bright A_FadeOut (0.2)
        wait
    }
}