//-------------------------------------------------------------------------------------------
//
// Super Shotgun
//
//-------------------------------------------------------------------------------------------
actor S7_SSGFauxClip : ammo {
    inventory.maxAmount 2
    ammo.backpackMaxAmount 2

    +ignoreSkill
}

actor S7_SSGLeftLoaded  : boolean { }
actor S7_SSGRightLoaded : boolean { }
actor S7_SSGFireBoth    : boolean { }
actor S7_SSG : S7_BaseWeapon 30006 {
    tag "$SSG"
    inventory.pickupMessage "$SSG"
    weapon.ammoType1 "S7_SSGFauxClip"
    weapon.ammoType2 "S7_12gaShells"
    weapon.ammoUse 0
    weapon.ammoGive 0

    states {
    Spawn:
        SSGP A -1
        loop

    Ready:
        TNT1 A     0 A_GiveInventory ("S7_SynthFireActive", 1)
        TNT1 A     0 ACS_NamedExecuteAlways ("S7_SynthFire", 0, TRUE)
        SSGS ABCDE 1
        SSG0 A     1
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_SynthFireLeft", 1, "Fire.Left")
        TNT1 A 0 A_JumpIfInventory ("S7_SynthFireRight", 1, "Fire.Right")
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory ("S7_SSGFireBoth", 0x7FFFFFFF)
        SSG0 A 1 A_WeaponReady (WRF_NoFire)
        loop

    TrueDeselect:
        SSG0 A     1
        SSGS EDCBA 1
        TNT1 A     0 A_Lower
        wait

    Fire.Left:
        SSG0 A          1 A_JumpIfInventory ("S7_SynthFireRight", 1, "Fire.Both2")
        SSG0 A          1 A_JumpIfInventory ("S7_SynthFireRight", 1, "Fire.Both")
        TNT1 A          0 A_JumpIfInventory ("S7_SSGLeftLoaded", 1, 1)
        goto DryFireLeft

        TNT1 AAAAAAAAAA 0 A_FireCustomMissile ("S7_12gaTracer", fRandom [weaponSpread] (-3.3, 3.3), 1, 2, -1, 0, fRandom [weaponSpread] (-3.8, 3.8))
        TNT1 A          0 A_AlertMonsters
        TNT1 A          0 A_PlaySound ("Weapons/SSG/Fire", CHAN_Weapon)
        TNT1 A          0 A_TakeInventory ("S7_SSGLeftLoaded", 0x7FFFFFFF)
        TNT1 A          0 A_GunFlash ("Flash.Left")
        SSG5 AB         1 A_SetPitch (pitch - 2.175, SPF_Interpolate)
        TNT1 A          0 A_TakeInventory ("S7_SynthFireLeft", 0x7FFFFFFF)
        goto Ready2
    Fire.Right:
        SSG0 A          1 A_JumpIfInventory ("S7_SynthFireLeft", 1, "Fire.Both2")
        SSG0 A          1 A_JumpIfInventory ("S7_SynthFireLeft", 1, "Fire.Both")
        TNT1 A          0 A_JumpIfInventory ("S7_SSGRightLoaded", 1, 1)
        goto DryFireRight

        TNT1 AAAAAAAAAA 0 A_FireCustomMissile ("S7_12gaTracer", fRandom [weaponSpread] (-3.3, 3.3), 1, 3, -1, 0, fRandom [weaponSpread] (-3.8, 3.8))
        TNT1 A          0 A_AlertMonsters
        TNT1 A          0 A_PlaySound ("Weapons/SSG/Fire", CHAN_5)
        TNT1 A          0 A_TakeInventory ("S7_SSGRightLoaded", 0x7FFFFFFF)
        TNT1 A          0 A_GunFlash ("Flash.Right")
        SSG5 AB         1 A_SetPitch (pitch - 2.175, SPF_Interpolate)
        TNT1 A          0 A_TakeInventory ("S7_SynthFireRight", 0x7FFFFFFF)
        goto Ready2

    Fire.Both2:
        SSG0 A          1
    Fire.Both:
        SSG0 A          1
        TNT1 A          0 A_JumpIfInventory ("S7_SSGLeftLoaded", 1, 1)
        goto Fire.Both.DryLeft
        TNT1 A          0 A_JumpIfInventory ("S7_SSGRightLoaded", 1, 1)
        goto Fire.Both.DryRight

        TNT1 AAAAAAAAAA 0 A_FireCustomMissile ("S7_12gaTracer", fRandom [weaponSpread] (-3.3, 3.3), 1, 2, -1, 0, fRandom [weaponSpread] (-3.8, 3.8))
        TNT1 A          0 A_AlertMonsters
        TNT1 A          0 A_PlaySound ("Weapons/SSG/Fire", CHAN_Weapon)
        TNT1 A          0 A_TakeInventory ("S7_SSGLeftLoaded", 0x7FFFFFFF)
        TNT1 A          0 A_GunFlash ("Flash.Both")
        SSG5 A          1 A_SetPitch (pitch - 2.175, SPF_Interpolate)

        TNT1 AAAAAAAAAA 0 A_FireCustomMissile ("S7_12gaTracer", fRandom [weaponSpread] (-3.3, 3.3), 1, 3, -1, 0, fRandom [weaponSpread] (-3.8, 3.8))
        TNT1 A          0 A_AlertMonsters
        TNT1 A          0 A_PlaySound ("Weapons/SSG/Fire", CHAN_5)
        TNT1 A          0 A_TakeInventory ("S7_SSGRightLoaded", 0x7FFFFFFF)

        SSG5 A          1 A_SetPitch (pitch - (2.175 * 2), SPF_Interpolate)
        SSG5 B          1 A_SetPitch (pitch -  2.175     , SPF_Interpolate)
        SSG0 A          4
        goto Fire.Both.End
    Fire.Both.DryLeft:
        TNT1 A         0 A_JumpIfInventory ("S7_SSGRightLoaded", 1, 1)
        goto DryFireBoth

        TNT1 A          0 A_PlaySound ("Weapons/DryFireShotgun", CHAN_Weapon)
        SSG0 A          1

        TNT1 AAAAAAAAAA 0 A_FireCustomMissile ("S7_12gaTracer", fRandom [weaponSpread] (-3.3, 3.3), 1, 3, -1, 0, fRandom [weaponSpread] (-3.8, 3.8))
        TNT1 A          0 A_AlertMonsters
        TNT1 A          0 A_PlaySound ("Weapons/SSG/Fire", CHAN_5)
        TNT1 A          0 A_TakeInventory ("S7_SSGRightLoaded", 0x7FFFFFFF)
        TNT1 A          0 A_GunFlash ("Flash.Right")

        SSG5 AB         1 A_SetPitch (pitch - 2.175, SPF_Interpolate)
        SSG0 A          4
        goto Fire.Both.End
    Fire.Both.DryRight:
        TNT1 AAAAAAAAAA 0 A_FireCustomMissile ("S7_12gaTracer", fRandom [weaponSpread] (-3.3, 3.3), 1, 2, -1, 0, fRandom [weaponSpread] (-3.8, 3.8))
        TNT1 A          0 A_AlertMonsters
        TNT1 A          0 A_PlaySound ("Weapons/SSG/Fire", CHAN_Weapon)
        TNT1 A          0 A_TakeInventory ("S7_SSGLeftLoaded", 0x7FFFFFFF)
        TNT1 A          0 A_GunFlash ("Flash.Left")
        SSG5 A          1 A_SetPitch (pitch - 2.175, SPF_Interpolate)

        TNT1 A          0 A_PlaySound ("Weapons/DryFireShotgun", CHAN_Weapon)

        SSG5 B          1 A_SetPitch (pitch - 2.175, SPF_Interpolate)
        SSG5 B          1
        SSG0 A          4
        goto Fire.Both.End
    Fire.Both.End:
        TNT1 A 0 A_TakeInventory ("S7_SynthFireLeft", 0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory ("S7_SynthFireRight", 0x7FFFFFFF)
        goto Ready2

    Flash.Left:
        SSG5 CD 1 bright
        stop
    Flash.Right:
        SSG5 EF 1 bright
        stop
    Flash.Both:
        SSG5 CGF 1 bright
        stop

    DryFireLeft:  SSG0 A 3 A_PlaySound ("Weapons/DryFireShotgun", CHAN_Weapon) goto DryFireEnd
    DryFireRight: SSG0 A 3 A_PlaySound ("Weapons/DryFireShotgun", CHAN_5)      goto DryFireEnd
    DryFireBoth:  SSG0 A 1 A_PlaySound ("Weapons/DryFireShotgun", CHAN_Weapon)
                  SSG0 A 2 A_PlaySound ("Weapons/DryFireShotgun", CHAN_5)      goto DryFireEnd

    DryFireEnd:
        TNT1 A 0 A_TakeInventory ("S7_SynthFireLeft", 0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory ("S7_SynthFireRight", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIfInventory ("S7_SSGFauxClip", 1, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "Reload")
        goto Ready2

    Reload:
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory ("S7_SynthFireLeft", 0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory ("S7_SynthFireRight", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIfInventory ("S7_SSGFauxClip", 2, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("S7_SSGFauxClip", 1, "Reload.NeedOne")
        goto Reload.NeedTwo

    Reload.NeedOne:
        TNT1 A 0 A_JumpIfInventory ("S7_12gaShells", 1, 1)
        goto Ready2
        TNT1 A 0
        goto Reload.Start
    Reload.NeedTwo:
        TNT1 A 0 A_JumpIfInventory ("S7_12gaShells", 2, 1)
        goto Ready2
        TNT1 A 0
        goto Reload.Start

    Reload.Start:
        SSG0 AAABC 1
        SSG0 D     1 A_PlaySound ("Weapons/SSG/Open", CHAN_6)
        SSG0 E     1
    ReloadSelect:
        TNT1 A 0 A_JumpIfInventory ("S7_SSGLeftLoaded", 1, "LoadRight")
        TNT1 A 0 A_JumpIfInventory ("S7_SSGRightLoaded", 1, "LoadLeft")
        goto LoadBoth

    LoadBoth:
        TNT1 A         0 A_FireCustomMissile ("S7_12gaShellCasingSpawn", 0, 0, 4, -16)
        SSG0 F         1 A_FireCustomMissile ("S7_12gaShellCasingSpawn", 0, 0, 7, -18)
        SSG0 GHIJKLMNO 1
        TNT1 A         0 A_GiveInventory ("S7_SSGLeftLoaded", 1)
        TNT1 A         0 A_TakeInventory ("S7_12gaShells", 1)
        SSG0 P         1 A_PlaySound ("Weapons/SSG/In", CHAN_6)
        SSG0 Q         4
        SSG0 RSTUVWXYZ 1
        SSG1 A         1
        TNT1 A         0 A_GiveInventory ("S7_SSGRightLoaded", 1)
        TNT1 A         0 A_TakeInventory ("S7_12gaShells", 1)
        SSG1 B         1 A_PlaySound ("Weapons/SSG/In", CHAN_6)
        SSG1 C         4
        SSG1 DEFGHI    1
        goto ReloadEnd

    LoadLeft:
        TNT1 A          0 A_JumpIfInventory ("S7_SSGRightLoaded", 1, 1)
        goto LoadBoth
        SSG3 A         1 A_FireCustomMissile ("S7_12gaShellCasingSpawn", 0, 0, 4, -16)
        SSG3 BCDEFHIJK 1
        TNT1 A         0 A_GiveInventory ("S7_SSGLeftLoaded", 1)
        TNT1 A         0 A_TakeInventory ("S7_12gaShells", 1)
        SSG0 Q         4 A_PlaySound ("Weapons/SSG/In", CHAN_6)
        SSG3 LM        1
        SSG1 DEFGHI    1
        goto ReloadEnd

    LoadRight:
        TNT1 A      0 A_JumpIfInventory ("S7_SSGLeftLoaded", 1, 1)
        goto LoadBoth
        SSG2 A      1 A_FireCustomMissile ("S7_12gaShellCasingSpawn", 0, 0, 7, -18)
        SSG2 BCDE   1
        SSG0 WXYZ   1
        SSG1 A      1
        TNT1 A      0 A_GiveInventory ("S7_SSGRightLoaded", 1)
        TNT1 A      0 A_TakeInventory ("S7_12gaShells", 1)
        SSG1 B      1 A_PlaySound ("Weapons/SSG/In", CHAN_6)
        SSG1 C      4
        SSG1 DEFGHI 1
        goto ReloadEnd

    ReloadEnd:
        SSG0 E     1
        SSG0 D     1 A_PlaySound ("Weapons/SSG/Close", CHAN_6)
        SSG0 CBAAA 1
        goto Ready2
    }
}