//-------------------------------------------------------------------------------------------
//
// Plasma Machine Gun Mk III
//
//-------------------------------------------------------------------------------------------
actor S7_PlasmaGun_Semi          : boolean { }
actor S7_PlasmaGun_Charge        : counter { inventory.maxAmount 4 }
actor S7_PlasmaGun_ChargingLevel : counter { inventory.maxAmount 53 }
actor S7_PlasmaGunMag            : ammo {
    inventory.maxAmount 36
    ammo.backpackMaxAmount 36

    +ignoreSkill
}

actor S7_PlasmaGun_AltfireLock : boolean { }

actor S7_PlasmaGun : S7_BaseWeapon 30003 {
    tag "$PLASMAGUN"
    inventory.pickupMessage "$PLASMAGUN"
    weapon.ammoType1 "S7_PlasmaGunMag"
    weapon.ammoType2 "S7_Cells"
    weapon.ammoUse 1
    weapon.ammoGive 0

    +weapon.noAutoFire

    states {
    Spawn:
        PLSG U -1
        stop

    Ready:
        PLSG VWXYZA 1
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_ChangeFireMode", 1, "ChangeFireMode")
        PLSG A 1 A_WeaponReady
        TNT1 A 0 A_TakeInventory ("S7_PlasmaGun_AltfireLock", 0x7FFFFFFF)
        loop
    Ready2Derp:
        PLSG A 2
        goto Ready2

    Deselect:
        PLSG AZYXWV 1
        goto TrueDeselect

    Fire:
        TNT1 A 0                A_JumpIfInventory ("S7_PlasmaGun_Charge", 1, "Fire.Charged")
        TNT1 A 0                A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0                A_JumpIfInventory ("S7_PlasmaGun_Semi", 1, "Fire.Semi")
        TNT1 A 0                A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        TNT1 A 0                A_PlaySound ("Weapons/PlasmaGun/Fire", CHAN_Weapon)
        TNT1 A 0                A_FireCustomMissile ("S7_PlasmaGunRail", fRandom [weaponSpread] (-3.35, 3.35), 1, 6, -4, 0, fRandom [weaponSpread] (-3.35, 3.35))
        PLSG A 1 offset (4, 36) A_GunFlash ("Flash.Fire")
        PLSG D 1 offset (3, 35)
        PLSG C 1 offset (2, 34)
        PLSG B 1 offset (1, 33)
        PLSG A 1 A_Refire ("Fire")
        TNT1 A 0 A_ClearRefire
        goto Ready2

    Fire.Semi:
        TNT1 A 0                A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        TNT1 A 0                A_PlaySound ("Weapons/PlasmaGun/Fire", CHAN_Weapon)
        TNT1 A 0                A_FireCustomMissile ("S7_PlasmaGunRail", fRandom [weaponSpread] (-3.35, 3.35), 1, 6, -4, 0, fRandom [weaponSpread] (-3.35, 3.35))
        PLSG A 1 offset (4, 36) A_GunFlash ("Flash.Fire")
        PLSG D 1 offset (3, 35)
        PLSG C 1 offset (2, 34)
        PLSG B 1 offset (1, 33)
        PLSG A 1                A_WeaponReady (WRF_NoBob | WRF_NoSecondary | WRF_DisableSwitch)
        goto Ready2

    DryFire:
        TNT1 A   0 A_PlaySound ("Weapons/Misc/Dryfire", CHAN_7)
        TNT1 A   0 A_WeaponReady (WRF_NoFire | WRF_NoSwitch)
        PLSG AAA 1
        TNT1 A   0 A_JumpIfInventory ("S7_AutoReloading", 1, "Reload")
        goto Ready2
    DryFire.Alt:
        PLSG A 3 A_PlaySound ("Weapons/Misc/Dryfire", CHAN_7)
        goto Ready2
    Reload:
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_Charge", 1, "Decharge")
        PLSG A 1 A_TakeInventory ("S7_Reloading", 1)
        goto Ready2

    Decharge:
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_Charge", 1, 1)
        goto Decharge.End
        TNT1 A 0 A_TakeInventory ("S7_PlasmaGun_Charge", 1)
        TNT1 A 0 A_GiveInventory ("S7_PlasmaGunMag", 5)
        loop
    Decharge.End:
        PLSG A 5 A_SetBlend ("10 6B DA", 0.25, 4)
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, 1)
        goto Reload
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 1)
        goto Ready2

    Fire.Charged:
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_Charge", 4, "Fire.ChargeLevel4")
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_Charge", 3, "Fire.ChargeLevel3")
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_Charge", 2, "Fire.ChargeLevel2")
    Fire.ChargeLevel1:
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        TNT1 A 0 A_FireCustomMissile ("S7_PlasmaGunRailCharge1", 0.0, 1, 6, -4, 0, 0.0)
        goto Fire.Charged.Finish
    Fire.ChargeLevel2:
        TNT1 A  0 A_SetBlend ("10 6B DA", 0.2333, 8)
        TNT1 AA 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        TNT1 A  0 A_FireCustomMissile ("S7_PlasmaGunRailCharge2", 0.0, 1, 6, -4, 0, 0.0)
        goto Fire.Charged.Finish
    Fire.ChargeLevel3:
        TNT1 A   0 A_SetBlend ("10 6B DA", 0.4666, 8)
        TNT1 AAA 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        TNT1 A   0 A_FireCustomMissile ("S7_PlasmaGunRailCharge3", 0.0, 1, 6, -4, 0, 0.0)
        goto Fire.Charged.Finish
    Fire.ChargeLevel4:
        TNT1 A    0 A_SetBlend ("10 6B DA", 0.7, 8)
        TNT1 AAAA 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        TNT1 A    0 A_FireCustomMissile ("S7_PlasmaGunRailCharge4", 0.0, 1, 6, -4, 0, 0.0)
        goto Fire.Charged.Finish
    Fire.Charged.Finish:
        TNT1 A     0                A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        TNT1 A     0                A_TakeInventory ("S7_PlasmaGun_Charge", 0x7FFFFFFF)
        PLSG A     1 offset (8, 40) A_GunFlash ("Flash.Charged")
        PLSG D     1 offset (6, 38)
        PLSG F     1 offset (4, 36)
        PLSG F     1 offset (2, 34)
        PLSG F     6
        PLSG EDCBA 1
        goto Ready2

    Flash.Fire:
        PLSF A 1 bright
        stop

    AltFire:
        PLSG A 1 A_JumpIfInventory ("S7_PlasmaGun_AltfireLock", 1, "Ready2")
        PLSG A 2 A_JumpIfInventory ("S7_PlasmaGun_Charge", 4, "Ready2Derp")
        PLSG A 1 A_JumpIfInventory ("S7_PlasmaGunMag", 5, 1)
        goto DryFire.Alt
        PLSG A 1 A_Refire ("ChargeStart")
        TNT1 A 0 A_ClearRefire
        goto Ready2Derp
    
    ChargeStart:
        TNT1 A 0 A_PlaySound ("Weapons/PlasmaGun/Charge", CHAN_Weapon)
    Charge:
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_ChargingLevel", 0, "ChargingFinished")
        PLSG A 1 A_GiveInventory ("S7_PlasmaGun_ChargingLevel", 1)
        PLSG A 1 A_Refire ("Charge2")
        PLSG A 1 A_ClearRefire
        goto CancelCharging
    Charge2:
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_ChargingLevel", 0, "ChargingFinished")
        PLSG G 1 A_GiveInventory ("S7_PlasmaGun_ChargingLevel", 1)
        PLSG A 1 A_Refire ("Charge3")
        PLSG A 1 A_ClearRefire
        goto CancelCharging
    Charge3:
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_ChargingLevel", 0, "ChargingFinished")
        PLSG I 1 A_GiveInventory ("S7_PlasmaGun_ChargingLevel", 1)
        PLSG A 1 A_Refire ("Charge4")
        PLSG A 1 A_ClearRefire
        goto CancelCharging
    Charge4:
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_ChargingLevel", 0, "ChargingFinished")
        PLSG J 1 A_GiveInventory ("S7_PlasmaGun_ChargingLevel", 1)
        PLSG A 1 A_Refire ("Charge5")
        PLSG A 1 A_ClearRefire
        goto CancelCharging
    Charge5:
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_ChargingLevel", 0, "ChargingFinished")
        PLSG H 1 A_GiveInventory ("S7_PlasmaGun_ChargingLevel", 1)
        PLSG A 1 A_Refire ("Charge")
        PLSG A 1 A_ClearRefire
        goto CancelCharging

    CancelCharging:
        TNT1 A 0 A_StopSound (CHAN_Weapon)
        TNT1 A 0 A_GiveInventory ("S7_PlasmaGun_AltfireLock", 1)
        PLSG A 1 A_ClearRefire
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_TakeInventory ("S7_PlasmaGun_ChargingLevel", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_ChargingLevel", 1, "CancelCharging")
        goto Ready2Derp
    ChargingFinished:
        TNT1 A     0 A_StopSound (CHAN_Weapon)
        TNT1 A     0 A_JumpIfInventory ("S7_PlasmaGunMag", 5, 1)
        goto DryFire.Alt
        TNT1 A     0 A_GiveInventory ("S7_PlasmaGun_AltfireLock", 1)
        TNT1 A     0 A_SetBlend ("10 6B DA", 0.25, 5)
        TNT1 A     0 A_TakeInventory ("S7_PlasmaGunMag", 5, TIF_NoTakeInfinite)
        TNT1 A     0 A_TakeInventory ("S7_PlasmaGun_ChargingLevel", 0x7FFFFFFF)
        TNT1 A     0 A_GiveInventory ("S7_PlasmaGun_Charge", 1)
        TNT1 A     0 A_PlaySound ("Weapons/PlasmaGun/ChargeFinish")
        PLSG AAAAA 1 A_ClearRefire
        goto Ready2Derp

    Flash.Charged:
        PLSF B 1 bright offset (8, 40)
        stop

    ChangeFireMode:
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_Semi", 1, "ToFullAuto")
        TNT1 A 0 A_GiveInventory ("S7_PlasmaGun_Semi", 1)
        goto ChangeFireMode.Animation
    ToFullAuto:
        TNT1 A 0 A_TakeInventory ("S7_PlasmaGun_Semi", 0x7FFFFFFF)
        goto ChangeFireMode.Animation
    ChangeFireMode.Animation:
        PLSG A 1 offset (4, 36) A_PlaySound ("Weapons/ModeChange", CHAN_Weapon)
        PLSG A 1 offset (3, 35)
        PLSG A 1 offset (2, 34)
        PLSG A 1 offset (1, 33)
        PLSG A 1                A_WeaponReady (WRF_NoBob | WRF_NoFire | WRF_DisableSwitch)
        goto Ready2
    }
}

/** Projectile **/
actor S7_PlasmaGunRail : fastProjectile {
    radius 2
    height 2
    speed 25
    projectile
    renderStyle add
    scale 0.015
    damage (fRandom [weaponDamage] (1.0, 5.0) * 13)
    missileType "S7_PlasmaGunRailEffectSpawner"
    decal S7_PlasmaGunRail

    +noDamageThrust +forceXYBillboard

    states {
    Spawn:
        TNT1 AA 0 A_ScaleVelocity (1.0 / 25)
        TNT1 A  0 A_ScaleVelocity (150.0)
    Idle:
        PLGF A 1 bright
        wait

    Death:
        HTS0 A       1 A_SetScale (0.15)
        HTS0 BCDEFGH 1
        stop
    }
}

/** Trail spawner **/
actor S7_PlasmaGunRailEffectSpawner {
    +noBlockmap +noGravity +noTeleport +noInteraction

    states {
    Spawn:
        TNT1 A 1 noDelay A_SpawnItemEx ("S7_PlasmaGunRailTrail", 0.0, 0.0, 8.0, 0.0, 0.0, 0.0, 0, SXF_Clientside | SXF_NoCheckPosition)
        stop
    }
}

/** Trails **/
actor S7_PlasmaGunRailTrail {
    renderStyle add
    scale 0.015

    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +forceXYBillboard +clientsideOnly

    states {
    Spawn:
         PLGF  A 1 bright
        "----" A 1 bright A_FadeOut (0.13)
        wait
    }
}

#INCLUDE "Decorate/Weapons/Primary/PlasmaGun/ChargeProj.DEC"