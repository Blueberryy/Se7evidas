actor S7_PlasmaGun_Semi : boolean { }

actor S7_PlasmaGun : S7_BaseWeapon 30002 {
    inventory.PickupMessage "$PLASMAGUN"
    tag "$PLASMAGUN"
    //weapon.AmmoType1 "S7_PlasmaGunMag"
    //weapon.AmmoType2 "S7_Cells"
    weapon.AmmoUse 0
    weapon.AmmoGive 0
    states {
    Spawn:
        PLSG Z -1
        stop
    
    Ready:
    Ready2:
        //TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_ChangeFireMode", 1, "ChangeFireMode")
        PLSG A 1 A_WeaponReady
        loop
    
    Fire:
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_Semi", 1, "Fire.Semi")
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        TNT1 A 0 A_PlaySound ("Weapons/PlasmaGun/Fire", CHAN_Weapon)
        TNT1 A 0 A_FireCustomMissile ("S7_PlasmaGunRail", fRandom [weaponSpread] (-2.5, 2.5), 0, 5, -7, 0, fRandom [weaponSpread] (-2.5, 2.5))
        PLSG A 1 offset (4, 36) A_GunFlash ("Flash.Fire")
        PLSG D 1 offset (3, 35)
        PLSG C 1 offset (2, 34)
        PLSG B 1 offset (1, 33)
        PLSG A 1 A_Refire
        goto Ready2

    Fire.Semi:
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        TNT1 A 0 A_PlaySound ("Weapons/PlasmaGun/Fire", CHAN_Weapon)
        TNT1 A 0 A_FireCustomMissile ("S7_PlasmaGunRail", fRandom [weaponSpread] (-2.5, 2.5), 0, 5, -7, 0, fRandom [weaponSpread] (-2.5, 2.5))
        PLSG A 1 offset (4, 36) A_GunFlash ("Flash.Fire")
        PLSG D 1 offset (3, 35)
        PLSG C 1 offset (2, 34)
        PLSG B 1 offset (1, 33)
        PLSG A 1 A_WeaponReady (WRF_NoBob | WRF_NoSecondary | WRF_DisableSwitch)
        goto Ready2

    Flash.Fire:
        PLSF A 1 bright offset (4, 36)
        stop

    AltFire:
        TNT1 AAAA 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        PLSG A 1 offset (8, 40) A_GunFlash ("Flash.AltFire")
        PLSG D 1 offset (6, 38)
        PLSG F 1 offset (4, 36)
        PLSG F 1 offset (2, 34)
        PLSG F 6
        PLSG EDCBA 1
        goto Ready2

    Flash.AltFire:
        PLSF B 1 bright offset (8, 40)
        stop

    ChangeFireMode:
        TNT1 A 0 A_PlaySound ("Weapons/ModeChange", CHAN_Weapon)
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 99999)
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_Semi", 1, "ToFullAuto")
        TNT1 A 0 A_GiveInventory ("S7_PlasmaGun_Semi", 1)
        PLSG A 5
        goto Ready2
    ToFullAuto:
        TNT1 A 0 A_TakeInventory ("S7_PlasmaGun_Semi", 99999)
        PLSG A 5
        goto Ready2
    }
}

actor S7_PlasmaGunRail : fastProjectile {
    radius 2
    height 2
    speed 25
    health 180
    projectile
    renderStyle add
    +noDamageThrust
    scale 0.015
    damage (random [weaponDamage] (1, 5) * 13)
    missileType "S7_PlasmaGunRailEffectSpawner"
    decal Bullet
    alpha 0.5

    var int user_ticCount;

    states {
    Spawn:
        TNT1 AA 0 A_ScaleVelocity (1.0 / speed)
        TNT1 A 0 A_ScaleVelocity (health)
    Idle:
        PLGF A 1 bright
        TNT1 A 0 A_SpawnItemEx ("S7_PlasmaGunRailShockwaveTrail", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_Clientside)
        TNT1 A 0 A_SpawnItemEx ("S7_PlasmaGunRailShockwaveTrail2", -(health / 2.0), 0.0, -(velZ / 2.0), 0.0, 0.0, 0.0, 0, SXF_Clientside)
        goto Idle
    }
}

actor S7_PlasmaGunRailSmokeTrail : S7_WhiteSmoke { +noInteraction scale 0.05 alpha 0.5 }

actor S7_PlasmaGunRailShockwaveTrail {
    renderStyle add
    scale 0.05
    alpha 0.5
    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +clientsideOnly
    states {
    Spawn:
        SSHK ABCDEFGHIJKLMNOPQR 1
        stop
    }
}
actor S7_PlasmaGunRailShockwaveTrail2 : S7_PlasmaGunRailShockwaveTrail {
    states {
    Spawn:
        SSHK DEFGHIJKLMNOPQR 1
        stop
    }
}

actor S7_PlasmaGunRailEffectSpawner {
    +noBlockmap +noGravity +noTeleport +noInteraction
    states {
    Spawn:
        TNT1 A 0 noDelay A_SpawnItemEx ("S7_PlasmaGunRailTrail", 0.0, 0.0, 8.0, 0.0, 0.0, 0.0, 0, SXF_Clientside)
        TNT1 A 0 A_SpawnItemEx ("S7_PlasmaGunRailSmokeTrail", 0.0 + fRandom [sfx] (-0.8, 0.8), 0.0 + fRandom [sfx] (-0.8, 0.8), 8.0 + fRandom [sfx] (-0.8, 0.8), 0.0, 0.0, 0.0, 0, SXF_Clientside)
        TNT1 A 1
        stop
    }
}

actor S7_PlasmaGunRailTrail {
    renderStyle add
    scale 0.015
    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +clientsideOnly
    states {
    Spawn:
        PLGF A 2 bright
        //"----" A 0 A_ChangeVelocity (fRandom [sfx] (-3.0, 3.0), fRandom [sfx] (-3.0, 3.0), fRandom [sfx] (-3.0, 3.0), CVF_Replace | CVF_Relative)
        "----" A 1 bright A_FadeOut (0.05)
        wait
    }
}