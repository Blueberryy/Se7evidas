actor S7_PlasmaGun_Semi          : boolean { }
actor S7_PlasmaGun_Charge        : counter { inventory.MaxAmount 4 }
actor S7_PlasmaGun_ChargingLevel : counter { inventory.MaxAmount 16 }
actor S7_PlasmaGunMag            : ammo {
    inventory.MaxAmount 36
    ammo.BackpackMaxAmount 36
    +ignoreSkill
}

actor S7_PlasmaGun : S7_BaseWeapon 30003 {
    inventory.PickupMessage "$PLASMAGUN"
    tag "$PLASMAGUN"
    weapon.AmmoType1 "S7_PlasmaGunMag"
    weapon.AmmoType2 "S7_Cells"
    weapon.AmmoUse 1
    weapon.AmmoGive 0
    states {
    Spawn:
        PLSG Z -1
        stop
    
    Ready:
        //PLSG XYZ 1
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_ChangeFireMode", 1, "ChangeFireMode")
        PLSG A 1 A_WeaponReady
        loop

    TrueDeselect:
        //PLSG ZYX 1
        TNT1 A 0 A_Lower
        wait
    
    Fire:
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_Charge", 1, "Fire.Charged")
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_Semi", 1, "Fire.Semi")
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        TNT1 A 0 A_PlaySound ("Weapons/PlasmaGun/Fire", CHAN_Weapon)
        TNT1 A 0 A_FireCustomMissile ("S7_PlasmaGunRail", fRandom [weaponSpread] (-3.35, 3.35), 1, 6, -4, 0, fRandom [weaponSpread] (-3.35, 3.35))
        PLSG A 1 offset (4, 36) A_GunFlash ("Flash.Fire")
        PLSG D 1 offset (3, 35)
        PLSG C 1 offset (2, 34)
        PLSG B 1 offset (1, 33)
        PLSG A 1 A_Refire
        goto Ready2

    Fire.Semi:
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        TNT1 A 0 A_PlaySound ("Weapons/PlasmaGun/Fire", CHAN_Weapon)
        TNT1 A 0 A_FireCustomMissile ("S7_PlasmaGunRail", fRandom [weaponSpread] (-3.35, 3.35), 1, 6, -4, 0, fRandom [weaponSpread] (-3.35, 3.35))
        PLSG A 1 offset (4, 36) A_GunFlash ("Flash.Fire")
        PLSG D 1 offset (3, 35)
        PLSG C 1 offset (2, 34)
        PLSG B 1 offset (1, 33)
        PLSG A 1 A_WeaponReady (WRF_NoBob | WRF_NoSecondary | WRF_DisableSwitch)
        goto Ready2
    
    DryFire:
        TNT1 A 0 A_PlaySound ("Weapons/Misc/Dryfire", 7)
        TNT1 A 0 A_WeaponReady (WRF_NoBob | WRF_NoFire | WRF_NoSwitch)
        PLSG A 3
        TNT1 A 0 A_JumpIf (callACS ("S7_GetAutoReloading"), "Reload")
        goto Ready2
    Reload:
        PLSG A 1 A_TakeInventory ("S7_Reloading", 1)
        goto Ready2

    Fire.Charged:
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_Charge", 4, "Fire.ChargeLevel4")
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_Charge", 3, "Fire.ChargeLevel3")
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_Charge", 2, "Fire.ChargeLevel2")
    Fire.ChargeLevel1:
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        TNT1 A 0 A_FireCustomMissile ("S7_PlasmaGunRailCharge1", 0.0, 1, 6, -4, 0, 0.0)
        goto Fire.Charged.Finish
    Fire.ChargeLevel2:
        TNT1 A 0 A_SetBlend ("10 6B DA", 0.2333, 8)
        TNT1 AA 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        TNT1 A 0 A_FireCustomMissile ("S7_PlasmaGunRailCharge2", 0.0, 1, 6, -4, 0, 0.0)
        goto Fire.Charged.Finish
    Fire.ChargeLevel3:
        TNT1 A 0 A_SetBlend ("10 6B DA", 0.4666, 8)
        TNT1 AAA 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        TNT1 A 0 A_FireCustomMissile ("S7_PlasmaGunRailCharge3", 0.0, 1, 6, -4, 0, 0.0)
        goto Fire.Charged.Finish
    Fire.ChargeLevel4:
        TNT1 A 0 A_SetBlend ("10 6B DA", 0.7, 8)
        TNT1 AAAA 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        TNT1 A 0 A_FireCustomMissile ("S7_PlasmaGunRailCharge4", 0.0, 1, 6, -4, 0, 0.0)
        goto Fire.Charged.Finish
    Fire.Charged.Finish:
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 3)
        TNT1 A 0 A_TakeInventory ("S7_PlasmaGun_Charge", 0x7FFFFFFF)
        PLSG A 1 offset (8, 40) A_GunFlash ("Flash.Charged")
        PLSG D 1 offset (6, 38)
        PLSG F 1 offset (4, 36)
        PLSG F 1 offset (2, 34)
        PLSG F 6
        PLSG EDCBA 1
        goto Ready2

    Flash.Fire:
        PLSF A 1 bright offset (4, 36)
        stop

    AltFire:
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_Charge", 0, "Ready2")
        PLSG A 5
        PLSG A 1 A_Refire ("Charge")
        goto Ready2
    Charge:
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_ChargingLevel", 0, "ChargingFinished")
        TNT1 A 0 A_Jump (256, "ChargeA", "ChargeB", "ChargeC", "ChargeD", "ChargeE")
        wait
    ChargeA:
        PLSG A 1
        goto ChargeContinue
    ChargeB:
        PLSG G 1
        goto ChargeContinue
    ChargeC:
        PLSG H 1
        goto ChargeContinue
    ChargeD:
        PLSG I 1
        goto ChargeContinue
    ChargeE:
        PLSG J 1
        goto ChargeContinue
    ChargeContinue:
        TNT1 A 0 A_GiveInventory ("S7_PlasmaGun_ChargingLevel", 1)
        PLSG A 1 A_Refire ("Charge")
        goto CancelCharging
    CancelCharging:
        PLSG A 1
        TNT1 A 0 A_TakeInventory ("S7_PlasmaGun_ChargingLevel", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_ChargingLevel", 1, "CancelCharging")
        goto Ready2
    ChargingFinished:
        TNT1 A 0 A_TakeInventory ("S7_PlasmaGun_ChargingLevel", 0x7FFFFFFF)
        TNT1 A 0 A_GiveInventory ("S7_PlasmaGun_Charge", 1)
        TNT1 A 0 A_PlaySound ("Weapons/PlasmaGun/ChargeFinish")
        PLSG A 5 A_SetBlend ("10 6B DA", 0.25, 5)
        goto Ready2

    Flash.Charged:
        PLSF B 1 bright offset (8, 40)
        stop

    ChangeFireMode:
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 0x7FFFFFFF)
        TNT1 A 0 A_JumpIfInventory ("S7_PlasmaGun_Semi", 1, "ToFullAuto")
        TNT1 A 0 A_GiveInventory ("S7_PlasmaGun_Semi", 1)
        goto ChangeFireMode.Animation
    ToFullAuto:
        TNT1 A 0 A_TakeInventory ("S7_PlasmaGun_Semi", 0x7FFFFFFF)
        goto ChangeFireMode.Animation
    ChangeFireMode.Animation:
        PLSG A 1 offset (4, 36) A_PlaySound ("Weapons/ModeChange", CHAN_Weapon)
        PLSG A 1 offset (3, 35)
        PLSG A 1 offset (2, 34)
        PLSG A 1 offset (1, 33)
        PLSG A 1 A_WeaponReady (WRF_NoBob | WRF_NoSecondary | WRF_DisableSwitch)
        goto Ready2
    }
}

actor S7_PlasmaGunRail : fastProjectile {
    radius 2
    height 2
    speed 25
    health 150
    projectile
    renderStyle add
    scale 0.015
    damage (random [weaponDamage] (1, 5) * 13)
    missileType "S7_PlasmaGunRailEffectSpawner"
    decal Bullet
    alpha 0.5

    +noDamageThrust +forceXYBillboard
    states {
    Spawn:
        TNT1 AA 0 A_ScaleVelocity (1.0 / (25 * 1.0))
        TNT1 A 0 A_ScaleVelocity (health)
        TNT1 A 0 A_Jump (256, "Idle")
        wait
    Idle:
        PLGF A 1 bright
        TNT1 A 0 A_Jump (256, "SpawnTrails")
        wait
    SpawnTrails:
        TNT1 A 0 A_SpawnItemEx ("S7_PlasmaGunRailShockwaveTrail", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_Clientside)
        TNT1 A 0 A_SpawnItemEx ("S7_PlasmaGunRailShockwaveTrail2", -(health / 2.0), 0.0, -(velZ / 2.0), 0.0, 0.0, 0.0, 0, SXF_Clientside)
        TNT1 A 0 A_Jump (256, "Idle")
        wait
    }
}

actor S7_PlasmaGunRailCharge1 : S7_PlasmaGunRail {
    health 170
    scale 0.03
    damage (21 + random [weaponDamage] (-1, 1))
    missileType "S7_PlasmaGunRailEffectSpawnerCharge1"
    alpha 0.8

    +ripper
    states {
    SpawnTrails:
        TNT1 A 0 A_SpawnItemEx ("S7_PlasmaGunRailShockwaveTrailCharge1", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_Clientside)
        TNT1 A 0 A_SpawnItemEx ("S7_PlasmaGunRailShockwaveTrail2Charge1", -(health / 2.0), 0.0, -(velZ / 2.0), 0.0, 0.0, 0.0, 0, SXF_Clientside)
        goto Idle
    }
}
actor S7_PlasmaGunRailCharge2 : S7_PlasmaGunRailCharge1 { health 175 damage (24 + random [weaponDamage] (-1, 1)) }
actor S7_PlasmaGunRailCharge3 : S7_PlasmaGunRailCharge1 {
    health 180
    scale 0.06
    damage (27 + random [weaponDamage] (-1, 1))
    missileType "S7_PlasmaGunRailEffectSpawnerCharge2"
    decal Scorch
    alpha 0.9

    states {
    SpawnTrails:
        TNT1 A 0 A_SpawnItemEx ("S7_PlasmaGunRailShockwaveTrailCharge2", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_Clientside)
        TNT1 A 0 A_SpawnItemEx ("S7_PlasmaGunRailShockwaveTrail2Charge2", -(health / 2.0), 0.0, -(velZ / 2.0), 0.0, 0.0, 0.0, 0, SXF_Clientside)
        goto Idle
    }
}
actor S7_PlasmaGunRailCharge4 : S7_PlasmaGunRailCharge3 { health 185 damage (30 + random [weaponDamage] (-1, 1)) }

actor S7_PlasmaGunRailSmokeTrail        : S7_WhiteSmoke { scale 0.05 alpha 0.6 +noInteraction }
actor S7_PlasmaGunRailSmokeTrailCharge1 : S7_PlasmaGunRailSmokeTrail { scale 0.06 alpha 0.8 }
actor S7_PlasmaGunRailSmokeTrailCharge2 : S7_PlasmaGunRailSmokeTrail { scale 0.07 alpha 0.9 }

actor S7_PlasmaGunRailShockwaveTrail {
    renderStyle add
    scale 0.05
    alpha 0.8

    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +clientsideOnly
    states {
    Spawn:
        SSHK ABCDEFGHIJKLMNOPQR 1
        stop
    }
}
actor S7_PlasmaGunRailShockwaveTrail2 : S7_PlasmaGunRailShockwaveTrail {
    states {
    Spawn:
        SSHK DEFGHIJKLMNOPQR 1
        stop
    }
}
actor S7_PlasmaGunRailShockwaveTrailCharge1  : S7_PlasmaGunRailShockwaveTrail { scale 0.08 alpha 0.9 }
actor S7_PlasmaGunRailShockwaveTrail2Charge1 : S7_PlasmaGunRailShockwaveTrail { scale 0.08 alpha 0.9 }
actor S7_PlasmaGunRailShockwaveTrailCharge2  : S7_PlasmaGunRailShockwaveTrail { scale 0.2  alpha 1.0 }
actor S7_PlasmaGunRailShockwaveTrail2Charge2 : S7_PlasmaGunRailShockwaveTrail { scale 0.2  alpha 1.0 }

actor S7_PlasmaGunRailEffectSpawner {
    +noBlockmap +noGravity +noTeleport +noInteraction
    states {
    Spawn:
        TNT1 A 0 noDelay A_SpawnItemEx ("S7_PlasmaGunRailTrail", 0.0, 0.0, 8.0, 0.0, 0.0, 0.0, 0, SXF_Clientside)
        TNT1 A 0 A_SpawnItemEx ("S7_PlasmaGunRailSmokeTrail", 0.0 + fRandom [sfx] (-0.8, 0.8), 0.0 + fRandom [sfx] (-0.8, 0.8), 8.0 + fRandom [sfx] (-0.8, 0.8), 0.0, 0.0, 0.0, 0, SXF_Clientside)
        TNT1 A 1
        stop
    }
}
actor S7_PlasmaGunRailEffectSpawnerCharge1 : S7_PlasmaGunRailEffectSpawner {
    states {
    Spawn:
        TNT1 A 0 noDelay A_SpawnItemEx ("S7_PlasmaGunRailTrailCharge1", 0.0, 0.0, 8.0, 0.0, 0.0, 0.0, 0, SXF_Clientside)
        TNT1 A 0 A_SpawnItemEx ("S7_PlasmaGunRailSmokeTrailCharge1", 0.0 + fRandom [sfx] (-0.8, 0.8), 0.0 + fRandom [sfx] (-0.8, 0.8), 8.0 + fRandom [sfx] (-0.8, 0.8), 0.0, 0.0, 0.0, 0, SXF_Clientside)
        TNT1 A 1
        stop
    }
}
actor S7_PlasmaGunRailEffectSpawnerCharge2 : S7_PlasmaGunRailEffectSpawner {
    states {
    Spawn:
        TNT1 A 0 noDelay A_SpawnItemEx ("S7_PlasmaGunRailTrailCharge2", 0.0, 0.0, 8.0, 0.0, 0.0, 0.0, 0, SXF_Clientside)
        TNT1 A 0 A_SpawnItemEx ("S7_PlasmaGunRailSmokeTrailCharge2", 0.0 + fRandom [sfx] (-0.8, 0.8), 0.0 + fRandom [sfx] (-0.8, 0.8), 8.0 + fRandom [sfx] (-0.8, 0.8), 0.0, 0.0, 0.0, 0, SXF_Clientside)
        TNT1 A 1
        stop
    }
}

actor S7_PlasmaGunRailTrail {
    renderStyle add
    scale 0.015

    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +forceXYBillboard +clientsideOnly
    states {
    Spawn:
        PLGF A 1 bright
        "----" A 1 bright A_FadeOut (0.05)
        wait
    }
}
actor S7_PlasmaGunRailTrailCharge1 : S7_PlasmaGunRailTrail { scale 0.03 }
actor S7_PlasmaGunRailTrailCharge2 : S7_PlasmaGunRailTrail { scale 0.06 }