actor S7_ShotgunMag : ammo {
    inventory.MaxAmount 8
    ammo.BackpackMaxAmount 8
    +ignoreSkill
}

actor S7_ShotgunLoaded : ammo {
    inventory.MaxAmount 2
    ammo.BackpackMaxAmount 2
    +ignoreSkill
}

actor S7_ShotgunDontPump : boolean { }
actor S7_Shotgun : S7_BaseWeapon 30001 {
    inventory.PickupMessage "$SHOTGUN"
    tag "$SHOTGUN"
    weapon.AmmoType1 "S7_ShotgunMag"
    weapon.AmmoType2 "S7_Shells"
    weapon.AmmoUse 0
    weapon.AmmoGive 12
    states {
    Spawn:
        DTUP A -1
        stop
    
    Ready:
    Ready2:
        TNT1 A 0 A_takeInventory ("S7_ShotgunDontPump", 1)
        TNT1 A 0 A_jumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_jumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_takeInventory ("S7_ChangeFireMode", 1)
        SSGR A 1 A_weaponReady
        loop
    
    Select:
        TNT1 A 0 A_raise
        wait
    
    Deselect:
        TNT1 A 0 A_jumpIfHealthLower (1, "Deselect.DEAD")
        TNT1 A 0 A_lower
        loop
    
    Fire:
        TNT1 A 0 A_jumpIfInventory ("S7_ShotgunLoaded", 2, "TrueFire")
        TNT1 A 0 A_jumpIfInventory ("S7_ShotgunLoaded", 1, "AltFireRight")
        goto Pump
    TrueFire:
        TNT1 A 0 A_jumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_playSound ("Weapons/Shotgun/Fire", CHAN_Weapon)
        TNT1 A 0 A_alertMonsters
        TNT1 A 0 A_takeInventory ("S7_ShotgunLoaded", 2)
        SSGF AA 0 A_fireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 4)
        SSGF AA 0 A_fireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 0, 4)
        SSGF AAAAAAAAA 0 A_fireCustomMissile ("S7_SSGTracer", fRandom (-3.8, 3.8), 0, 2, -1, 0, fRandom (-4.3, 4.3))
        SSGF AAAAAAAA 0 A_fireCustomMissile ("S7_SSGTracer", fRandom (-3.8, 3.8), 0, 3, -1, 0, fRandom (-4.3, 4.3))
        SSGF A 2 A_fireCustomMissile ("S7_SSGTracer", fRandom (-3.8, 3.8), 1, 3, -1, 0, fRandom (-4.3, 4.3))
        SSGF EFG 1 A_setPitch (pitch - 1.5)
        SSGR A 6
        goto Ready2
    
    AltFire:
        TNT1 A 0 A_jumpIfInventory ("S7_ShotgunLoaded", 1, 1)
        goto Pump
        TNT1 A 0 A_jumpIfInventory ("S7_ShotgunLoaded", 2, 1)
        goto AltFireRight
        TNT1 A 0 A_playSound ("Weapons/Shotgun/AltFire", 5)
        TNT1 A 0 A_alertMonsters
        TNT1 A 0 A_takeInventory ("S7_ShotgunLoaded", 1)
        SSGF BB 0 A_fireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 0, 4)
        SSGF BBBBBBBB 0 A_fireCustomMissile ("S7_SSGTracer", fRandom (-3.8, 3.8), 0, 2, -1, 0, fRandom (-4.3, 4.3))
        SSGF B 2 A_fireCustomMissile ("S7_SSGTracer", fRandom (-3.8, 3.8), 1, 2, -1, 0, fRandom (-4.3, 4.3))
        SSGF FG 1 A_setPitch (pitch - 0.75)
        SSGR A 3
        goto Ready2
    
    AltFireRight:
        TNT1 A 0 A_playSound ("Weapons/Shotgun/AltFire", 6)
        TNT1 A 0 A_alertMonsters
        TNT1 A 0 A_takeInventory ("S7_ShotgunLoaded", 1)
        SSGF CC 0 A_fireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 4)
        SSGF CCCCCCCC 0 A_fireCustomMissile ("S7_SSGTracer", fRandom (-3.8, 3.8), 0, 3, -1, 0, fRandom (-4.3, 4.3))
        SSGF C 2 A_fireCustomMissile ("S7_SSGTracer", fRandom (-3.8, 3.8), 1, 3, -1, 0, fRandom (-4.3, 4.3))
        SSGF FG 1 A_setPitch (pitch - 0.75)
        SSGR A 3
        goto Ready2
    
    Pump:
        TNT1 A 0 A_jumpIfInventory ("S7_ShotgunMag", 1, 1)
        goto DryFire
        TNT1 A 0 A_giveInventory ("S7_ShotgunLoaded", 2)
        SSGF G 2 A_takeInventory ("S7_ShotgunMag", 2)
        SSGF H 2
        TNT1 A 0 A_playSound ("Weapons/Shotgun/Pump", 7)
        SSGF IJKLMN 2
        SSGF MLKJIH 2
        goto Ready2
    
    DryFire:
        TNT1 A 0 A_playSound ("Weapons/Misc/Dryfire", 7)
        SSGR A 3
    
    DryReload:
        TNT1 A 0 A_giveInventory ("S7_Reloading", 1)
    Reload:
        TNT1 A 0 A_jumpIfInventory ("S7_Reloading", 1, 1)
        goto DontReload
        TNT1 A 0 A_jumpIfInventory ("S7_ShotgunMag", 0, "DontReload")
        TNT1 A 0 A_jumpIfInventory ("S7_Shells", 2, 1)
        goto DontReload
        SSGR A 5
        TNT1 A 0 A_jumpIfInventory ("S7_ShotgunLoaded", 2, "InsertShells")
        TNT1 A 0 A_jumpIfInventory ("S7_ShotgunLoaded", 1, "InsertShells.Pump")
        goto InsertFirstShells
    
    InsertShells:
        TNT1 A 0 A_jumpIfInventory ("S7_Reloading", 1, 1)
        goto Reloaded
        TNT1 A 0 A_jumpIfInventory ("S7_ShotgunMag", 0, "Reloaded")
        TNT1 A 0 A_jumpIfInventory ("S7_Shells", 2, 1)
        goto Reloaded
        TNT1 A 0 A_giveInventory ("S7_ShotgunMag", 2)
        TNT1 A 0 A_takeInventory ("S7_Shells", 2)
        TNT1 A 0 A_playSound ("Weapons/Shotgun/ShellIn", CHAN_Weapon)
        DSGR ABCCCCBA 1
        TNT1 A 0 A_playSound ("Weapons/Shotgun/ShellIn", 5)
        DSGR ABCCCCBAAAAAAAAAAAAA 1
        goto InsertShells
    
    InsertFirstShells:
        TNT1 A 0 A_jumpIfInventory ("S7_Reloading", 1, 1)
        goto Reloaded
        TNT1 A 0 A_jumpIfInventory ("S7_ShotgunMag", 0, "Reloaded")
        TNT1 A 0 A_jumpIfInventory ("S7_Shells", 2, 1)
        goto Reloaded
        TNT1 A 0 A_giveInventory ("S7_ShotgunMag", 2)
        TNT1 A 0 A_takeInventory ("S7_Shells", 2)
        TNT1 A 0 A_playSound ("Weapons/Shotgun/ShellIn", CHAN_Weapon)
        DSGR ABCCCCBA 1
        TNT1 A 0 A_playSound ("Weapons/Shotgun/ShellIn", 5)
        DSGR ABCCCCBAAAAAAAAAAAAA 1
        TNT1 A 0 A_giveInventory ("S7_ShotgunLoaded", 2)
        SSGF G 2 A_takeInventory ("S7_ShotgunMag", 2)
        SSGF H 2
        TNT1 A 0 A_playSound ("Weapons/Shotgun/Pump", 6)
        SSGF IJKLMN 2
        SSGF MLKJIH 2
        goto InsertShells
    
    InsertShells.Pump:
        TNT1 A 0 A_jumpIfInventory ("S7_ShotgunMag", 2, "InsertShells.PumpLoad")
        TNT1 A 0 A_jumpIfInventory ("S7_Shells", 0, "InsertShells.Pump2")
        TNT1 A 0 A_giveInventory ("S7_Shells", 1)
    
    InsertShells.Pump2:
        
    InsertShells.PumpAnim:
        SSGF G 2 A_takeInventory ("S7_ShotgunLoaded", 1)
        SSGF H 2
        TNT1 A 0 A_playSound ("Weapons/Shotgun/Pump", CHAN_Weapon)
        SSGF IJKLMN 2
        SSGF MLKJIH 2
        goto InsertFirstShells
    
    InsertShells.PumpLoad:
        TNT1 A 0 A_jumpIfInventory ("S7_ShotgunMag", 2, 1)
        goto InsertShells.PumpLoad2
        TNT1 A 0 A_giveInventory ("S7_ShotgunDontPump", 1)
    InsertShells.PumpLoad2:
        TNT1 A 0 A_giveInventory ("S7_ShotgunLoaded", 2)
        SSGF G 2 A_takeInventory ("S7_ShotgunMag", 2)
        SSGF H 2
        TNT1 A 0 A_playSound ("Weapons/Shotgun/Pump", 7)
        SSGF IJKLMN 2
        SSGF MLKJIH 2
        TNT1 A 0 A_jumpIfInventory ("S7_ShotgunDontPump", 1, "InsertShells")
        goto InsertFirstShells
    
    Reloaded:
    DontReload:
        TNT1 A 0 A_takeInventory ("S7_ShotgunDontPump", 1)
        TNT1 A 0 A_takeInventory ("S7_Reloading", 1)
        goto Ready2
    }
}