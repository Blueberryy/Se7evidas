actor S7_ShotgunMag : ammo {
    inventory.MaxAmount 8
    ammo.BackpackMaxAmount 8
    +ignoreSkill
}

actor S7_ShotgunLoaded : ammo {
    inventory.MaxAmount 2
    ammo.BackpackMaxAmount 2
    +ignoreSkill
}

actor S7_ShotgunDontPump : boolean { }
actor S7_Shotgun : S7_BaseWeapon 30000 {
    inventory.PickupMessage "$SHOTGUN"
    tag "$SHOTGUN"
    weapon.AmmoType1 "S7_ShotgunMag"
    weapon.AmmoType2 "S7_20gaShells"
    weapon.AmmoUse 0
    weapon.AmmoGive 12
    states {
    Spawn:
        DTUP A -1
        stop
    
    Ready:
    Ready2:
        TNT1 A 0 a_takeInventory ("S7_ShotgunDontPump", 1)
        TNT1 A 0 a_jumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 a_jumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 a_takeInventory ("S7_ChangeFireMode", 1)
        SSGR A 1 a_weaponReady
        loop
    
    /*Select:
        TNT1 A 0 a_raise
        wait
    
    Deselect:
        TNT1 A 0 a_jumpIfHealthLower (1, "Deselect.DEAD")
        TNT1 A 0 a_lower
        loop*/
    
    Fire:
        TNT1 A 0 a_jumpIfInventory ("S7_ShotgunLoaded", 2, "TrueFire")
        TNT1 A 0 a_jumpIfInventory ("S7_ShotgunLoaded", 1, "AltFireRight")
        goto Pump
    TrueFire:
        TNT1 A 0 a_jumpIfNoAmmo ("DryFire")
        TNT1 A 0 a_playSound ("Weapons/Shotgun/Fire", CHAN_Weapon)
        TNT1 A 0 a_alertMonsters
        TNT1 A 0 a_takeInventory ("S7_ShotgunLoaded", 2)
        TNT1 AA 0 a_fireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 4)
        TNT1 AA 0 a_fireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 0, 4)
        TNT1 AAAAAAAAA 0 a_fireCustomMissile ("S7_SSGTracer", fRandom (-3.8, 3.8), 0, 2, -1, 0, fRandom (-4.3, 4.3))
        TNT1 AAAAAAAA 0 a_fireCustomMissile ("S7_SSGTracer", fRandom (-3.8, 3.8), 0, 3, -1, 0, fRandom (-4.3, 4.3))
        TNT1 A 0 a_fireCustomMissile ("S7_SSGTracer", fRandom (-3.8, 3.8), 1, 3, -1, 0, fRandom (-4.3, 4.3))
        SSGF AAE 1 a_setPitch (pitch - 1.5)
        SSGF FG 1
        SSGR A 6
        goto Ready2
    
    AltFire:
        TNT1 A 0 a_jumpIfInventory ("S7_ShotgunLoaded", 1, 1)
        goto Pump
        TNT1 A 0 a_jumpIfInventory ("S7_ShotgunLoaded", 2, 1)
        goto AltFireRight
        TNT1 A 0 a_playSound ("Weapons/Shotgun/AltFire", 5)
        TNT1 A 0 a_alertMonsters
        TNT1 A 0 a_takeInventory ("S7_ShotgunLoaded", 1)
        TNT1 AA 0 a_fireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 0, 4)
        TNT1 AAAAAAAA 0 a_fireCustomMissile ("S7_SSGTracer", fRandom (-3.8, 3.8), 0, 2, -1, 0, fRandom (-4.3, 4.3))
        TNT1 A 0 a_fireCustomMissile ("S7_SSGTracer", fRandom (-3.8, 3.8), 1, 2, -1, 0, fRandom (-4.3, 4.3))
        SSGF BB 1 a_setPitch (pitch - 0.75)
        SSGF FG 1
        SSGR A 3
        goto Ready2
    
    AltFireRight:
        TNT1 A 0 a_playSound ("Weapons/Shotgun/AltFire", 6)
        TNT1 A 0 a_alertMonsters
        TNT1 A 0 a_takeInventory ("S7_ShotgunLoaded", 1)
        TNT1 AA 0 a_fireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 4)
        TNT1 AAAAAAAA 0 a_fireCustomMissile ("S7_SSGTracer", fRandom (-3.8, 3.8), 0, 3, -1, 0, fRandom (-4.3, 4.3))
        TNT1 A 0 a_fireCustomMissile ("S7_SSGTracer", fRandom (-3.8, 3.8), 1, 3, -1, 0, fRandom (-4.3, 4.3))
        SSGF CC 1 a_setPitch (pitch - 0.75)
        SSGF FG 1
        SSGR A 3
        goto Ready2
    
    Pump:
        TNT1 A 0 a_jumpIfInventory ("S7_ShotgunMag", 1, 1)
        goto DryFire
        TNT1 A 0 a_giveInventory ("S7_ShotgunLoaded", 2)
        SSGF G 2 a_takeInventory ("S7_ShotgunMag", 2)
        SSGF H 2
        TNT1 AA 0 a_fireCustomMissile ("S7_ShellCasingSpawn", 0, 0, 6, -3)
        TNT1 A 0 a_playSound ("Weapons/Shotgun/Pump", 7)
        SSGF IJKLMN 2
        SSGF MLKJIH 2
        goto Ready2
    
    DryFire:
        TNT1 A 0 a_playSound ("Weapons/Misc/Dryfire", 7)
        SSGR A 3
        TNT1 A 0 a_jumpIf (callACS ("S7_GetAutoReloading"), "DryReload")
        goto Ready2
    
    DryReload:
        TNT1 A 0 a_giveInventory ("S7_Reloading", 1)
    Reload:
        TNT1 A 0 a_jumpIfInventory ("S7_Reloading", 1, 1)
        goto DontReload
        TNT1 A 0 a_jumpIfInventory ("S7_ShotgunMag", 0, "DontReload")
        TNT1 A 0 a_jumpIfInventory ("S7_20gaShells", 2, 1)
        goto DontReload
        SSGR A 5
        TNT1 A 0 a_jumpIfInventory ("S7_ShotgunLoaded", 2, "InsertShells")
        TNT1 A 0 a_jumpIfInventory ("S7_ShotgunLoaded", 1, "InsertShells.Pump")
        goto InsertFirstShells
    
    InsertShells:
        TNT1 A 0 a_jumpIfInventory ("S7_Reloading", 1, 1)
        goto Reloaded
        TNT1 A 0 a_jumpIfInventory ("S7_ShotgunMag", 0, "Reloaded")
        TNT1 A 0 a_jumpIfInventory ("S7_20gaShells", 2, 1)
        goto Reloaded
        TNT1 A 0 a_giveInventory ("S7_ShotgunMag", 2)
        TNT1 A 0 a_takeInventory ("S7_20gaShells", 2)
        TNT1 A 0 a_playSound ("Weapons/Shotgun/ShellIn", CHAN_Weapon)
        DSGR ABCCCCBA 1
        TNT1 A 0 a_playSound ("Weapons/Shotgun/ShellIn", 5)
        DSGR ABCCCCBAAAAAAAAAAAAA 1
        goto InsertShells
    
    InsertFirstShells:
        TNT1 A 0 a_jumpIfInventory ("S7_Reloading", 1, 1)
        goto Reloaded
        TNT1 A 0 a_jumpIfInventory ("S7_ShotgunMag", 0, "Reloaded")
        TNT1 A 0 a_jumpIfInventory ("S7_20gaShells", 2, 1)
        goto Reloaded
        TNT1 A 0 a_giveInventory ("S7_ShotgunMag", 2)
        TNT1 A 0 a_takeInventory ("S7_20gaShells", 2)
        TNT1 A 0 a_playSound ("Weapons/Shotgun/ShellIn", CHAN_Weapon)
        DSGR ABCCCCBA 1
        TNT1 A 0 a_playSound ("Weapons/Shotgun/ShellIn", 5)
        DSGR ABCCCCBAAAAAAAAAAAAA 1
        TNT1 A 0 a_giveInventory ("S7_ShotgunLoaded", 2)
        SSGF G 2 a_takeInventory ("S7_ShotgunMag", 2)
        SSGF H 2
        TNT1 A 0 a_playSound ("Weapons/Shotgun/Pump", 6)
        SSGF IJKLMN 2
        SSGF MLKJIH 2
        goto InsertShells
    
    InsertShells.Pump:
        TNT1 A 0 a_jumpIfInventory ("S7_ShotgunMag", 2, "InsertShells.PumpLoad")
        TNT1 A 0 a_jumpIfInventory ("S7_20gaShells", 0, "InsertShells.Pump2")
        TNT1 A 0 a_giveInventory ("S7_20gaShells", 1)
    
    InsertShells.Pump2:
        
    InsertShells.PumpAnim:
        SSGF G 2 a_takeInventory ("S7_ShotgunLoaded", 1)
        SSGF H 2
        TNT1 A 0 a_playSound ("Weapons/Shotgun/Pump", CHAN_Weapon)
        SSGF IJKLMN 2
        SSGF MLKJIH 2
        goto InsertFirstShells
    
    InsertShells.PumpLoad:
        TNT1 A 0 a_jumpIfInventory ("S7_ShotgunMag", 2, 1)
        goto InsertShells.PumpLoad2
        TNT1 A 0 a_giveInventory ("S7_ShotgunDontPump", 1)
    InsertShells.PumpLoad2:
        TNT1 A 0 a_giveInventory ("S7_ShotgunLoaded", 2)
        SSGF G 2 a_takeInventory ("S7_ShotgunMag", 2)
        SSGF H 2
        TNT1 A 0 a_playSound ("Weapons/Shotgun/Pump", 7)
        SSGF IJKLMN 2
        SSGF MLKJIH 2
        TNT1 A 0 a_jumpIfInventory ("S7_ShotgunDontPump", 1, "InsertShells")
        goto InsertFirstShells
    
    Reloaded:
    DontReload:
        TNT1 A 0 a_takeInventory ("S7_ShotgunDontPump", 1)
        TNT1 A 0 a_takeInventory ("S7_Reloading", 1)
        goto Ready2
    }
}