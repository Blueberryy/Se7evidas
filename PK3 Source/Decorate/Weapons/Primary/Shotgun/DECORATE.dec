//-------------------------------------------------------------------------------------------
//
// AM-10 "Lucifer" Combat Shotgun
//
//-------------------------------------------------------------------------------------------
actor S7_ShotgunMagCounter : ammo {
    inventory.maxAmount 10
    ammo.backpackMaxAmount 10

    +ignoreSkill
}

actor S7_ShotgunMag : ammo {
    inventory.maxAmount 8
    ammo.backpackMaxAmount 8

    +ignoreSkill
}

actor S7_ShotgunLoaded : ammo {
    inventory.maxAmount 2
    ammo.backpackMaxAmount 2

    +ignoreSkill
}

actor S7_ShotgunDontPump : boolean { }
actor S7_Shotgun : S7_BaseWeapon 30000 {
    tag "$SHOTGUN"
    inventory.pickupMessage "$SHOTGUN"
    weapon.ammoType1 "S7_ShotgunMagCounter"
    weapon.ammoType2 "S7_20gaShells"
    weapon.ammoUse 0
    weapon.ammoGive 0

    states {
    Spawn:
        DTUP A -1
        stop

    Ready:
    Ready2:
        TNT1 A 0 A_TakeInventory ("S7_ShotgunDontPump", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 1)
        TNT1 A 0 A_GiveInventory ("S7_WeapReady", 1)
        SSGR A 1 A_WeaponReady
        loop

    Fire:
        TNT1 A 0 A_JumpIfInventory ("S7_ShotgunLoaded", 2, "FireLeft")
        TNT1 A 0 A_JumpIfInventory ("S7_ShotgunLoaded", 1, "FireRight")
        goto Pump

    FireLeft:
        TNT1 A         0 A_JumpIfInventory ("S7_ShotgunLoaded", 1, 1)
        goto Pump
        TNT1 A         0 A_JumpIfInventory ("S7_ShotgunLoaded", 2, 1)
        goto FireRight
        TNT1 A         0 A_PlaySound ("Weapons/Shotgun/Fire", CHAN_5)
        TNT1 A         0 A_AlertMonsters
        TNT1 A         0 A_GunFlash ("FlashLeft")
        TNT1 A         0 A_TakeInventory ("S7_ShotgunLoaded", 1)
        TNT1 AA        0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 1, 3)
        TNT1 AAAAAAAAA 0 A_FireCustomMissile ("S7_SSGTracer", fRandom [weaponSpread] (-0.25, 0.25), 0, 2, -1, 0, fRandom [weaponSpread] (-0.25, 0.25))
        TNT1 AAAAAAAAA 0 A_FireCustomMissile ("S7_SSGTracer", fRandom [weaponSpread] (-0.25, 0.25), 0, 2, -1, 0, fRandom [weaponSpread] (-0.25, 0.25))
        SSGF EE        1 A_SetPitch (pitch - 1.45, SPF_Interpolate)
        SSGF E         1
        SSGF F         1 A_SetPitch (pitch + 0.725, SPF_Interpolate)
        SSGR A         2 A_SetPitch (pitch + 0.725, SPF_Interpolate)
        goto Ready2

    FireRight:
        TNT1 A         0 A_PlaySound ("Weapons/Shotgun/Fire", CHAN_6)
        TNT1 A         0 A_AlertMonsters
        TNT1 A         0 A_GunFlash ("FlashRight")
        TNT1 A         0 A_TakeInventory ("S7_ShotgunLoaded", 1)
        TNT1 AA        0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 3, 2)
        TNT1 AAAAAAAAA 0 A_FireCustomMissile ("S7_SSGTracer", fRandom [weaponSpread] (-0.25, 0.25), 0, 4, -1, 0, fRandom [weaponSpread] (-0.25, 0.25))
        TNT1 AAAAAAAAA 0 A_FireCustomMissile ("S7_SSGTracer", fRandom [weaponSpread] (-0.25, 0.25), 0, 4, -1, 0, fRandom [weaponSpread] (-0.25, 0.25))
        SSGF EE        1 A_SetPitch (pitch - 1.45, SPF_Interpolate)
        SSGF E         1
        SSGF F         1 A_SetPitch (pitch + 0.725, SPF_Interpolate)
        SSGR A         2 A_SetPitch (pitch + 0.725, SPF_Interpolate)
        goto Ready2

    AltFire:
        TNT1 A 0 A_JumpIfInventory ("S7_ShotgunLoaded", 2, "TrueAltFire")
        TNT1 A 0 A_JumpIfInventory ("S7_ShotgunLoaded", 1, "FireRight")
        goto Pump
    TrueAltFire:
        TNT1 A 0 A_PlaySound ("Weapons/Shotgun/AltFire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash ("AltFlash")

        TNT1 A         0 A_TakeInventory ("S7_ShotgunLoaded", 1)
        TNT1 AA        0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 1, 3)
        TNT1 AAAAAAAAA 0 A_FireCustomMissile ("S7_SSGTracer", fRandom [weaponSpread] (-0.25, 0.25), 0, 2, -1, 0, fRandom [weaponSpread] (-0.25, 0.25))
        TNT1 AAAAAAAAA 0 A_FireCustomMissile ("S7_SSGTracer", fRandom [weaponSpread] (-0.25, 0.25), 0, 2, -1, 0, fRandom [weaponSpread] (-0.25, 0.25))
        SSGF E         1 A_SetPitch (pitch - 0.725, SPF_Interpolate)

        TNT1 A         0 A_TakeInventory ("S7_ShotgunLoaded", 1)
        TNT1 AA        0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 3, 2)
        TNT1 AAAAAAAAA 0 A_FireCustomMissile ("S7_SSGTracer", fRandom [weaponSpread] (-0.25, 0.25), 0, 4, -1, 0, fRandom [weaponSpread] (-0.25, 0.25))
        TNT1 AAAAAAAAA 0 A_FireCustomMissile ("S7_SSGTracer", fRandom [weaponSpread] (-0.25, 0.25), 0, 4, -1, 0, fRandom [weaponSpread] (-0.25, 0.25))
        SSGF D         1 A_SetPitch (pitch - 2.175, SPF_Interpolate)

        SSGF DD 1 A_SetPitch (pitch - 1.45, SPF_Interpolate)
        SSGF E  1
        SSGF F  1 A_SetPitch (pitch + 1.45, SPF_Interpolate)
        SSGR A  2 A_SetPitch (pitch + 1.45, SPF_Interpolate)
        goto Ready2

    FlashLeft:
        SSGF A 2 bright
        stop
    FlashRight:
        SSGF B 2 bright
        stop
    AltFlash:
        SSGF AC 1 bright
        stop

    Pump:
        TNT1 A    0 A_JumpIfInventory ("S7_ShotgunMag", 1, 1)
        goto DryFire
        TNT1 A    0 A_GiveInventory ("S7_ShotgunLoaded", 2)
        SSGF G    2 A_TakeInventory ("S7_ShotgunMag", 2)
        TNT1 AA   0 A_FireCustomMissile ("S7_ShellCasingSpawn", 0, 0, 6, -3)
        TNT1 A    0 A_PlaySound ("Weapons/Shotgun/Pump", CHAN_7)
        SSGF HIK  2
        SSGF JIHG 2
        SSGR A    2
        goto Ready2

    DryFire:
        SSGR A 3 A_PlaySound ("Weapons/Misc/Dryfire", CHAN_7)
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "DryReload")
        goto Ready2

    DryReload:
        TNT1 A 0 A_GiveInventory ("S7_Reloading", 1)
    Reload:
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, 1)
        goto DontReload
        TNT1 A 0 A_JumpIfInventory ("S7_ShotgunMag", 0, "DontReload")
        TNT1 A 0 A_JumpIfInventory ("S7_20gaShells", 2, 1)
        goto DontReload
        SSGR A 5
        TNT1 A 0 A_JumpIfInventory ("S7_ShotgunLoaded", 2, "InsertShells")
        TNT1 A 0 A_JumpIfInventory ("S7_ShotgunLoaded", 1, "InsertShells.Pump")
        goto InsertFirstShells

    InsertShells:
        TNT1 A        0 A_JumpIfInventory ("S7_Reloading", 1, 1)
        goto Reloaded
        TNT1 A        0 A_JumpIfInventory ("S7_ShotgunMag", 0, "Reloaded")
        TNT1 A        0 A_JumpIfInventory ("S7_20gaShells", 2, 1)
        goto Reloaded
        TNT1 A        0 A_GiveInventory ("S7_ShotgunMag", 2)
        TNT1 A        0 A_TakeInventory ("S7_20gaShells", 2)
        TNT1 A        0 A_PlaySound ("Weapons/Shotgun/ShellIn", CHAN_5)
        DSGR ABCCCCBA 1
        TNT1 A        0 A_PlaySound ("Weapons/Shotgun/ShellIn", CHAN_6)
        DSGR ABCCCCB  1
        DSGR A       13
        goto InsertShells

    InsertFirstShells:
        TNT1 A        0 A_JumpIfInventory ("S7_Reloading", 1, 1)
        goto Reloaded
        TNT1 A        0 A_JumpIfInventory ("S7_ShotgunMag", 0, "Reloaded")
        TNT1 A        0 A_JumpIfInventory ("S7_20gaShells", 2, 1)
        goto Reloaded
        TNT1 A        0 A_GiveInventory ("S7_ShotgunMag", 2)
        TNT1 A        0 A_TakeInventory ("S7_20gaShells", 2)
        TNT1 A        0 A_PlaySound ("Weapons/Shotgun/ShellIn", CHAN_5)
        DSGR ABCCCCBA 1
        TNT1 A        0 A_PlaySound ("Weapons/Shotgun/ShellIn", CHAN_6)
        DSGR ABCCCCB  1
        DSGR A       13

        TNT1 A    0 A_GiveInventory ("S7_ShotgunLoaded", 2)
        SSGF G    2 A_TakeInventory ("S7_ShotgunMag", 2)
        TNT1 A    0 A_PlaySound ("Weapons/Shotgun/Pump", CHAN_7)
        SSGF HIK  2
        SSGF JIHG 2
        SSGR A    2
        goto InsertShells

    InsertShells.Pump:
        TNT1 A 0 A_JumpIfInventory ("S7_ShotgunMag", 2, "InsertShells.PumpLoad")
        TNT1 A 0 A_JumpIfInventory ("S7_20gaShells", 0, "InsertShells.Pump2")
        TNT1 A 0 A_GiveInventory ("S7_20gaShells", 1)

    InsertShells.Pump2:

    InsertShells.PumpAnim:
        SSGF G    2 A_TakeInventory ("S7_ShotgunLoaded", 1)
        TNT1 A    0 A_PlaySound ("Weapons/Shotgun/Pump", CHAN_7)
        SSGF HIK  2
        SSGF JIHG 2
        SSGR A    2
        goto InsertFirstShells

    InsertShells.PumpLoad:
        TNT1 A 0 A_JumpIfInventory ("S7_ShotgunMag", 2, 1)
        goto InsertShells.PumpLoad2
        TNT1 A 0 A_GiveInventory ("S7_ShotgunDontPump", 1)
    InsertShells.PumpLoad2:
        TNT1 A    0 A_GiveInventory ("S7_ShotgunLoaded", 2)
        SSGF G    2 A_TakeInventory ("S7_ShotgunMag", 2)
        TNT1 A    0 A_PlaySound ("Weapons/Shotgun/Pump", CHAN_7)
        SSGF HIK  2
        SSGF JIHG 2
        SSGR A    2
        TNT1 A    0 A_JumpIfInventory ("S7_ShotgunDontPump", 1, "InsertShells")
        goto InsertFirstShells

    Reloaded:
    DontReload:
        TNT1 A 0 A_TakeInventory ("S7_ShotgunDontPump", 1)
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 1)
        goto Ready2
    }
}