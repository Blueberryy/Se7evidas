//-------------------------------------------------------------------------------------------
//
// AM-10 "Lucifer" Combat Shotgun
//
//-------------------------------------------------------------------------------------------
actor S7_ShotgunMagCounter : ammo {
    inventory.maxAmount 10
    ammo.backpackMaxAmount 10

    +ignoreSkill
}

actor S7_ShotgunMag : ammo {
    inventory.maxAmount 8
    ammo.backpackMaxAmount 8

    +ignoreSkill
}

actor S7_ShotgunLoaded : ammo {
    inventory.maxAmount 2
    ammo.backpackMaxAmount 2

    +ignoreSkill
}

actor S7_ShotgunDontPump : boolean { }
actor S7_Shotgun : S7_BaseWeapon 30000 {
    tag "$SHOTGUN"
    inventory.pickupMessage "$SHOTGUN"
    weapon.ammoType1 "S7_ShotgunMagCounter"
    weapon.ammoType2 "S7_20gaShells"
    weapon.ammoUse 0
    weapon.ammoGive 0

    states {
    Spawn:
        DTUP A -1
        stop

    Ready:
    Ready2:
        TNT1 A 0 A_TakeInventory ("S7_ShotgunDontPump", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 1)
        SSGR A 1 A_WeaponReady
        loop

    Fire:
        TNT1 A 0 A_JumpIfInventory ("S7_ShotgunLoaded", 2, "TrueFire")
        TNT1 A 0 A_JumpIfInventory ("S7_ShotgunLoaded", 1, "AltFireRight")
        goto Pump
    TrueFire:
        TNT1 A         0 A_PlaySound ("Weapons/Shotgun/Fire", CHAN_Weapon)
        TNT1 A         0 A_AlertMonsters
        TNT1 A         0 A_GunFlash ("Flash")

        TNT1 A         0 A_TakeInventory ("S7_ShotgunLoaded", 1)
        TNT1 AA        0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 0, 4)
        TNT1 AAAAAAAAA 0 A_FireCustomMissile ("S7_SSGTracer", fRandom [weaponSpread] (-2.8, 2.8), 0, 2, -1, 0, fRandom [weaponSpread] (-3.3, 3.3))
        SSGF E         1 A_SetPitch (pitch - 2.9, SPF_Interpolate)

        TNT1 A         0 A_TakeInventory ("S7_ShotgunLoaded", 1)
        TNT1 AA        0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 4)
        TNT1 AAAAAAAAA 0 A_FireCustomMissile ("S7_SSGTracer", fRandom [weaponSpread] (-2.8, 2.8), 0, 4, -1, 0, fRandom [weaponSpread] (-3.3, 3.3))
        SSGF D         1 A_SetPitch (pitch - 2.9, SPF_Interpolate)

        SSGF D         1
        SSGF DE        1 A_SetPitch (pitch + 1.45, SPF_Interpolate)
        SSGF F         1
        SSGR A         2
        goto Ready2

    AltFire:
        TNT1 A         0 A_JumpIfInventory ("S7_ShotgunLoaded", 1, 1)
        goto Pump
        TNT1 A         0 A_JumpIfInventory ("S7_ShotgunLoaded", 2, 1)
        goto AltFireRight
        TNT1 A         0 A_PlaySound ("Weapons/Shotgun/AltFire", CHAN_5)
        TNT1 A         0 A_AlertMonsters
        TNT1 A         0 A_GunFlash ("AltFlash")
        TNT1 A         0 A_TakeInventory ("S7_ShotgunLoaded", 1)
        TNT1 AA        0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 0, 4)
        TNT1 AAAAAAAAA 0 A_FireCustomMissile ("S7_SSGTracer", fRandom [weaponSpread] (-2.8, 2.8), 0, 2, -1, 0, fRandom [weaponSpread] (-3.3, 3.3))
        SSGF EE        1 A_SetPitch (pitch - 1.45, SPF_Interpolate)
        SSGF E         1
        SSGF F         1 A_SetPitch (pitch + 0.725, SPF_Interpolate)
        SSGR A         3 A_SetPitch (pitch + 0.725, SPF_Interpolate)
        goto Ready2

    AltFireRight:
        TNT1 A         0 A_PlaySound ("Weapons/Shotgun/AltFire", CHAN_6)
        TNT1 A         0 A_AlertMonsters
        TNT1 A         0 A_GunFlash ("AltFlashRight")
        TNT1 A         0 A_TakeInventory ("S7_ShotgunLoaded", 1)
        TNT1 AA        0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2, 4)
        TNT1 AAAAAAAAA 0 A_FireCustomMissile ("S7_SSGTracer", fRandom [weaponSpread] (-3.3, 3.3), 0, 4, -1, 0, fRandom [weaponSpread] (-3.8, 3.8))
        SSGF EE        1 A_SetPitch (pitch - 1.45, SPF_Interpolate)
        SSGF E         1
        SSGF F         1 A_SetPitch (pitch + 0.725, SPF_Interpolate)
        SSGR A         3 A_SetPitch (pitch + 0.725, SPF_Interpolate)
        goto Ready2

    Flash:
        SSGF AC 1 bright
        stop
    AltFlash:
        SSGF A 2 bright
        stop
    AltFlashRight:
        SSGF B 2 bright
        stop

    Pump:
        TNT1 A       0 A_JumpIfInventory ("S7_ShotgunMag", 1, 1)
        goto DryFire
        TNT1 A       0 A_GiveInventory ("S7_ShotgunLoaded", 2)
        SSGF F       2 A_TakeInventory ("S7_ShotgunMag", 2)
        SSGF G       2
        TNT1 AA      0 A_FireCustomMissile ("S7_ShellCasingSpawn", 0, 0, 6, -3)
        TNT1 A       0 A_PlaySound ("Weapons/Shotgun/Pump", CHAN_7)
        SSGF HIJKLMN 2
        SSGF MLKJIH  2
        goto Ready2

    DryFire:
        SSGR A 3 A_PlaySound ("Weapons/Misc/Dryfire", CHAN_7)
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "DryReload")
        goto Ready2

    DryReload:
        TNT1 A 0 A_GiveInventory ("S7_Reloading", 1)
    Reload:
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, 1)
        goto DontReload
        TNT1 A 0 A_JumpIfInventory ("S7_ShotgunMag", 0, "DontReload")
        TNT1 A 0 A_JumpIfInventory ("S7_20gaShells", 2, 1)
        goto DontReload
        SSGR A 5
        TNT1 A 0 A_JumpIfInventory ("S7_ShotgunLoaded", 2, "InsertShells")
        TNT1 A 0 A_JumpIfInventory ("S7_ShotgunLoaded", 1, "InsertShells.Pump")
        goto InsertFirstShells

    InsertShells:
        TNT1 A                    0 A_JumpIfInventory ("S7_Reloading", 1, 1)
        goto Reloaded
        TNT1 A                    0 A_JumpIfInventory ("S7_ShotgunMag", 0, "Reloaded")
        TNT1 A                    0 A_JumpIfInventory ("S7_20gaShells", 2, 1)
        goto Reloaded
        TNT1 A                    0 A_GiveInventory ("S7_ShotgunMag", 2)
        TNT1 A                    0 A_TakeInventory ("S7_20gaShells", 2)
        TNT1 A                    0 A_PlaySound ("Weapons/Shotgun/ShellIn", CHAN_Weapon)
        DSGR ABCCCCBA             1
        TNT1 A                    0 A_PlaySound ("Weapons/Shotgun/ShellIn", CHAN_5)
        DSGR ABCCCCBAAAAAAAAAAAAA 1
        goto InsertShells

    InsertFirstShells:
        TNT1 A                    0 A_JumpIfInventory ("S7_Reloading", 1, 1)
        goto Reloaded
        TNT1 A                    0 A_JumpIfInventory ("S7_ShotgunMag", 0, "Reloaded")
        TNT1 A                    0 A_JumpIfInventory ("S7_20gaShells", 2, 1)
        goto Reloaded
        TNT1 A                    0 A_GiveInventory ("S7_ShotgunMag", 2)
        TNT1 A                    0 A_TakeInventory ("S7_20gaShells", 2)
        TNT1 A                    0 A_PlaySound ("Weapons/Shotgun/ShellIn", CHAN_Weapon)
        DSGR ABCCCCBA             1
        TNT1 A                    0 A_PlaySound ("Weapons/Shotgun/ShellIn", CHAN_5)
        DSGR ABCCCCBAAAAAAAAAAAAA 1
        TNT1 A                    0 A_GiveInventory ("S7_ShotgunLoaded", 2)
        SSGF E                    2 A_TakeInventory ("S7_ShotgunMag", 2)
        SSGF F                    2
        TNT1 A                    0 A_PlaySound ("Weapons/Shotgun/Pump", CHAN_7)
        SSGF HIJKLMN              2
        SSGF MLKJIH               2
        goto InsertShells

    InsertShells.Pump:
        TNT1 A 0 A_JumpIfInventory ("S7_ShotgunMag", 2, "InsertShells.PumpLoad")
        TNT1 A 0 A_JumpIfInventory ("S7_20gaShells", 0, "InsertShells.Pump2")
        TNT1 A 0 A_GiveInventory ("S7_20gaShells", 1)

    InsertShells.Pump2:

    InsertShells.PumpAnim:
        SSGF E       2 A_TakeInventory ("S7_ShotgunLoaded", 1)
        SSGF F       2
        TNT1 A       0 A_PlaySound ("Weapons/Shotgun/Pump", CHAN_7)
        SSGF HIJKLMN 2
        SSGF MLKJIH  2
        goto InsertFirstShells

    InsertShells.PumpLoad:
        TNT1 A 0 A_JumpIfInventory ("S7_ShotgunMag", 2, 1)
        goto InsertShells.PumpLoad2
        TNT1 A 0 A_GiveInventory ("S7_ShotgunDontPump", 1)
    InsertShells.PumpLoad2:
        TNT1 A       0 A_GiveInventory ("S7_ShotgunLoaded", 2)
        SSGF E       2 A_TakeInventory ("S7_ShotgunMag", 2)
        SSGF F       2
        TNT1 A       0 A_PlaySound ("Weapons/Shotgun/Pump", CHAN_7)
        SSGF HIJKLMN 2
        SSGF MLKJIH  2
        TNT1 A       0 A_JumpIfInventory ("S7_ShotgunDontPump", 1, "InsertShells")
        goto InsertFirstShells

    Reloaded:
    DontReload:
        TNT1 A 0 A_TakeInventory ("S7_ShotgunDontPump", 1)
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 1)
        goto Ready2
    }
}