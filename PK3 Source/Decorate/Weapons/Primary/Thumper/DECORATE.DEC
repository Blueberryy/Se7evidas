// help ;-;
// Zandronum's netcode fucking sucks.
#INCLUDE "Decorate/Weapons/Primary/Thumper/Casings.DEC"
#INCLUDE "Decorate/Weapons/Primary/Thumper/Grenades.DEC"
enum {
    S7_TH_None      = 0,
    S7_TH_Exp       = 1,
    S7_TH_Frag      = 2,
    S7_TH_Therm     = 3,
    S7_TH_Flare     = 4,
    S7_TH_Cluster   = 5,
    S7_TH_Nail      = 6,
    S7_TH_NGas      = 7,
};

//-------------------------------------------------------------------------------------------
//
// XPL-50 "Thumper" Grenade Launcher
//
//-------------------------------------------------------------------------------------------
actor S7_ThumperMag : ammo {
    inventory.maxAmount 5
    ammo.backpackMaxAmount 5

    +ignoreSkill
}

actor S7_ThumperMag1 : counter { inventory.maxAmount 7 }
actor S7_ThumperMag2 : counter { inventory.maxAmount 7 }
actor S7_ThumperMag3 : counter { inventory.maxAmount 7 }
actor S7_ThumperMag4 : counter { inventory.maxAmount 7 }
actor S7_ThumperMag5 : counter { inventory.maxAmount 7 }

actor S7_ThumperSelectedPool : counter { inventory.maxAmount 8 }

actor S7_ThumperChambered : boolean { }

actor S7_ThumperCurrentShell : counter { inventory.maxAmount 7 }

// WHYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYYY
actor S7_Thumper : S7_BaseWeapon 30004 {
    tag "$THUMPER"
    inventory.pickupMessage "$THUMPER"
    weapon.ammoType1 "S7_ThumperMag"
    weapon.ammoUse 0
    weapon.ammoGive 0

    +weapon.noAutoAim

    states {
    Spawn:
        THMP Z -1
        stop

    Ready:
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_ChangeFireMode", 1, "ChangeFireMode")
        THMP A 1 A_WeaponReady
        loop

    Fire:
        THMP A 2 A_JumpIfInventory ("S7_ThumperChambered", 1, 1)
        goto Pump
        TNT1 A 0 A_PlaySound ("Weapons/Thumper/Fire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_JumpIfInventory ("S7_ThumperCurrentShell",    S7_TH_NGas,    "Fire.NGas")
        TNT1 A 0 A_JumpIfInventory ("S7_ThumperCurrentShell",    S7_TH_Nail,    "Fire.Nail")
        TNT1 A 0 A_JumpIfInventory ("S7_ThumperCurrentShell", S7_TH_Cluster, "Fire.Cluster")
        TNT1 A 0 A_JumpIfInventory ("S7_ThumperCurrentShell",   S7_TH_Flare,   "Fire.Flare")
        TNT1 A 0 A_JumpIfInventory ("S7_ThumperCurrentShell",   S7_TH_Therm,   "Fire.Therm")
        TNT1 A 0 A_JumpIfInventory ("S7_ThumperCurrentShell",    S7_TH_Frag,    "Fire.Frag")
        TNT1 A 0 A_JumpIfInventory ("S7_ThumperCurrentShell",     S7_TH_Exp,     "Fire.Exp")
        goto Fire.Exp

    Fire.Exp:
        TNT1 A 0 A_FireCustomMissile ("S7_50mmCasingSpawn", 0, 0, 10, -9)
        TNT1 A 0 A_FireCustomMissile ("S7_ThumperGrenade", 0, 0, 8, -5, 0, 4.5)
        goto Fire.Finish
    Fire.Frag:
        TNT1 A 0 A_FireCustomMissile ("S7_50mmCasingSpawnFrag", 0, 0, 10, -9)
        TNT1 A 0 A_FireCustomMissile ("S7_ThumperGrenadeFrag", 0, 0, 8, -5, 0, 4.5)
        goto Fire.Finish
    Fire.Therm:
        TNT1 A 0 A_FireCustomMissile ("S7_50mmCasingSpawnTherm", 0, 0, 10, -9)
        TNT1 A 0 A_FireCustomMissile ("S7_ThumperGrenadeTherm", 0, 0, 8, -5, 0, 4.5)
        goto Fire.Finish
    Fire.Flare:
        TNT1 A 0 A_FireCustomMissile ("S7_50mmCasingSpawnFlare", 0, 0, 10, -9)
        TNT1 A 0 A_FireCustomMissile ("S7_ThumperGrenadeFlare", 0, 0, 8, -5, 0, 4.5)
        goto Fire.Finish
    Fire.Cluster:
        TNT1 A 0 A_FireCustomMissile ("S7_50mmCasingSpawnCluster", 0, 0, 10, -9)
        TNT1 A 0 A_FireCustomMissile ("S7_ThumperGrenadeCluster", 0, 0, 8, -5, 0, 4.5)
        goto Fire.Finish
    Fire.Nail:
        TNT1 A 0 A_FireCustomMissile ("S7_50mmCasingSpawnNail", 0, 0, 10, -9)
        TNT1 A 0 A_FireCustomMissile ("S7_ThumperGrenadeNail", 0, 0, 8, -5, 0, 4.5)
        goto Fire.Finish
    Fire.NGas:
        TNT1 A 0 A_FireCustomMissile ("S7_50mmCasingSpawnNGas", 0, 0, 10, -9)
        TNT1 A 0 A_FireCustomMissile ("S7_ThumperGrenadeNGas", 0, 0, 8, -5, 0, 4.5)
        goto Fire.Finish

    Fire.Finish:
        TNT1 A      0 ACS_NamedExecuteAlways ("S7_ThumperConsumeChamber", 0)
        TNT1 A      0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 4, 4)
        THMP AA     1 bright A_SetPitch (pitch - 1.65, SPF_Interpolate)
        THMP DBCDEA 1
        goto Ready2

    ChangeFireMode:
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "ChangeFireMode.Reloading")
        THMP A 2 A_JumpIf (callACS ("S7_ThumperCyclePools") == FALSE, 2)
        TNT1 A 0 A_PlaySound ("Weapons/ModeChange", CHAN_Weapon)
        THMP A 4
        goto Ready2
    ChangeFireMode.Reloading:
        THPR E 2 A_JumpIf (callACS ("S7_ThumperCyclePools") == FALSE, 2)
        TNT1 A 0 A_PlaySound ("Weapons/ModeChange", CHAN_Weapon)
        THPR E 1
        goto Reload.Select

    Pump:
        THMP A           2 A_JumpIf (callACS ("S7_ThumperGetNext") == S7_TH_None, "DryFire")
        TNT1 A           0 ACS_NamedExecuteAlways ("S7_ThumperPump", 0)
        THMP A           2
        THPP A           2
        TNT1 A           0 A_PlaySound ("Weapons/Thumper/Pump", CHAN_7)
        THPP BCDDDDDDCBA 1
        THMP A           2
        goto Ready2

    Flash:
        THPF AB 1 bright
        stop

    DryFire:
        THMP A 1 A_PlaySound ("Weapons/Misc/Dryfire", CHAN_7)
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "DryReload")
        goto Ready2

    DryReload:
        TNT1 A 0 A_GiveInventory ("S7_Reloading", 1)
    Reload:
        TNT1 A     0 A_JumpIfInventory ("S7_Reloading", 1, 1)
        goto EndReload
        TNT1 A     0 A_JumpIf (callACS ("S7_ThumperFull"), "EndReload")
        THMP A     2 A_JumpIf (callACS ("S7_ThumperNextPool") == 0, "EndReload")
        THPR ABCDE 2
    Reload.Select:
        THPR E 2 A_JumpIfInventory ("S7_ThumperChambered", 1, "InsertShells")
        goto InsertFirstShells

    InsertShells:
        TNT1 A 0 A_JumpIfInventory ("S7_ChangeFireMode", 1, "ChangeFireMode")
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, 1)
        goto Reloaded
        TNT1 A 0 A_JumpIf (callACS ("S7_ThumperFull"), "Reloaded")
        THPR E 2 A_JumpIf (callACS ("S7_ThumperNextPool") == 0, "Reloaded")
        TNT1 A 0 ACS_NamedExecuteAlways ("S7_ThumperPerformReload")
        THPR E 5
        TNT1 A 0 A_PlaySound ("Weapons/Thumper/ShellIn", CHAN_Weapon)
        THPR E 8
        goto InsertShells

    InsertFirstShells:
        TNT1 A           0 A_JumpIfInventory ("S7_ChangeFireMode", 1, "ChangeFireMode")
        // Load
        TNT1 A           0 A_JumpIfInventory ("S7_Reloading", 1, 1)
        goto Reloaded
        TNT1 A           0 A_JumpIf (callACS ("S7_ThumperFull"), "Reloaded")
        THPR E           2 A_JumpIf (callACS ("S7_ThumperNextPool") == 0, "Reloaded")
        TNT1 A           0 ACS_NamedExecuteAlways ("S7_ThumperPerformReload")
        THPR E           5
        TNT1 A           0 A_PlaySound ("Weapons/Thumper/ShellIn", CHAN_Weapon)
        THPR E           8
        THPR EDCBA       2
        // Pump
        THMP A           2 A_JumpIf (callACS ("S7_ThumperGetNext") == S7_TH_None, "DryFire")
        TNT1 A           0 ACS_NamedExecuteAlways ("S7_ThumperPump", 0)
        THMP A           2
        THPP A           2
        TNT1 A           0 A_PlaySound ("Weapons/Thumper/Pump", CHAN_7)
        THPP BCDDDDDDCBA 1
        THMP A           1
        TNT1 A           0 A_JumpIfInventory ("S7_Reloading", 1, 1)
        goto EndReload
        THMP A           2 A_JumpIf (callACS ("S7_ThumperNextPool") == 0, "EndReload")
        THPR ABCDE       2
        goto InsertShells

    Reloaded:
        THPR EDCBA 2
    EndReload:
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 1)
        goto Ready2
    }
}