actor S7_ThumperGrenade {
    radius 10
    height 10
    speed 40
    projectile
    damage (random [weaponDamage] (1, 6) * 24)
    scale 0.8
    gravity 0.25
    translation "112:127=216:223"

    -noGravity

    states {
    Spawn:
        THGR A 1
        loop
    Death:
        TNT1 A 0 A_ChangeFlag ("noGravity", TRUE)
        TNT1 A 0 A_Jump (256, "Loople")
        wait
    Loople:
        TNT1 A 0 A_ChangeFlag ("forceXYBillboard", TRUE)
        TNT1 A 0 A_PlaySound ("Explosions/Explosion1")
        TNT1 A 0 A_Explode (128, 128)
        XPL1 A 3 bright A_SetScale (0.5)
        XPL1 BCDE 3 bright
        stop
    }
}

actor S7_ThumperGrenadeFrag : S7_ThumperGrenade {
    damage (random [weaponDamage] (1, 6) * 16)

    states {
    Loople:
        TNT1 A 0 A_ChangeFlag ("forceXYBillboard", TRUE)
        TNT1 A 0 A_PlaySound ("Explosions/Explosion1")
        TNT1 A 0 A_Explode (128, 128)
        TNT1 A 0 A_Jump (256, 1, 2, 3, 4)
        TNT1 AAAAAAAAAAAAAAAA 0 A_SpawnItemEx ("S7_Shrapnel", 0.2, fRandom (-0.025, 0.025), 0.25,
                                               fRandom (0.5, 4.8), 0.0, fRandom (3.2, 8.8), fRandom (0, 359), SXF_TransferPointers | SXF_NoCheckPosition)
        XPL1 A 3 bright A_SetScale (0.43)
        XPL1 BCDE 3 bright
        stop
    }
}

actor S7_ThumperGrenadeTherm : S7_ThumperGrenade {
    damage (random [weaponDamage] (1, 6) * 16)
    translation "112:127=184:191"

    states {
    Loople:
        TNT1 A 0 A_ChangeFlag ("forceXYBillboard", TRUE)
        TNT1 A 0 A_PlaySound ("Explosions/Explosion1")
        TNT1 A 0 A_Explode (80, 80)
        TNT1 A 0 A_Jump (256, 1, 2, 3, 4)
        TNT1 AAAAAAA 0 A_SpawnItemEx ("S7_ThumperGrenadeTherm_Fire", fRandom (-0.025, 0.025), fRandom (-0.025, 0.025), 0.25, fRandom (2.8, 3.0), 0.0, fRandom (2.5, 4.0), random (0, 359), SXF_TransferPointers | SXF_NoCheckPosition)
        XPL1 A 3 bright A_SetScale (0.43)
        XPL1 BCDE 3 bright
        stop
    }
}

actor S7_ThumperGrenadeTherm_Fire {
    radius 1
    height 1
    mass 0x7FFFFFFF
    args 24
    renderStyle add
    damage 0
    damageType ThermiteFire
    projectile

    +thruActors +noDamageThrust +bloodlessImpact +forceRadiusDMG
    +forceXYBillboard
    -noGravity

    states {
    Spawn:
        THGR F 1
        wait
    Death:
        TNT1 A 0 A_ChangeFlag ("forceXYBillboard", FALSE)
        TNT1 A 0 A_PlaySound ("Fire/Small", 7, 0.8, TRUE, 0.5)
        TNT1 A 0 A_ChangeFlag ("noBlockmap", FALSE)
    Loople:
        TNT1 A 0 A_CountdownArg (0, "FadeOut")
        TNT1 A 0 A_Explode (3, 64)
        THGR CDECDECDE 2 bright
        loop
    FadeOut:
        TNT1 A 0 A_Explode (3, 64)
        TNT1 A 0 A_SetScale (scaleX - 0.035)
        THGR CDE 2 bright A_FadeOut (0.08)
        TNT1 A 0 A_SetScale (scaleX - 0.035)
        THGR CDE 2 bright A_FadeOut (0.08)
        TNT1 A 0 A_SetScale (scaleX - 0.035)
        THGR CDE 2 bright A_FadeOut (0.08)
        loop
    }
}

actor S7_ThumperGrenadeFlare : S7_ThumperGrenade {
    damage (random [weaponDamage] (1, 6) * 5)
    damageType TH_GrenadeImpact
    translation "112:127=80:95"
    args -64, 64

    var int user_timesToLoop;
    var int user_moveDir;
    var int user_initialized;

    states {
    Spawn:
        TNT1 A 0 noDelay A_PlaySound ("Fire/Flare", 7, 0.15, TRUE)
        TNT1 A 0 A_SetUserVar ("user_timesToLoop", random (236, 280))
        TNT1 A 0 A_SetUserVar ("user_moveDir", args [random (0, 1)])
        TNT1 A 0 A_SetUserVar ("user_initialized", 1)
    Idle:
        THGR A 1 light (TGFlare1)
        loop
    Death:
        TNT1 A 0 A_SetGravity (1.0)
        TNT1 A 0 A_JumpIf (user_initialized, "Loople")
        TNT1 A 0 A_PlaySound ("Fire/Flare", 7, 0.15, TRUE)
        TNT1 A 0 A_SetUserVar ("user_timesToLoop", random (236, 280))
        TNT1 A 0 A_SetUserVar ("user_moveDir", args [random (0, 1)])
        TNT1 A 0 A_SetUserVar ("user_initialized", 1)
    Loople:
        TNT1 A 0 A_JumpIf (user_timesToLoop < 150, "Loople2")
        TNT1 A 0 A_JumpIf (user_timesToLoop <=  0, "Loopled")
        TNT1 A 0 A_SetUserVar ("user_timesToLoop", user_timesToLoop - 1)
        THGR A 2 light (TGFlare1) A_SetAngle (angle + user_moveDir)
        loop
    Loople2:
        TNT1 A 0 A_JumpIf (user_timesToLoop < 100, "Loople3")
        TNT1 A 0 A_SetUserVar ("user_timesToLoop", user_timesToLoop - 1)
        THGR A 2 light (TGFlare2) A_SetAngle (angle + user_moveDir)
        loop
    Loople3:
        TNT1 A 0 A_JumpIf (user_timesToLoop <  50, "Loople4")
        TNT1 A 0 A_SetUserVar ("user_timesToLoop", user_timesToLoop - 1)
        THGR A 2 light (TGFlare3) A_SetAngle (angle + user_moveDir)
        loop
    Loople4:
        TNT1 A 0 A_JumpIf (user_timesToLoop <  25, "Loople5")
        TNT1 A 0 A_SetUserVar ("user_timesToLoop", user_timesToLoop - 1)
        THGR A 2 light (TGFlare4) A_SetAngle (angle + user_moveDir)
        loop
    Loople5:
        TNT1 A 0 A_JumpIf (user_timesToLoop <  13, "Loople6")
        TNT1 A 0 A_SetUserVar ("user_timesToLoop", user_timesToLoop - 1)
        THGR A 2 light (TGFlare5) A_SetAngle (angle + user_moveDir)
        loop
    Loople6:
        TNT1 A 0 A_JumpIf (user_timesToLoop <=  0, "Loopled")
        TNT1 A 0 A_SetUserVar ("user_timesToLoop", user_timesToLoop - 1)
        THGR A 2 light (TGFlare6) A_SetAngle (angle + user_moveDir)
        loop
    Loopled:
        TNT1 A 0 A_StopSound (7)
        THGR B 2 A_SetAngle (angle + user_moveDir)
        THGR B 2 A_SetAngle (angle + (user_moveDir - (user_moveDir / 10)))
        THGR B 3 A_SetAngle (angle + (user_moveDir - (user_moveDir / 9)))
        THGR B 3 A_SetAngle (angle + (user_moveDir - (user_moveDir / 8)))
        THGR B 4 A_SetAngle (angle + (user_moveDir - (user_moveDir / 7)))
        THGR B 4 A_SetAngle (angle + (user_moveDir - (user_moveDir / 6)))
        THGR B 5 A_SetAngle (angle + (user_moveDir - (user_moveDir / 5)))
        THGR B 5 A_SetAngle (angle + (user_moveDir - (user_moveDir / 4)))
        THGR B 6 A_SetAngle (angle + (user_moveDir - (user_moveDir / 3)))
        THGR B 6 A_SetAngle (angle + (user_moveDir - (user_moveDir / 2)))
        THGR B 4
        THGR B 2 A_SpawnItemEx ("S7_Thumper_Used", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_TransferTranslation | SXF_NoCheckPosition | SXF_TransferPitch | SXF_TransferPointers)
        stop
    }
}

const int TH_GrenCluster_AngleDiff = 60;
actor S7_ThumperGrenadeCluster : S7_ThumperGrenade {
    damage (random [weaponDamage] (1, 6) * 18)
    translation "112:127=200:207"

    var int user_angle;

    states {
    Loople:
        TNT1 A 0 A_ChangeFlag ("forceXYBillboard", TRUE)
        TNT1 A 0 A_PlaySound ("Explosions/Explosion1")
        TNT1 A 0 A_Explode (80, 80)
        TNT1 A 0 A_CheckCeiling ("OnCeiling")
    ActualLoople:
        TNT1 A 0 A_JumpIf (user_angle >= 360, "Finish")
        TNT1 A 0 A_SpawnItemEx ("S7_ThumperGrenadeCluster_Bomb", 0.8, fRandom (-0.025, 0.025), 0.25,
                                3.0, 0.0, fRandom (4.75, 5.0), user_angle, SXF_TransferPointers | SXF_NoCheckPosition)
        TNT1 A 0 A_SetUserVar ("user_angle", user_angle + TH_GrenCluster_AngleDiff)
        TNT1 A 0 A_Jump (256, "ActualLoople")
        wait
    Finish:
        XPL1 A 3 bright A_SetScale (0.43)
        XPL1 BCDE 3 bright
        stop

    OnCeiling:
        TNT1 A 0 A_SetPitch (25)
        goto ActualLoople
    }
}

actor S7_ThumperGrenadeCluster_Bomb {
    radius 1
    height 1
    damage (random (1, 6) * 8)
    bounceCount 2
    BounceSound "Bounce/MetalSmall"
    projectile

    +bounceOnCeilings +bounceOnFloors +bounceOnWalls +canBounceWater
    +forceXYBillboard
    -noGravity

    states {
    Spawn:
        THGR G 1
        wait
    Death:
        TNT1 A 0 A_ChangeFlag ("noGravity", TRUE)
        TNT1 A 0 A_PlaySound ("Explosions/Explosion2", CHAN_BODY, 0.25)
        TNT1 A 0 A_Explode (64, 64)
        XPL3 A 3 bright A_SetScale (0.2)
        XPL3 BCDEFGHIJ 3 bright
        stop
    }
}

// Nailbomb: "112:127=160:167"
// Need to find good nail sprites.

actor S7_ThumperGrenadeNGas : S7_ThumperGrenade {
    damage (random [weaponDamage] (1, 6) * 6)
    damageType TH_GrenadeImpact
    translation "112:127=104:111"

    states {
    Loople:
        TNT1 A 0 A_PlaySound ("Steam/Burst")
        TNT1 AAAAAAA 0 A_SpawnItemEx ("S7_ThumperGrenadeNGas_GasCloud", fRandom (-1.2, 1.2), fRandom (-1.2, 1.2), 2.2,
                                   fRandom (5.3, 6.5), 0.0, fRandom (1.2, 1.5), random (0, 359), SXF_NoCheckPosition | SXF_TransferPointers)
        THGR B 3 A_SpawnItemEx ("S7_Thumper_Used", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_TransferTranslation | SXF_NoCheckPosition | SXF_TransferPitch | SXF_TransferPointers)
        stop
    }
}

actor S7_ThumperGrenadeNGas_GasCloud {
    radius 1
    height 1
    damage 0
    damageType NerveGas

    +thruActors +noDamageThrust +bloodlessImpact +noGravity
    +forceRadiusDMG +forceXYBillboard // +noClip

    var int user_time;

    states {
    Spawn:
    Loople:
        TNT1 A 0 A_JumpIf (user_time >= 2, "StartLoople2")
        TNT1 A 0 A_SetUserVar ("user_time", user_time + 1)
        TNT1 A 0 A_Explode (3, 64, XF_HurtSource, FALSE, 32)
        TNT1 AAA 1 A_SpawnItemEx ("S7_ThumperGrenadeNGas_GasCloud_Trail")
        loop

    StartLoople2:
        TNT1 A 0 A_ScaleVelocity (0.0)
        TNT1 A 0 A_SetUserVar ("user_time", 0)
        // TNT1 A 0 A_ChangeFlag ("noClip", FALSE)
    Loople2:
        TNT1 A 0 A_JumpIf (user_time >= 6, "FadeOut")
        TNT1 A 0 A_SetUserVar ("user_time", user_time + 1)
        TNT1 A 0 A_Explode (3, 64, XF_HurtSource, FALSE, 32)
        TNT1 AAA 1 A_SpawnItemEx ("S7_ThumperGrenadeNGas_GasCloud_Trail")
        loop

    FadeOut:
        TNT1 A 0 A_Explode (3, 64, XF_HurtSource, FALSE, 32)
        TNT1 A 0 A_FadeOut (0.03)
        TNT1 A 1 A_SpawnItemEx ("S7_ThumperGrenadeNGas_GasCloud_Trail")
        TNT1 A 0 A_FadeOut (0.03)
        TNT1 A 1 A_SpawnItemEx ("S7_ThumperGrenadeNGas_GasCloud_Trail")
        TNT1 A 0 A_FadeOut (0.03)
        TNT1 A 1 A_SpawnItemEx ("S7_ThumperGrenadeNGas_GasCloud_Trail")
        loop
    }
}

actor S7_ThumperGrenadeNGas_GasCloud_Trail : S7_ShotSmoke {
    vSpeed 0
    scale 0.3
    alpha 0.35

    states {
    Spawn:
        TNT1 A 0 A_JumpIf (scaleX <= 0, "Die")
        TNT1 A 0 A_SetScale (scaleX - 0.03)
        SMK2 A 1 A_FadeOut (0.03)
        TNT1 A 0 A_JumpIf (scaleX <= 0, "Die")
        TNT1 A 0 A_SetScale (scaleX - 0.03)
        SMK2 A 1 A_FadeOut (0.03)
        TNT1 A 0 A_JumpIf (scaleX <= 0, "Die")
        TNT1 A 0 A_SetScale (scaleX - 0.03)
        SMK2 B 1 A_FadeOut (0.03)
        TNT1 A 0 A_JumpIf (scaleX <= 0, "Die")
        TNT1 A 0 A_SetScale (scaleX - 0.03)
        SMK2 B 1 A_FadeOut (0.03)
        TNT1 A 0 A_JumpIf (scaleX <= 0, "Die")
        TNT1 A 0 A_SetScale (scaleX - 0.03)
        SMK2 C 1 A_FadeOut (0.03)
        TNT1 A 0 A_JumpIf (scaleX <= 0, "Die")
        TNT1 A 0 A_SetScale (scaleX - 0.03)
        SMK2 C 1 A_FadeOut (0.03)
        TNT1 A 0 A_JumpIf (scaleX <= 0, "Die")
        TNT1 A 0 A_SetScale (scaleX - 0.03)
        SMK2 D 1 A_FadeOut (0.03)
        TNT1 A 0 A_JumpIf (scaleX <= 0, "Die")
        TNT1 A 0 A_SetScale (scaleX - 0.03)
        SMK2 D 1 A_FadeOut (0.03)
        loop
    Die:
        TNT1 A 0
        stop
    }
}