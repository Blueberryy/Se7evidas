/* Credits:
** THGR  AB: ???, Chronos "phantombeta" Ouroboros
** THGR CDE: Id Software, Chronos "phantombeta" Ouroboros
** THGR   F: Raven Software, Chronos "phantombeta" Ouroboros
** THGR   G: Raven Software
*/

//-------------------------------------------------------------------------------------------
//
// Normal/Explosive
//
//-------------------------------------------------------------------------------------------
actor S7_ThumperGrenade {
    radius 10
    height 10
    speed 75
    projectile
    damage (fRandom [weaponDamage] (1.0, 6.0) * 18)
    scale 0.8
    gravity 0.25
    translation "112:127=216:223"

    -noGravity

    states {
    Spawn:
        THGR A 1
        loop
    Death:
        TNT1 A 0 A_SetGravity (0.0)
        TNT1 A 1 A_Jump (256, "Loople")
        wait
    Loople:
        TNT1 A  0 A_SetScale (0.5)
        TNT1 A  0 A_Stop
        TNT1 A  0 A_PlaySound ("Explosions/Explosion1")
        TNT1 A  0 A_SetDamageType ("ExplosionSplashDMG")
        TNT1 A  0 A_Explode (128, 128)
        TNT1 A 15 A_SpawnItemEx ("S7_ThumperExplosion", 0.0, 0.0, 5.0, 0.0, 0.0, 0.0, 0, SXF_NoCheckPosition | SXF_TransferScale)
        stop
    }
}
actor S7_ThumperExplosion {
    radius 10
    height 10

    +noGravity +forceXYBillboard +noInteraction +noBlockmap

    states {
    Spawn:
        XPL1 A    3 bright
        XPL1 BCDE 3 bright
        stop
    }
}

//-------------------------------------------------------------------------------------------
//
// Fragmentation
//
//-------------------------------------------------------------------------------------------
actor S7_ThumperGrenadeFrag : S7_ThumperGrenade {
    damage (fRandom [weaponDamage] (1.0, 6.0) * 12)

    states {
    Loople:
        TNT1 A   0 A_SetScale (0.43)
        TNT1 A   0 A_PlaySound ("Explosions/Explosion1")
        TNT1 A   0 A_SetDamageType ("ExplosionSplashDMG")
        TNT1 A   0 A_Explode (128, 128)
        TNT1 AAA 0 A_SpawnItemEx ("S7_ShrapnelSpawner100x", 0.0, 0.0, 2.0, 0.0, 0.0, 0.0, 0, SXF_NoCheckPosition | SXF_TransferPointers)
        TNT1 A  15 A_SpawnItemEx ("S7_ThumperExplosion", 0.0, 0.0, 5.0, 0.0, 0.0, 0.0, 0, SXF_NoCheckPosition | SXF_TransferScale)
        stop
    }
}

//-------------------------------------------------------------------------------------------
//
// Thermite/Incendiary
//
//-------------------------------------------------------------------------------------------
actor S7_ThumperGrenadeTherm : S7_ThumperGrenade {
    damage (fRandom [weaponDamage] (1.0, 6.0) * 12)
    translation "112:127=184:191"

    states {
    Loople:
        TNT1 A       0 A_SetScale (0.43)
        TNT1 A       0 A_PlaySound ("Explosions/Explosion1")
        TNT1 A       0 A_SetDamageType ("ExplosionSplashDMG")
        TNT1 A       0 A_Explode (80, 80)
        TNT1 A       0 A_Jump (256, 1, 2, 3, 4)
        TNT1 AAAAAAA 0 A_SpawnItemEx ("S7_ThumperGrenadeTherm_Fire", fRandom (-0.025, 0.025), fRandom (-0.025, 0.025), 0.25, fRandom (2.8, 3.0), 0.0, fRandom (2.5, 4.0), random (0, 359), SXF_TransferPointers | SXF_NoCheckPosition)
        TNT1 A      15 A_SpawnItemEx ("S7_ThumperExplosion", 0.0, 0.0, 5.0, 0.0, 0.0, 0.0, 0, SXF_NoCheckPosition | SXF_TransferScale)
        stop
    }
}

actor S7_ThumperGrenadeTherm_Fire {
    radius 1
    height 1
    mass 0x7FFFFFFF
    args 24
    renderStyle add
    damage 0
    damageType ThermiteFire
    projectile

    +thruActors +noDamageThrust +bloodlessImpact +forceRadiusDMG
    +forceXYBillboard
    -noGravity

    states {
    Spawn:
        THGR F 1
        wait
    Death:
        TNT1 A 0 A_ChangeFlag ("forceXYBillboard", FALSE)
        TNT1 A 0 A_PlaySound ("Fire/Small", CHAN_7, 0.8, TRUE, 0.5)
        TNT1 A 0 A_ChangeFlag ("noBlockmap", FALSE)
    Loople:
        TNT1 A         0 A_CountdownArg (0, "FadeOut")
        TNT1 A         0 A_Explode (3, 64)
        THGR CDECDECDE 2 bright
        loop
    FadeOut:
        TNT1 A   0        A_Explode (3, 64)
        TNT1 A   0        A_SetScale (scaleX - 0.035)
        THGR CDE 2 bright A_FadeOut (0.08)
        TNT1 A   0        A_SetScale (scaleX - 0.035)
        THGR CDE 2 bright A_FadeOut (0.08)
        TNT1 A   0        A_SetScale (scaleX - 0.035)
        THGR CDE 2 bright A_FadeOut (0.08)
        loop
    }
}

//-------------------------------------------------------------------------------------------
//
// Flare
//
//-------------------------------------------------------------------------------------------
actor S7_ThumperGrenadeFlare : S7_ThumperGrenade {
    damage (fRandom [weaponDamage] (1.0, 6.0) * 5)
    damageType TH_GrenadeImpact
    translation "112:127=80:95"
    args -64, 64

    -noGravity

    var int user_timesToLoop;
    var int user_moveDir;
    var int user_initialized;

    states {
    Spawn:
        TNT1 A 0 noDelay A_PlaySound ("Fire/Flare", CHAN_7, 0.15, TRUE)
        TNT1 A 0         A_SetUserVar ("user_timesToLoop", random (1025, 1050))
        TNT1 A 0         A_SetUserVar ("user_moveDir", args [random (0, 1)])
        TNT1 A 0         A_SetUserVar ("user_initialized", 1)
    Idle:
        THGR A 1 light (TGFlare1)
        loop

    Death:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_SetGravity (1.0)
        TNT1 A 0 A_ChangeFlag ("noBlockmap", FALSE)
        TNT1 A 0 A_SetUserVar ("user_timesToLoop", random (1025, 1050))
        TNT1 A 0 A_SetUserVar ("user_moveDir", args [random (0, 1)])
        TNT1 A 0 A_SetUserVar ("user_initialized", 1)
    Loople:
        /*TNT1 A 0                  A_JumpIf (user_timesToLoop < 678, "Loople2")
        TNT1 A 0                  A_SetUserVar ("user_timesToLoop", user_timesToLoop - 1)*/
        THGR A 2 light (TGFlare1) A_SetAngle (angle + user_moveDir)
        loop
    Loople2:
        TNT1 A 0                  A_JumpIf (user_timesToLoop < 565, "Loople3")
        TNT1 A 0                  A_SetUserVar ("user_timesToLoop", user_timesToLoop - 1)
        THGR A 2 light (TGFlare2) A_SetAngle (angle + user_moveDir)
        loop
    Loople3:
        TNT1 A 0                  A_JumpIf (user_timesToLoop < 452, "Loople4")
        TNT1 A 0                  A_SetUserVar ("user_timesToLoop", user_timesToLoop - 1)
        THGR A 2 light (TGFlare3) A_SetAngle (angle + user_moveDir)
        loop
    Loople4:
        TNT1 A 0                  A_JumpIf (user_timesToLoop < 339, "Loople5")
        TNT1 A 0                  A_SetUserVar ("user_timesToLoop", user_timesToLoop - 1)
        THGR A 2 light (TGFlare4) A_SetAngle (angle + user_moveDir)
        loop
    Loople5:
        TNT1 A 0                  A_JumpIf (user_timesToLoop < 226, "Loople6")
        TNT1 A 0                  A_SetUserVar ("user_timesToLoop", user_timesToLoop - 1)
        THGR A 2 light (TGFlare5) A_SetAngle (angle + user_moveDir)
        loop
    Loople6:
        TNT1 A 0                  A_JumpIf (user_timesToLoop < 113, "Loople7")
        TNT1 A 0                  A_SetUserVar ("user_timesToLoop", user_timesToLoop - 1)
        THGR A 2 light (TGFlare6) A_SetAngle (angle + user_moveDir)
        loop
    Loople7:
        TNT1 A 0                  A_JumpIf (user_timesToLoop <=  0, "Loopled")
        TNT1 A 0                  A_SetUserVar ("user_timesToLoop", user_timesToLoop - 1)
        THGR A 2 light (TGFlare7) A_SetAngle (angle + user_moveDir)
        loop

    Loopled:
        TNT1 A 0 A_StopSound (CHAN_7)
        THGR B 2 A_SetAngle (angle + user_moveDir)
        THGR B 2 A_SetAngle (angle + (user_moveDir - (user_moveDir / 10)))
        THGR B 3 A_SetAngle (angle + (user_moveDir - (user_moveDir / 9)))
        THGR B 3 A_SetAngle (angle + (user_moveDir - (user_moveDir / 8)))
        THGR B 4 A_SetAngle (angle + (user_moveDir - (user_moveDir / 7)))
        THGR B 4 A_SetAngle (angle + (user_moveDir - (user_moveDir / 6)))
        THGR B 5 A_SetAngle (angle + (user_moveDir - (user_moveDir / 5)))
        THGR B 5 A_SetAngle (angle + (user_moveDir - (user_moveDir / 4)))
        THGR B 6 A_SetAngle (angle + (user_moveDir - (user_moveDir / 3)))
        THGR B 6 A_SetAngle (angle + (user_moveDir - (user_moveDir / 2)))
        THGR B 4
        THGR B 2 A_SpawnItemEx ("S7_Thumper_Used", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_TransferTranslation | SXF_NoCheckPosition | SXF_TransferPitch | SXF_TransferPointers)
        stop
    }
}

//-------------------------------------------------------------------------------------------
//
// Clusterbomb
//
//-------------------------------------------------------------------------------------------
const int TH_GrenCluster_AngleDiff = 60;
actor S7_ThumperGrenadeCluster : S7_ThumperGrenade {
    damage (fRandom [weaponDamage] (1.0, 6.0) * 14)
    translation "112:127=200:207"

    var int user_angel;

    states {
    Loople:
        TNT1 A 0 A_PlaySound ("Explosions/Explosion1")
        TNT1 A 0 A_SetDamageType ("ExplosionSplashDMG")
        TNT1 A 0 A_Explode (80, 80)
        TNT1 A 0 A_CheckCeiling ("OnCeiling")
        TNT1 A 1 A_Jump (256, "ActualLoople")
        wait

    ActualLoople:
        TNT1 A 0 A_JumpIf (user_angel >= 360, "Finish")
        TNT1 A 0 A_SpawnItemEx ("S7_ThumperGrenadeCluster_Bomb", 0.8, fRandom (-0.025, 0.025), 0.25,
                                3.0, 0.0, fRandom (6.75, 7.0), user_angel, SXF_TransferPointers | SXF_NoCheckPosition)
        TNT1 A 0 A_SetUserVar ("user_angel", user_angel + TH_GrenCluster_AngleDiff)
        TNT1 A 1 A_Jump (256, "ActualLoople")
        wait

    Finish:
        TNT1 A  0 A_SetScale (0.43)
        TNT1 A 15 A_SpawnItemEx ("S7_ThumperExplosion", 0.0, 0.0, 5.0, 0.0, 0.0, 0.0, 0, SXF_NoCheckPosition | SXF_TransferScale)
        stop

    OnCeiling:
        TNT1 A 0 A_SetPitch (25)
        TNT1 A 1 A_Jump (256, "ActualLoople")
        wait
    }
}

actor S7_ThumperGrenadeCluster_Bomb {
    radius 1
    height 1
    damage (fRandom [weaponDamage] (1.0, 6.0) * 8)
    bounceCount 2
    BounceSound "Bounce/MetalSmall"
    projectile

    +bounceOnCeilings +bounceOnFloors +bounceOnWalls +canBounceWater
    +forceXYBillboard
    -noGravity

    states {
    Spawn:
        THGR G 1
        loop
    Death:
        TNT1 A         0        A_ChangeFlag ("noGravity", TRUE)
        TNT1 A         0        A_PlaySound ("Explosions/Explosion2", CHAN_Body, 0.25)
        TNT1 A         0        A_SetDamageType ("ExplosionSplashDMG")
        TNT1 A         0        A_Explode (128, 96)
        XPL3 A         3 bright A_SetScale (0.2)
        XPL3 BCDEFGHIJ 3 bright
        stop
    }
}

//-------------------------------------------------------------------------------------------
//
// Nailbomb
//
//-------------------------------------------------------------------------------------------
const int TH_GrenNail_AngleDiff = 20;
actor S7_ThumperGrenadeNail : S7_ThumperGrenade {
    damage (fRandom [weaponDamage] (1.0, 6.0) * 13)
    translation "112:127=200:207"

    var int user_angle;
    var int user_DirectionNone; // 1 == No up, 2 == No down

    states {
    Loople:
        TNT1 A 0 A_PlaySound ("Explosions/Explosion1")
        TNT1 A 0 A_SetDamageType ("ExplosionSplashDMG")
        TNT1 A 0 A_Explode (65, 65)
        /*TNT1 A 0 A_SetUserVar ("user_DirectionNone", 0)
        TNT1 A 0 A_CheckCeiling ("OnCeiling")
        TNT1 A 0 A_CheckFloor ("OnFloor")*/

    StartLoople:
        TNT1 A 0
        //goto StartUp
        TNT1 A 1 A_Jump (256, "StartMiddle")
        wait

    StartUp:
        TNT1 A 0 A_JumpIf (user_DirectionNone == 1, "LoopleMiddle")
        TNT1 A 0 A_SetUserVar ("user_angle", 0)
        TNT1 A 1 A_Jump (256, "LoopleUp")
        wait
    LoopleUp:
        TNT1 A 0 A_JumpIf (user_angle >= 360, "StartMiddle")
        TNT1 A 0 A_CustomMissile ("S7_ThumperGrenadeNail_Nail", 2.2, 0, user_angle, CMF_AimDirection | CMF_TrackOwner, 35)
        TNT1 A 0 A_SetUserVar ("user_angle", user_angle + TH_GrenNail_AngleDiff)
        TNT1 A 1 A_Jump (256, "LoopleUp")
        wait

    StartMiddle:
        TNT1 A 0 A_SetUserVar ("user_angle", 0)
        TNT1 A 1 A_Jump (256, "LoopleMiddle")
        wait
    LoopleMiddle:
        TNT1 A 0 A_JumpIf (user_angle >= 360, "Finish") // "StartDown")
        TNT1 A 0 A_CustomMissile ("S7_ThumperGrenadeNail_Nail", 2.2, 0, user_angle, CMF_AimDirection | CMF_TrackOwner, 0)
        TNT1 A 0 A_SetUserVar ("user_angle", user_angle + TH_GrenNail_AngleDiff)
        TNT1 A 1 A_Jump (256, "LoopleMiddle")
        wait

    /*StartDown:
        TNT1 A 0 A_JumpIf (user_DirectionNone == 2, "Finish")
        TNT1 A 0 A_SetUserVar ("user_angle", 0)
        TNT1 A 1 A_Jump (256, "LoopleDown")
        wait
    LoopleDown:
        TNT1 A 0 A_JumpIf (user_angle >= 360, "Finish")
        TNT1 A 0 A_CustomMissile ("S7_ThumperGrenadeNail_Nail", 2.2, 0, user_angle, CMF_AimDirection | CMF_TrackOwner, -35)
        TNT1 A 0 A_SetUserVar ("user_angle", user_angle + TH_GrenNail_AngleDiff)
        TNT1 A 1 A_Jump (256, "LoopleDown")
        wait*/


    Finish:
        TNT1 A  0 A_SetScale (0.43)
        TNT1 A 15 A_SpawnItemEx ("S7_ThumperExplosion", 0.0, 0.0, 5.0, 0.0, 0.0, 0.0, 0, SXF_NoCheckPosition | SXF_TransferScale)
        stop

    OnCeiling:
        TNT1 A 0 A_SetUserVar ("user_DirectionNone", 1)
        goto StartLoople
    OnFloor:
        TNT1 A 0 A_SetUserVar ("user_DirectionNone", 2)
        goto StartLoople
    }
}

actor S7_ThumperGrenadeNail_Nail : fastProjectile {
    radius 2
    height 2
    damage (fRandom [weaponDamage] (3.0, 5.0))
    speed 1
    health 45
    missileType "S7_HeatSmokeStatic"
    missileHeight 8

    projectile
    +noGravity +bloodSplatter +noExtremeDeath +noDamageThrust
    +ripper

    states {
    Spawn:
        TNT1 AA 0 A_ScaleVelocity (45)
        NAIL A 5
    Flight:
        TNT1 A 0 A_ChangeVelocity (0.0, 0.0, -0.5)
        NAIL AA 1 A_ScaleVelocity (0.99)
        NAIL A 1
        loop
    Death:
        NAIL A 500
    Loople:
        NAIL A 1 A_FadeOut (0.03)
        wait
    XDeath:
        TNT1 A 3 A_PlaySound ("Bullet/HitFlesh", CHAN_Body, 0.5)
        stop
    }
}

//-------------------------------------------------------------------------------------------
//
// Nerve gas
//
//-------------------------------------------------------------------------------------------
actor S7_ThumperGrenadeNGas : S7_ThumperGrenade {
    damage (fRandom [weaponDamage] (1.0, 6.0) * 6)
    damageType TH_GrenadeImpact
    translation "112:127=104:111"

    states {
    Loople:
        TNT1 A       0 A_PlaySound ("Steam/Burst")
        TNT1 AAAAAAA 0 A_SpawnItemEx ("S7_ThumperGrenadeNGas_GasCloud", fRandom (-1.2, 1.2), fRandom (-1.2, 1.2), 2.2,
                                      fRandom (5.3, 6.5), 0.0, fRandom (1.2, 1.5), random (0, 359), SXF_NoCheckPosition | SXF_TransferPointers)
        THGR B       3 A_SpawnItemEx ("S7_Thumper_Used", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_TransferTranslation | SXF_NoCheckPosition | SXF_TransferPitch | SXF_TransferPointers)
        stop
    }
}

/** Nerve gas cloud **/
damageType NerveGas { noArmor }
actor S7_ThumperGrenadeNGas_GasCloud {
    radius 1
    height 1
    damage 0
    damageType NerveGas

    +thruActors +noDamageThrust +bloodlessImpact +noGravity
    +forceRadiusDMG +forceXYBillboard // +noClip

    var int user_time;

    states {
    Spawn:
    Loople:
        TNT1 A   0 A_JumpIf (user_time >= 2, "StartLoople2")
        TNT1 A   0 A_SetUserVar ("user_time", user_time + 1)
        TNT1 A   0 A_Explode (3, 64, XF_HurtSource, FALSE, 32)
        TNT1 AAA 1 A_SpawnItemEx ("S7_ThumperGrenadeNGas_GasCloud_Trail")
        loop

    StartLoople2:
        TNT1 A 0 A_ScaleVelocity (0.0)
        TNT1 A 0 A_SetUserVar ("user_time", 0)
        // TNT1 A 0 A_ChangeFlag ("noClip", FALSE)
    Loople2:
        TNT1 A    0 A_JumpIf (user_time >= 6, "FadeOut")
        TNT1 A    0 A_SetUserVar ("user_time", user_time + 1)
        TNT1 A    0 A_Explode (3, 64, XF_HurtSource, FALSE, 32)
        TNT1 AAA 1 A_SpawnItemEx ("S7_ThumperGrenadeNGas_GasCloud_Trail")
        loop

    FadeOut:
        TNT1 A 0 A_Explode (3, 64, XF_HurtSource, FALSE, 32)
        TNT1 A 0 A_FadeOut (0.03)
        TNT1 A 1 A_SpawnItemEx ("S7_ThumperGrenadeNGas_GasCloud_Trail")
        TNT1 A 0 A_FadeOut (0.03)
        TNT1 A 1 A_SpawnItemEx ("S7_ThumperGrenadeNGas_GasCloud_Trail")
        TNT1 A 0 A_FadeOut (0.03)
        TNT1 A 1 A_SpawnItemEx ("S7_ThumperGrenadeNGas_GasCloud_Trail")
        loop
    }
}

/** Nerve gas cloud trail **/
actor S7_ThumperGrenadeNGas_GasCloud_Trail : S7_ShotSmoke {
    vSpeed 0
    scale 0.3
    alpha 0.35

    states {
    Spawn:
        TNT1 A 0 A_JumpIf (scaleX <= 0, "Die")
        TNT1 A 0 A_SetScale (scaleX - 0.03)
        SMK2 A 1 A_FadeOut (0.03)
        TNT1 A 0 A_JumpIf (scaleX <= 0, "Die")
        TNT1 A 0 A_SetScale (scaleX - 0.03)
        SMK2 A 1 A_FadeOut (0.03)
        TNT1 A 0 A_JumpIf (scaleX <= 0, "Die")
        TNT1 A 0 A_SetScale (scaleX - 0.03)
        SMK2 B 1 A_FadeOut (0.03)
        TNT1 A 0 A_JumpIf (scaleX <= 0, "Die")
        TNT1 A 0 A_SetScale (scaleX - 0.03)
        SMK2 B 1 A_FadeOut (0.03)
        TNT1 A 0 A_JumpIf (scaleX <= 0, "Die")
        TNT1 A 0 A_SetScale (scaleX - 0.03)
        SMK2 C 1 A_FadeOut (0.03)
        TNT1 A 0 A_JumpIf (scaleX <= 0, "Die")
        TNT1 A 0 A_SetScale (scaleX - 0.03)
        SMK2 C 1 A_FadeOut (0.03)
        TNT1 A 0 A_JumpIf (scaleX <= 0, "Die")
        TNT1 A 0 A_SetScale (scaleX - 0.03)
        SMK2 D 1 A_FadeOut (0.03)
        TNT1 A 0 A_JumpIf (scaleX <= 0, "Die")
        TNT1 A 0 A_SetScale (scaleX - 0.03)
        SMK2 D 1 A_FadeOut (0.03)
        loop
    Die:
        TNT1 A 0
        stop
    }
}