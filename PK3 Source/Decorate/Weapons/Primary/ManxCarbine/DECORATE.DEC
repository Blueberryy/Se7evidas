//-------------------------------------------------------------------------------------------
//
// C23 "Manx" Carbine
//
//-------------------------------------------------------------------------------------------
actor S7_ManxCarbineClip : ammo {
    inventory.maxAmount 23
    ammo.backpackMaxAmount 23

    +ignoreSkill
}

actor S7_ManxCarbine_Semi : boolean { }

actor S7_ManxCarbine : S7_BaseWeapon {
    tag "$MANXCARBINE"
    inventory.pickupMessage "$MANXCARBINE"
    weapon.ammoType1 "S7_ManxCarbineClip"
    weapon.ammoType2 "S7_9mmCartridges"
    weapon.ammoUse 1
    weapon.ammoGive 0
    
    states {
    Spawn:
        MANX Z -1
        loop

    Ready:
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_JumpIfInventory ("S7_ChangeFireMode", 1, "ChangeFireMode")
        MANX A 1 A_WeaponReady
        loop
    
    Fire:
        TNT1 A  0 A_JumpIfInventory ("S7_ManxCarbine_Semi", 1, "Fire.Semi")
        TNT1 A  0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A  0 A_PlaySound ("Weapons/ManxCarbine/Fire", CHAN_Weapon)
        TNT1 A  0 A_AlertMonsters
        TNT1 A  0 A_GunFlash
        TNT1 A  0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 3.5, 4.5)
        TNT1 A  0 A_FireCustomMissile ("S7_9mmCasingSpawn", 0, 0, 13, -3)
        TNT1 A  0 A_FireCustomMissile ("S7_ManxCarbineTracer", fRandom [weaponSpread] (-3.2, 3.2), 1, 6, 1, 0, fRandom [weaponSpread] (-2.4, 2.4))
        MANX AB 1 A_SetPitch (pitch - 0.7, SPF_Interpolate)
        MANX A  2
        MANX A  1 A_Refire
        MANX A  3
        goto Ready2

    Fire.Semi:
        TNT1 A   0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A   0 A_PlaySound ("Weapons/ManxCarbine/Fire", CHAN_Weapon)
        TNT1 A   0 A_AlertMonsters
        TNT1 A   0 A_GunFlash
        TNT1 A   0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 3.5, 4.5)
        TNT1 A   0 A_FireCustomMissile ("S7_9mmCasingSpawn", 0, 0, 13, -3)
        TNT1 A   0 A_FireCustomMissile ("S7_ManxCarbineTracer", fRandom [weaponSpread] (-2.9, 2.9), 1, 6, 1, 0, fRandom [weaponSpread] (-2.1, 2.1))
        MANX AB  1 A_SetPitch (pitch - 0.7, SPF_Interpolate)
        MANX A   1
        MANX AAA 1 A_WeaponReady (WRF_NoBob | WRF_NoSecondary | WRF_NoSwitch)
        MANX A   2
        goto Ready2

    ChangeFireMode:
        TNT1 A 0 A_PlaySound ("Weapons/ModeChange", CHAN_Weapon)
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 99999)
        TNT1 A 0 A_JumpIfInventory ("S7_ManxCarbine_Semi", 1, "ToFullAuto")
        MANX A 5 A_GiveInventory ("S7_ManxCarbine_Semi", 1)
        goto Ready2
    ToFullAuto:
        MANX A 5 A_TakeInventory ("S7_ManxCarbine_Semi", 99999)
        goto Ready2

    DryFire:
        MANX A 3 A_PlaySound ("Weapons/DryFireRifle", CHAN_7)
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "Reload")
        goto Ready2

    Reload:
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_ManxCarbineClip", 0, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("S7_9mmCartridges", 1, "TrueReload")
        goto Ready2
    TrueReload:
        TNT1 A 0 A_JumpIfInventory ("S7_ManxCarbineClip", 0, "ReloadFinish")
        TNT1 A 0 A_JumpIfInventory ("S7_9mmCartridges", 1, 1)
        goto ReloadFinish
        TNT1 A 0 A_GiveInventory ("S7_ManxCarbineClip", 1)
        TNT1 A 0 A_TakeInventory ("S7_9mmCartridges", 1)
        loop
    ReloadFinish:
        MANX CDEFG    2
        MANX HHHHHHIJ 1
        MANX K        1 A_PlaySound ("Weapons/ManxCarbine/In", CHAN_Weapon)
        MANX L        1
        MANX MNOPCA   2
        goto Ready2
    
    // Muzzle flashes
    Flash:
        MNXF A 1 bright
        stop
    }
}