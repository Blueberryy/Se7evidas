actor S7_AMGMag : ammo {
    inventory.maxAmount 35
    ammo.backpackMaxAmount 35

    +ignoreSkill
}

actor S7_AMG_Burst : boolean { }

actor S7_AMG : S7_BaseWeapon 30002 {
    tag "$AMG"
    inventory.pickupMessage "$AMG"
    weapon.ammoType1 "S7_AMGMag"
    weapon.ammoType2 "S7_762x39Cartridges"
    weapon.ammoUse 1
    weapon.ammoGive 0

    states {
    Spawn:
        AMGG Z -1
        stop
    
    Ready:
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_ChangeFireMode", 1, "ChangeFireMode")
        AMGG A 1 A_WeaponReady
        loop
    
    Fire:
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_JumpIfInventory ("S7_AMG_Burst", 1, "Fire.Burst")
        TNT1 A 0 A_PlaySound ("Weapons/AMG/Fire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 1, 5)
        TNT1 A 0 A_FireCustomMissile ("S7_RifleCasingSpawn", 0, 0, 8, -5)
        TNT1 A 0 A_FireCustomMissile ("S7_AMGTracer", fRandom [weaponSpread] (-2.8, 2.8), 1, 2, 2, 0, fRandom [weaponSpread] (-3.15, 3.15))
        AMGG BB 1 bright A_SetPitch (pitch - 1.2, SPF_Interpolate)
        AMGG C 1
        AMGG C 1 A_Refire
        goto Ready2
    Fire.Burst:
        TNT1 A 0 A_PlaySound ("Weapons/AMG/Fire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 1, 5)
        TNT1 A 0 A_FireCustomMissile ("S7_RifleCasingSpawn", 0, 0, 8, -5)
        TNT1 A 0 A_FireCustomMissile ("S7_AMGTracer", fRandom [weaponSpread] (-2.8, 2.8), 1, 2, 2, 0, fRandom [weaponSpread] (-3.15, 3.15))
        AMGG BB 1 bright A_SetPitch (pitch - 1.2, SPF_Interpolate)
        AMGG C 1
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_PlaySound ("Weapons/AMG/Fire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 1, 5)
        TNT1 A 0 A_FireCustomMissile ("S7_RifleCasingSpawn", 0, 0, 8, -5)
        TNT1 A 0 A_FireCustomMissile ("S7_AMGTracer", fRandom [weaponSpread] (-2.8, 2.8), 1, 2, 2, 0, fRandom [weaponSpread] (-3.15, 3.15))
        AMGG BB 1 bright A_SetPitch (pitch - 1.2, SPF_Interpolate)
        AMGG C 1
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_PlaySound ("Weapons/AMG/Fire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 1, 5)
        TNT1 A 0 A_FireCustomMissile ("S7_RifleCasingSpawn", 0, 0, 8, -5)
        TNT1 A 0 A_FireCustomMissile ("S7_AMGTracer", fRandom [weaponSpread] (-2.8, 2.8), 1, 2, 2, 0, fRandom [weaponSpread] (-3.15, 3.15))
        AMGG BB 1 bright A_SetPitch (pitch - 1.2, SPF_Interpolate)
        AMGG CC 1
        goto Ready2
    
    Flash:
        AMGF AB 1 bright
        stop
    
    ChangeFireMode:
        TNT1 A 0 A_PlaySound ("Weapons/ModeChange", CHAN_Weapon)
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 99999)
        TNT1 A 0 A_JumpIfInventory ("S7_AMG_Burst", 1, "ToFullAuto")
        AMGG A 5 A_GiveInventory ("S7_AMG_Burst", 1)
        goto Ready2
    ToFullAuto:
        AMGG A 5 A_TakeInventory ("S7_AMG_Burst", 99999)
        goto Ready2
    
    DryFire:
        AMGG A 3 A_PlaySound ("Weapons/DryFireRifle", 7)
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "Reload")
        goto Ready2
    
    Reload:
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_AMGMag", 0, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("S7_762x39Cartridges", 1, "TrueReload")
        goto Ready2
    TrueReload:
        TNT1 A 0 A_JumpIfInventory ("S7_AMGMag", 0, "ReloadFinish")
        TNT1 A 0 A_JumpIfInventory ("S7_762x39Cartridges", 1, 1)
        goto ReloadFinish
        TNT1 A 0 A_GiveInventory ("S7_AMGMag", 1)
        TNT1 A 0 A_TakeInventory ("S7_762x39Cartridges", 1)
        loop
    ReloadFinish:
        AMGG A 2
        AMGR ABCDE 2
        AMGR F 2 A_PlaySound ("Weapons/AMG/Out", CHAN_Weapon)
        AMGR GHIJKLMNO 2
        AMGR P 2 A_PlaySound ("Weapons/AMG/In", CHAN_Weapon)
        AMGR QR 4
        AMGR S 7 A_PlaySound ("Weapons/AMG/Tap", CHAN_Weapon)
        AMGR TUVWXYA 2
        AMGG A 2
        goto Ready2
    }
}