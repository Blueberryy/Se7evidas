actor S7_AMGMag : ammo {
    inventory.MaxAmount 41
    ammo.BackpackMaxAmount 41
    +ignoreSkill
}

actor S7_AMG_Burst : boolean { }

actor S7_AMG : S7_BaseWeapon 30002 {
    inventory.PickupMessage "$AMG"
    tag "$AMG"
    weapon.AmmoType1 "S7_AMGMag"
    weapon.AmmoType2 "S7_762x39Cartridges"
    weapon.AmmoUse 1
    weapon.AmmoGive 41
    states {
    Spawn:
        AMGG Z -1
        stop
    
    Ready:
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_ChangeFireMode", 1, "ChangeFireMode")
        AMGG A 1 A_WeaponReady
        loop
    
    Fire:
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_JumpIfInventory ("S7_AMG_Burst", 1, "Fire.Burst")
        TNT1 A 0 A_PlaySound ("Weapons/AMG/Fire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 1, 5)
        TNT1 A 0 A_FireCustomMissile ("S7_RifleCasingSpawn", 0, 0, 8, -5)
        TNT1 A 0 A_FireCustomMissile ("S7_AMGTracer", fRandom (-5.5, 5.5), 1, 2, 2, 0, fRandom (-4.5, 4.5))
        AMGG BC 1 acs_namedExecuteAlways ("S7_RecoilPitch", 0, 0.5 * 65535)
        AMGG D 1
        AMGG D 1 A_Refire
        goto Ready2
    Fire.Burst:
        TNT1 A 0 A_PlaySound ("Weapons/AMG/Fire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 1, 5)
        TNT1 A 0 A_FireCustomMissile ("S7_RifleCasingSpawn", 0, 0, 8, -5)
        TNT1 A 0 A_FireCustomMissile ("S7_AMGTracer", fRandom (-5.5, 5.5), 1, 3, 2, 0, fRandom (-4.5, 4.5))
        AMGG BC 1 acs_namedExecuteAlways ("S7_RecoilPitch", 0, 0.5 * 65536)
        AMGG D 1
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_PlaySound ("Weapons/AMG/Fire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 1, 5)
        TNT1 A 0 A_FireCustomMissile ("S7_RifleCasingSpawn", 0, 0, 8, -5)
        TNT1 A 0 A_FireCustomMissile ("S7_AMGTracer", fRandom (-5.5, 5.5), 1, 3, 2, 0, fRandom (-4.5, 4.5))
        AMGG BC 1 acs_namedExecuteAlways ("S7_RecoilPitch", 0, 0.5 * 65536)
        AMGG D 1
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_PlaySound ("Weapons/AMG/Fire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 1, 5)
        TNT1 A 0 A_FireCustomMissile ("S7_RifleCasingSpawn", 0, 0, 8, -5)
        TNT1 A 0 A_FireCustomMissile ("S7_AMGTracer", fRandom (-5.5, 5.5), 1, 3, 2, 0, fRandom (-4.5, 4.5))
        AMGG BC 1 acs_namedExecuteAlways ("S7_RecoilPitch", 0, 0.5 * 65536)
        AMGG DD 1
        goto Ready2
    
    ChangeFireMode:
        TNT1 A 0 A_PlaySound ("Weapons/ModeChange", CHAN_Weapon)
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 99999)
        TNT1 A 0 A_JumpIfInventory ("S7_AMG_Burst", 1, "ToFullAuto")
        TNT1 A 0 A_GiveInventory ("S7_AMG_Burst", 1)
        AMGG A 5
        goto Ready2
    ToFullAuto:
        TNT1 A 0 A_TakeInventory ("S7_AMG_Burst", 99999)
        AMGG A 5
        goto Ready2
    
    DryFire:
        TNT1 A 0 A_PlaySound ("Weapons/Misc/Dryfire", 7)
        AMGG A 3
        TNT1 A 0 A_JumpIf (callACS ("S7_GetAutoReloading"), "Reload")
        goto Ready2
    
    Reload:
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_AMGMag", 0, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("S7_762x39Cartridges", 1, "TrueReload")
        goto Ready2
    TrueReload:
        TNT1 A 0 A_JumpIfInventory ("S7_AMGMag", 0, "ReloadFinish")
        TNT1 A 0 A_JumpIfInventory ("S7_762x39Cartridges", 1, 1)
        goto ReloadFinish
        TNT1 A 0 A_GiveInventory ("S7_AMGMag", 1)
        TNT1 A 0 A_TakeInventory ("S7_762x39Cartridges", 1)
        loop
    ReloadFinish:
        AMGG A 2
        AMGR ABCDE 2
        AMGR F 2 A_PlaySound ("Weapons/AMG/Out", CHAN_Weapon)
        AMGR GHIJKLMNO 2
        TNT1 A 0 A_PlaySound ("Weapons/AMG/In", CHAN_Weapon)
        AMGR P 2
        AMGR QR 4
        TNT1 A 0 A_PlaySound ("Weapons/AMG/Tap", CHAN_Weapon)
        AMGR S 7
        AMGR TUVWXYA 2
        AMGG A 2
        goto Ready2
    }
}