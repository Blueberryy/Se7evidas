actor S7_AMGMag : ammo {
    inventory.MaxAmount 41
    ammo.BackpackMaxAmount 41
    +ignoreSkill
}

actor S7_AMG : S7_BaseWeapon 30002 {
    inventory.PickupMessage "$AMG"
    tag "$AMG"
    weapon.AmmoType1 "S7_AMGMag"
    weapon.AmmoType2 "S7_Clip"
    weapon.AmmoUse 1
    weapon.AmmoGive 12
    states {
    Spawn:
        AMGG Z -1
        stop
    
    Ready:
    Ready2:
        TNT1 A 0 a_jumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 a_jumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 a_takeInventory ("S7_ChangeFireMode", 1)
        AMGG A 1 a_weaponReady
        loop
    
    Fire:
        TNT1 A 0 a_jumpIfNoAmmo ("DryFire")
        TNT1 A 0 a_playSound ("Weapons/AMG/Fire", CHAN_Weapon)
        TNT1 A 0 a_alertMonsters
        TNT1 A 0 a_fireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 3, 5)
        TNT1 A 0 a_fireCustomMissile ("S7_9mmCasingSpawn", 0, 0, 9, -2)
        TNT1 A 0 a_fireCustomMissile ("S7_TEC9Tracer", fRandom (-5.5, 5.5), 1, 3, 2, 0, fRandom (-4.5, 4.5))
        AMGG BC 1 a_setPitch (pitch - 0.5)
        AMGG D 1
        AMGG D 1 a_refire
        goto Ready2
    
    DryFire:
        TNT1 A 0 a_playSound ("Weapons/Misc/Dryfire", 7)
        AMGG A 3
    
    Reload:
        TNT1 A 0 a_takeInventory ("S7_Reloading", 1)
        TNT1 A 0 a_jumpIfInventory ("S7_AMGMag", 0, "Ready2")
        TNT1 A 0 a_jumpIfInventory ("S7_Clip", 1, "TrueReload")
        goto Ready2
    TrueReload:
        TNT1 A 0 a_jumpIfInventory ("S7_AMGMag", 0, "ReloadFinish")
        TNT1 A 0 a_jumpIfInventory ("S7_Clip", 1, 1)
        goto ReloadFinish
        TNT1 A 0 a_giveInventory ("S7_AMGMag", 1)
        TNT1 A 0 a_takeInventory ("S7_Clip", 1)
        loop
    ReloadFinish:
        AMGG A 2
        AMGR ABCDE 2
        AMGR F 2 a_playSound ("Weapons/AMG/Out", CHAN_Weapon)
        AMGR GHIJKLMNO 2
        TNT1 A 0 a_playSound ("Weapons/AMG/In", CHAN_Weapon)
        AMGR P 2
        AMGR QR 4
        TNT1 A 0 a_playSound ("Weapons/AMG/Tap", CHAN_Weapon)
        AMGR S 7
        AMGR TUVWXYA 2
        AMGG A 2
        goto Ready2
    }
}