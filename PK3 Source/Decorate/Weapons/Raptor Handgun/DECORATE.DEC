actor S7_RaptorClip : ammo {
    inventory.MaxAmount 16
    ammo.BackpackMaxAmount 16
    +ignoreSkill
}

actor S7_RaptorRefire : boolean { }

actor S7_Raptor : S7_BaseWeapon {
    //$Category Se7evidas - Weapons
    //$Title Raptor
    //$Sprite RPTPA0
    inventory.PickupMessage "$RAPTORHANDGUN"
    tag "$RAPTORHANDGUN"
    weapon.AmmoType1 "S7_RaptorClip"
    weapon.AmmoType2 "S7_9mmCartridges"
    weapon.AmmoUse 1
    weapon.AmmoGive 12
    states {
    Spawn:
        RPTP A -1
        loop
    Ready:
    Ready2:
        TNT1 A 0 a_jumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 a_jumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 a_takeInventory ("S7_ChangeFireMode", 1)
        RPTN A 1 a_weaponReady
        TNT1 A 0 a_jumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 a_jumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 a_takeInventory ("S7_ChangeFireMode", 1)
        TNT1 A 0 a_takeInventory ("S7_RaptorRefire", 1)
        RPTN A 1 a_weaponReady
        loop
    /*Deselect:
        TNT1 A 0 a_jumpIfHealthLower (1, "Deselect.DEAD")
        TNT1 A 0 a_lower
        wait
    Select:
        TNT1 A 0 a_raise
        wait*/
    Fire:
        TNT1 A 0 a_jumpIfInventory ("S7_RaptorRefire", 1, "Fire2")
        TNT1 A 0 a_jumpIfNoAmmo ("DryFire")
        TNT1 A 0 a_playSound ("Weapons/Raptor/Fire", CHAN_Weapon)
        TNT1 A 0 a_alertMonsters
        TNT1 A 0 a_fireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 3, 4)
        TNT1 AA 0 a_fireCustomMissile ("S7_9mmCasingSpawn", 0, 0, 7, -2)
        TNT1 AA 0 a_fireCustomMissile ("S7_RaptorTracer", fRandom (-2.8, 2.8), 1, 3, 2, 0, fRandom (-3.7, 3.7))
        RPTN B 1 bright acs_namedExecuteAlways ("S7_RecoilPitch", 0, 0.4 * 65536)
        RPTN C 1 acs_namedExecuteAlways ("S7_RecoilPitch", 0, 0.4 * 65536)
        RPTN DAA 1
        TNT1 A 0 a_giveInventory ("S7_RaptorRefire", 1)
        RPTN AAA 1 a_refire
        RPTN A 7
        goto Ready2
    Fire2:
        TNT1 A 0 a_takeInventory ("S7_RaptorRefire", 1)
        TNT1 A 0 a_jumpIfNoAmmo ("DryFire")
        TNT1 A 0 a_clearRefire
        TNT1 A 0 a_playSound ("Weapons/Raptor/Fire", CHAN_Weapon)
        TNT1 A 0 a_alertMonsters
        TNT1 A 0 a_fireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 3, 4)
        TNT1 AA 0 a_fireCustomMissile ("S7_9mmCasingSpawn", 0, 0, 7, -2)
        TNT1 AA 0 a_fireCustomMissile ("S7_RaptorTracer", fRandom (-2.8, 2.8), 1, 3, 2, 0, fRandom (-3.7, 3.7))
        RPTN B 1 bright acs_namedExecuteAlways ("S7_RecoilPitch", 0, 0.4 * 65536)
        RPTN C 1 acs_namedExecuteAlways ("S7_RecoilPitch", 0, 0.4 * 65536)
        RPTN DAA 1
        RPTN A 7
        goto Ready2
    AltFire:
        TNT1 A 0 a_playSound ("Weapons/Raptor/Melee", CHAN_Weapon)
        RPTM ABC 1
        RPTM D 1 a_customPunch (20, 0, 0, "S7_RaptorMPuff", 85)
        RPTM EFGH 1
        TNT1 A 4
        RPTN A 1
        goto Ready2
    DryFire:
        TNT1 A 0 a_playSound ("Weapons/Misc/Dryfire", 7)
        RPTN A 3
        TNT1 A 0 a_jumpIf (callACS ("S7_GetAutoReloading"), "Reload")
        goto Ready2
    Reload:
        TNT1 A 0 a_takeInventory ("S7_Reloading", 1)
        TNT1 A 0 a_jumpIfInventory ("S7_RaptorClip", 0, "Ready2")
        TNT1 A 0 a_jumpIfInventory ("S7_9mmCartridges", 1, "TrueReload")
        goto Ready2
    TrueReload:
        TNT1 A 0 a_takeInventory ("S7_RaptorRefire", 1)
        TNT1 A 0 a_jumpIfInventory ("S7_RaptorClip", 0, "ReloadFinish")
        TNT1 A 0 a_jumpIfInventory ("S7_9mmCartridges", 1, 1)
        goto ReloadFinish
        TNT1 A 0 a_giveInventory ("S7_RaptorClip", 1)
        TNT1 A 0 a_takeInventory ("S7_9mmCartridges", 1)
        loop
    ReloadFinish:
        RPTR FGGH 1
        RPTR I 4
        RPTR I 25 a_playSound ("Weapons/Raptor/Out", CHAN_Weapon)
        RPTR I 1 a_playSound ("Weapons/Raptor/In", CHAN_Weapon)
        RPTR JDD 1
        RPTR BA 1
        RPTN A 1
        goto Ready2
    Kick.Start:
        RPTN STU 1
        TNT1 A 0 a_jump (256, "Kicking.Mid")
        wait
    Kick.End:
        RPTN Y 4
        RPTN ZXWVUTSR 1
        goto Ready2
    }
}

actor S7_RaptorMPuff {
    +noBlockmap +noGravity +noExtremeDeath +puffOnActors
    renderStyle translucent
    Alpha 0.5
    seeSound "Weapons/Raptor/Melee/Hit"
    attackSound "Weapons/Raptor/Melee/Wall"
    states {
    Crash:
        RMPF AB 2 bright
        RMPF C 1 bright
        stop
    Spawn:
    Melee:
        TNT1 A 0
        stop
    }
}