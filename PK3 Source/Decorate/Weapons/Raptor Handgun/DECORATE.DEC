Actor S7_RaptorClip : Ammo {
    Inventory.MaxAmount 8
    Ammo.BackpackMaxAmount 8
    +IgnoreSkill
}

Actor S7_RaptorRefire : Boolean { }

Actor S7_Raptor : S7_BaseWeapon 30000 {
    //$Category Se7evidas - Weapons
    //$Title Raptor
    //$Sprite RPTPA0
    Inventory.PickupMessage "$RAPTORHANDGUN"
    Tag "$RAPTORHANDGUN"
    Weapon.AmmoType1 "S7_RaptorClip"
    Weapon.AmmoType2 "S7_Clip"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 12
    States {
    Spawn:
        RPTP A -1
        Loop
    Ready:
    Ready2:
        RPTN A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        RPTN A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        RPTN A 0 A_TakeInventory ("S7_ChangeFireMode", 1)
        RPTN A 1 A_WeaponReady
        RPTN A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        RPTN A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        RPTN A 0 A_TakeInventory ("S7_ChangeFireMode", 1)
        RPTN A 0 A_TakeInventory ("S7_RaptorRefire", 1)
        RPTN A 1 A_WeaponReady
        Loop
    Deselect:
        SSGR A 0 A_JumpIfHealthLower (1, "Deselect.DEAD")
        RPTN A 0 A_Lower
        Wait
    Select:
        RPTN A 0 A_Raise
        Wait
    Fire:
        RPTN B 0 A_JumpIfInventory ("S7_RaptorRefire", 1, "Fire2")
        RPTN B 0 A_JumpIfInventory ("S7_RaptorClip", 1, 1)
        Goto DryFire
        RPTN B 0 A_PlaySound ("Weapons/Raptor/Fire", CHAN_WEAPON)
        RPTN B 0 A_SetPitch (pitch - 0.2)
        RPTN B 0 A_AlertMonsters
        RPTN B 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 5, 4)
        RPTN B 1 Bright A_FireCustomMissile ("S7_RaptorTracer", frandom (-2.8, 2.8), 1, 5, 2, 0, frandom (-3.7, 3.7))
        RPTN A 0 A_SetPitch (pitch - 0.2)
        RPTN CDAA 1
        RPTN A 0 A_GiveInventory ("S7_RaptorRefire", 1)
        RPTN AAA 1 A_Refire
        RPTN A 7
        Goto Ready2
    Fire2:
        RPTN A 0 A_TakeInventory ("S7_RaptorRefire", 1)
        RPTN A 0 A_JumpIfInventory ("S7_RaptorClip", 1, 1)
        Goto DryFire
        RPTN A 0 A_ClearRefire
        RPTN A 0 A_PlaySound ("Weapons/Raptor/Fire", CHAN_WEAPON)
        RPTN A 0 A_GunFlash
        RPTN A 0 A_SetPitch (pitch - 0.2)
        RPTN A 0 A_AlertMonsters
        RPTN B 1 Bright A_FireCustomMissile ("S7_RaptorTracer", frandom (-2.8, 2.8), 1, 5, 2, 0, frandom (-3.7, 3.7))
        RPTN A 0 A_SetPitch (pitch - 0.2)
        RPTN CD 1
        RPTN AA 1
        RPTN A 7
        Goto Ready2
    AltFire:
        RPTM A 0 A_PlaySound ("Weapons/Raptor/Melee", CHAN_WEAPON)
        RPTM ABC 1
        RPTM D 1 A_CustomPunch (20, 0, 0, "S7_RaptorMPuff", 85)
        RPTM EFGH 1
        TNT1 A 4
        RPTN A 1
        Goto Ready2
    DryFire:
        RPTN A 0 A_PlaySound ("Weapons/Misc/Dryfire", 7)
        RPTN A 3
        Goto Reload
    Reload:
        RPTR A 0 A_TakeInventory ("S7_Reloading", 1)
        RPTR A 0 A_JumpIfInventory ("S7_RaptorClip", 0, "Ready2")
        RPTR A 0 A_JumpIfInventory ("S7_Clip", 1, "TrueReload")
        Goto Ready2
    TrueReload:
        RPTR A 0 A_TakeInventory ("S7_RaptorRefire", 1)
        RPTR A 0 A_JumpIfInventory ("S7_RaptorClip", 0, "ReloadFinish")
        RPTR A 0 A_JumpIfInventory ("S7_Clip", 1, 1)
        Goto ReloadFinish
        RPTR A 0 A_GiveInventory ("S7_RaptorClip", 1)
        RPTR A 0 A_TakeInventory ("S7_Clip", 1)
        Loop
    ReloadFinish:
        RPTR FGGH 1
        RPTR I 4
        RPTR I 25 A_PlaySound ("Weapons/Raptor/Out", CHAN_WEAPON)
        RPTR I 1 A_PlaySound ("Weapons/Raptor/In", CHAN_WEAPON)
        RPTR JDD 1
        RPTR BA 1
        RPTN A 1
        Goto Ready2
    Kick.Start:
        RPTN STU 1
        RPTN A 0 A_Jump (256, "Kicking.Mid")
        Wait
    Kick.End:
        RPTN Y 4
        RPTN ZXWVUTSR 1
        Goto Ready2
    }
}

Actor S7_RaptorMPuff {
    +NoBlockmap +NoGravity +NoExtremeDeath +PuffOnActors
    RenderStyle Translucent
    Alpha 0.5
    SeeSound "Weapons/Raptor/Melee/Hit"
    AttackSound "Weapons/Raptor/Melee/Wall"
    States {
    Crash:
        RMPF AB 2 Bright
        RMPF C 1 Bright
        Stop
    Spawn:
    Melee:
        TNT1 A 0
        Stop
    }
}