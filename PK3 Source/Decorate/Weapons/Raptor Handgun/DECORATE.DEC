Actor S7_RaptorClip : Ammo {
	Inventory.MaxAmount 8
	Ammo.BackpackMaxAmount 8
	+IgnoreSkill
}

Actor S7_RaptorRefire : Boolean { }

Actor S7_Raptor : S7_BaseWeapon {
	Inventory.PickupMessage "$RAPTORHANDGUN"
	Tag "$RAPTORHANDGUN"
	Weapon.AmmoType1 "S7_RaptorClip"
	Weapon.AmmoType2 "S7_Clip"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 12
	States {
	Spawn:
		RPTP A -1
		Loop
	Ready:
	Ready2:
		RPTN A 0 A_JumpIfInventory ("S7_Kicking", 1, "Kicking")
		"----" A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
		"----" A 0 A_TakeInventory ("S7_ChangeFireMode", 1)
		RPTN A 1 A_WeaponReady
		"----" A 0 A_JumpIfInventory ("S7_Kicking", 1, "Kicking")
		"----" A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
		"----" A 0 A_TakeInventory ("S7_RaptorRefire", 1)
		RPTN A 1 A_WeaponReady
		Loop
	Deselect:
		RPTN A 0 A_Lower
		Wait
	Select:
		RPTN A 0 A_Raise
		Wait
	Fire:
		"----" A 0 A_JumpIfInventory ("S7_RaptorRefire", 1, "Fire2")
		"----" A 0 A_JumpIfInventory ("S7_RaptorClip", 1, 1)
		Goto DryFire
		"----" A 0 A_PlaySound ("Weapons/Raptor/Fire", CHAN_WEAPON)
		"----" A 0 A_GunFlash
		"----" A 0 A_SetPitch (pitch - 0.2)
		"----" A 0 A_AlertMonsters
		RPTN B 1 Bright A_FireBullets (3.0, 4.0, -1, 6, "S7_BulletPuff", 1)
		"----" A 0 A_SetPitch (pitch - 0.2)
		RPTN CDAA 1
		"----" A 0 A_GiveInventory ("S7_RaptorRefire", 1)
		RPTN AAA 1 A_Refire
		RPTN A 7
		Goto Ready2
	Fire2:
		"----" A 0 A_TakeInventory ("S7_RaptorRefire", 1)
		"----" A 0 A_JumpIfInventory ("S7_RaptorClip", 1, 1)
		Goto DryFire
		"----" A 0 A_ClearRefire
		"----" A 0 A_PlaySound ("Weapons/Raptor/Fire", CHAN_WEAPON)
		"----" A 0 A_GunFlash
		"----" A 0 A_SetPitch (pitch - 0.2)
		"----" A 0 A_AlertMonsters
		RPTN B 1 Bright A_FireBullets (3.0, 4.0, -1, 6, "S7_BulletPuff", 1)
		"----" A 0 A_SetPitch (pitch - 0.2)
		RPTN CD 1
		RPTN AA 1
		RPTN A 7
		Goto Ready2
	AltFire:
		"----" A 0 A_PlaySound ("Weapons/Raptor/Melee", CHAN_WEAPON)
		RPTM ABC 1
		RPTM D 1 A_CustomPunch (20, 0, 0, "S7_RaptorMPuff", 85)
		RPTM EFGH 1
		TNT1 A 4
		RPTN A 1
		Goto Ready2
	Reload:
		"----" A 0 A_TakeInventory ("S7_Reloading", 1)
		"----" A 0 A_JumpIfInventory ("S7_RaptorClip", 0, "Ready2")
		"----" A 0 A_JumpIfInventory ("S7_Clip", 1, "TrueReload")
		Goto Ready2
	TrueReload:
		"----" A 0 A_TakeInventory ("S7_RaptorRefire", 1)
		"----" A 0 A_JumpIfInventory ("S7_RaptorClip", 0, "ReloadFinish")
		"----" A 0 A_JumpIfInventory ("S7_Clip", 1, 1)
		Goto ReloadFinish
		"----" A 0 A_GiveInventory ("S7_RaptorClip", 1)
		"----" A 0 A_TakeInventory ("S7_Clip", 1)
		Loop
	ReloadFinish:
		RPTR FGGH 1
		RPTR I 4
		RPTR I 25 A_PlaySound ("Weapons/Raptor/Out", CHAN_WEAPON)
		RPTR I 1 A_PlaySound ("Weapons/Raptor/In", CHAN_WEAPON)
		RPTR JDD 1
		RPTR BA 1
		RPTN A 1
		Goto Ready2
	}
}

Actor S7_RaptorMPuff {
	+NoBlockmap +NoGravity +NoExtremeDeath -PuffOnActors
	RenderStyle Translucent
	Alpha 0.5
	SeeSound "Weapons/Raptor/Melee/Hit"
	AttackSound "Weapons/Raptor/Melee/Wall"
	States {
	Spawn:
		RMPF AB 2
		RMPF C 1
		Stop
	}
}