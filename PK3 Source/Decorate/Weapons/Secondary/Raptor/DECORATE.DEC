actor S7_RaptorClip : ammo {
    inventory.maxAmount 16
    ammo.backpackMaxAmount 16

    +ignoreSkill
}

actor S7_RaptorRefire : boolean { }

actor S7_Raptor : S7_BaseWeapon {
    tag "$RAPTOR"
    inventory.pickupMessage "$RAPTOR"
    weapon.ammoType1 "S7_RaptorClip"
    weapon.ammoType2 "S7_9mmCartridges"
    weapon.ammoUse 1
    weapon.ammoGive 0
    
    states {
    Spawn:
        RPTP A -1
        loop
    Ready:
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 1)
        RPTN A 1 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 1)
        TNT1 A 0 A_TakeInventory ("S7_RaptorRefire", 1)
        RPTN A 1 A_WeaponReady
        loop
    
    Fire:
        TNT1 A 0 A_JumpIfInventory ("S7_RaptorRefire", 1, "Fire2")
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_PlaySound ("Weapons/Raptor/Fire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 3, 4)
        TNT1 AA 0 A_FireCustomMissile ("S7_9mmCasingSpawn", 0, 0, 7, -2)
        TNT1 AA 0 A_FireCustomMissile ("S7_RaptorTracer", fRandom [weaponSpread] (-2.8, 2.8), 1, 5, 0, 0, fRandom [weaponSpread] (-3.7, 3.7))
        RPTN B 1 bright A_SetPitch (pitch - 0.8, SPF_Interpolate)
        RPTN C 1 A_SetPitch (pitch - 0.8, SPF_Interpolate)
        RPTN AAA 1
        TNT1 A 0 A_GiveInventory ("S7_RaptorRefire", 1)
        RPTN AAA 1 A_Refire
        RPTN A 7
        goto Ready2
    Fire2:
        TNT1 A 0 A_TakeInventory ("S7_RaptorRefire", 1)
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_PlaySound ("Weapons/Raptor/Fire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 3, 4)
        TNT1 AA 0 A_FireCustomMissile ("S7_9mmCasingSpawn", 0, 0, 7, -2)
        TNT1 AA 0 A_FireCustomMissile ("S7_RaptorTracer", fRandom [weaponSpread] (-2.8, 2.8), 1, 5, 0, 0, fRandom [weaponSpread] (-3.7, 3.7))
        RPTN B 1 bright A_SetPitch (pitch - 0.8, SPF_Interpolate)
        RPTN C 1 A_SetPitch (pitch - 0.8, SPF_Interpolate)
        RPTN AAA 1
        RPTN A 7
        goto Ready2
    AltFire:
        TNT1 A 0 A_PlaySound ("Weapons/Raptor/Melee", CHAN_Weapon)
        RPTM ABC 1
        RPTM D 1 A_CustomPunch (20, 0, 0, "S7_RaptorMPuff", 85)
        RPTM EFGH 1
        TNT1 A 4
        RPTN A 1
        goto Ready2
    DryFire:
        TNT1 A 0 A_ClearRefire
        RPTN A 3 A_PlaySound ("Weapons/DryFirePistol", 7)
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "Reload")
        goto Ready2
    Reload:
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_RaptorClip", 0, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("S7_9mmCartridges", 1, "TrueReload")
        goto Ready2
    TrueReload:
        TNT1 A 0 A_TakeInventory ("S7_RaptorRefire", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_RaptorClip", 0, "ReloadFinish")
        TNT1 A 0 A_JumpIfInventory ("S7_9mmCartridges", 1, 1)
        goto ReloadFinish
        TNT1 A 0 A_GiveInventory ("S7_RaptorClip", 1)
        TNT1 A 0 A_TakeInventory ("S7_9mmCartridges", 1)
        loop
    ReloadFinish:
        RPTR FGGH 1
        RPTR I 4
        RPTR I 23 A_PlaySound ("Weapons/Raptor/Out", CHAN_Weapon)
        RPTR I 1 A_PlaySound ("Weapons/Raptor/In", CHAN_Weapon)
        RPTR JDD 1
        RPTR BA 1
        RPTN A 1
        goto Ready2
    }
}

actor S7_RaptorMPuff {
    renderStyle translucent
    alpha 0.5
    seeSound "Weapons/Raptor/Melee/Hit"
    attackSound "Weapons/Raptor/Melee/Wall"

    +noBlockmap +noGravity +noExtremeDeath +puffOnActors
    
    states {
    Crash:
        RMPF AB 2 bright
        RMPF C 1 bright
        stop
    Spawn:
    Melee:
        TNT1 A 0
        stop
    }
}