actor S7_TEC9Clip : ammo {
    inventory.maxAmount 32
    ammo.backpackMaxAmount 32

    +ignoreSkill
}

actor S7_TEC9_Flash : counter { inventory.maxAmount 2 }
actor S7_TEC9_Burst : boolean { }

actor S7_TEC9 : S7_BaseWeapon {
    tag "$TEC9"
    inventory.pickupMessage "$TEC9"
    weapon.ammoType1 "S7_TEC9Clip"
    weapon.ammoType2 "S7_9mmCartridges"
    weapon.ammoUse 1
    weapon.ammoGive 12
    
    states {
    Spawn:
        TEC9 Z -1
        loop

    Ready:
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_ChangeFireMode", 1, "ChangeFireMode")
        TEC9 A 1 A_WeaponReady
        loop

    Fire:
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TEC9 B 1
        TNT1 A 0 A_JumpIfInventory ("S7_TEC9_Burst", 1, "Fire.Burst")
    Hold:
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_PlaySound ("Weapons/TEC-9/Fire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2.8, 4.5)
        TNT1 A 0 A_FireCustomMissile ("S7_9mmCasingSpawn", 0, 0, 9, -2)
        TNT1 A 0 A_FireCustomMissile ("S7_TEC9Tracer", fRandom [weaponSpread] (-5.5, 5.5), 1, 3, 2, 0, fRandom [weaponSpread] (-4.5, 4.5))
        TEC9 CD 1 ACS_NamedExecuteAlways ("S7_RecoilPitch", 0, 0.8 * 65536)
        TEC9 B 1 A_Refire
        TEC9 A 7 A_TakeInventory ("S7_TEC9_Flash", 999999)
        goto Ready2
    Fire.Burst:
        TNT1 A 0 A_PlaySound ("Weapons/TEC-9/Fire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2.8, 4.5)
        TNT1 A 0 A_FireCustomMissile ("S7_9mmCasingSpawn", 0, 0, 9, -2)
        TNT1 A 0 A_FireCustomMissile ("S7_TEC9Tracer", fRandom [weaponSpread] (-5.5, 5.5), 1, 3, 2, 0, fRandom [weaponSpread] (-4.5, 4.5))
        TEC9 CD 1 ACS_NamedExecuteAlways ("S7_RecoilPitch", 0, 0.8 * 65536)
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_PlaySound ("Weapons/TEC-9/Fire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2.8, 4.5)
        TNT1 A 0 A_FireCustomMissile ("S7_9mmCasingSpawn", 0, 0, 9, -2)
        TNT1 A 0 A_FireCustomMissile ("S7_TEC9Tracer", fRandom [weaponSpread] (-5.5, 5.5), 1, 3, 2, 0, fRandom [weaponSpread] (-4.5, 4.5))
        TEC9 CD 1 ACS_NamedExecuteAlways ("S7_RecoilPitch", 0, 0.8 * 65536)
        TNT1 A 0 A_JumpIfNoAmmo ("DryFire")
        TNT1 A 0 A_PlaySound ("Weapons/TEC-9/Fire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_GunFlash
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 2.8, 4.5)
        TNT1 A 0 A_FireCustomMissile ("S7_9mmCasingSpawn", 0, 0, 9, -2)
        TNT1 A 0 A_FireCustomMissile ("S7_TEC9Tracer", fRandom [weaponSpread] (-5.5, 5.5), 1, 3, 2, 0, fRandom [weaponSpread] (-4.5, 4.5))
        TEC9 CD 1 ACS_NamedExecuteAlways ("S7_RecoilPitch", 0, 0.8 * 65536)
        TEC9 B 1
        TEC9 A 7 A_TakeInventory ("S7_TEC9_Flash", 999999)
        goto Ready2
    ChangeFireMode:
        TNT1 A 0 A_PlaySound ("Weapons/ModeChange", CHAN_Weapon)
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 99999)
        TNT1 A 0 A_JumpIfInventory ("S7_TEC9_Burst", 1, "ToFullAuto")
        TEC9 A 5 A_GiveInventory ("S7_TEC9_Burst", 1)
        goto Ready2
    ToFullAuto:
        TEC9 A 5 A_TakeInventory ("S7_TEC9_Burst", 99999)
        goto Ready2

    DryFire:
        TEC9 A 3 A_PlaySound ("Weapons/Misc/Dryfire", 7)
        TNT1 A 0 A_JumpIf (callACS ("S7_GetAutoReloading"), "Reload")
        goto Ready2
        
    Reload:
        TNT1 A 0 A_ClearRefire
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_TEC9Clip", 0, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("S7_9mmCartridges", 1, "TrueReload")
        goto Ready2
    TrueReload:
        TNT1 A 0 A_JumpIfInventory ("S7_TEC9Clip", 0, "ReloadFinish")
        TNT1 A 0 A_JumpIfInventory ("S7_9mmCartridges", 1, 1)
        goto ReloadFinish
        TNT1 A 0 A_GiveInventory ("S7_TEC9Clip", 1)
        TNT1 A 0 A_TakeInventory ("S7_9mmCartridges", 1)
        loop
    ReloadFinish:
        TNT1 A 0 A_PlaySound ("Weapons/TEC-9/Out", CHAN_Weapon)
        TECR ABCDEEEEE 1
        TECR FGH 2
        TNT1 A 0 A_PlaySound ("Weapons/TEC-9/In", CHAN_Weapon)
        TECR IJKLMNO 2
        TNT1 A 0 A_PlaySound ("Weapons/TEC-9/Tap", CHAN_Weapon)
        TECR PQRRRSTU 2
        goto Ready2
    
    // Muzzle flashes
    Flash:
        TNT1 A 0 A_JumpIfInventory ("S7_TEC9_Flash", 2, "FlashSet2")
        TNT1 A 0 A_JumpIfInventory ("S7_TEC9_Flash", 1, "FlashSet1")
        TNT1 A 0 A_GiveInventory ("S7_TEC9_Flash", random [sfx] (1, 2))
        loop
    
    FlashSet1:
        TNT1 A 0 bright A_Jump (256, "FlashSet1.Flash1", "FlashSet1.Flash2")
    FlashSet1.Flash1:
        TECF A 1 Bright
        stop
    FlashSet1.Flash2:
        TECF B 1 Bright
        stop
    
    FlashSet2:
        TNT1 A 0 bright A_Jump (256, "FlashSet2.Flash1", "FlashSet2.Flash2")
    FlashSet2.Flash1:
        TECF C 1 Bright
        stop
    FlashSet2.Flash2:
        TECF D 1 Bright
        stop
    }
}