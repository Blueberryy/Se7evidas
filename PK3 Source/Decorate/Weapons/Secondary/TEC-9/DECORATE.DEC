Actor S7_TEC9Clip : Ammo {
	Inventory.MaxAmount 32
	Ammo.BackpackMaxAmount 32
	+IgnoreSkill
}

Actor S7_TEC9_Flash : Counter { Inventory.MaxAmount 2 }
Actor S7_TEC9_Burst : Boolean { }

Actor S7_TEC9 : S7_BaseWeapon 30002 {
	Inventory.PickupMessage "$TEC9"
	Tag "$TEC9"
	Weapon.AmmoType1 "S7_TEC9Clip"
	Weapon.AmmoType2 "S7_Clip"
	Weapon.AmmoUse 1
	Weapon.AmmoGive 12
	States {
	Spawn:
		RPTP A -1
		Loop
	Ready:
	Ready2:
		TEC9 A 0 A_JumpIfInventory ("S7_Kicking", 1, "Kicking")
		TEC9 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
		TEC9 A 0 A_JumpIfInventory ("S7_ChangeFireMode", 1, "ChangeFireMode")
		TEC9 A 1 A_WeaponReady
		Loop
	Deselect:
		SSGR A 0 A_JumpIfHealthLower (1, "Deselect.DEAD")
		TEC9 A 0 A_Lower
		Wait
	Select:
		TEC9 A 0 A_Raise
		Wait
	Fire:
		TEC9 B 0 A_JumpIfInventory ("S7_TEC9_Burst", 1, "Fire.Burst")
		TEC9 B 0 A_JumpIfInventory ("S7_TEC9Clip", 1, 1)
		Goto DryFire
		TEC9 B 1
	Hold:
		TEC9 C 0 A_JumpIfInventory ("S7_TEC9Clip", 1, 1)
		Goto DryFire
		TEC9 C 0 A_PlaySound ("Weapons/TEC-9/Fire", CHAN_WEAPON)
		TEC9 C 0 A_SetPitch (pitch - 0.5)
		TEC9 C 0 A_AlertMonsters
		TEC9 C 0 A_GunFlash
		TEC9 C 1 A_FireBullets (4.5, 5.5, -1, Random (3, 4), "S7_BulletPuff", 1)
		TEC9 D 1 A_SetPitch (pitch - 0.5)
		TEC9 B 1 A_Refire
		TEC9 A 0 A_TakeInventory ("S7_TEC9_Flash", 999999)
		TEC9 A 7
		Goto Ready2
	Fire.Burst:
		TEC9 B 0 A_JumpIfInventory ("S7_TEC9Clip", 1, 1)
		Goto DryFire
		TEC9 B 1
		TEC9 C 0 A_PlaySound ("Weapons/TEC-9/Fire", CHAN_WEAPON)
		TEC9 C 0 A_SetPitch (pitch - 0.5)
		TEC9 C 0 A_AlertMonsters
		TEC9 C 0 A_GunFlash
		TEC9 C 1 A_FireBullets (4.5, 5.5, -1, Random (3, 4), "S7_BulletPuff", 1)
		TEC9 D 1 A_SetPitch (pitch - 0.5)
		TEC9 C 0 A_PlaySound ("Weapons/TEC-9/Fire", CHAN_WEAPON)
		TEC9 C 0 A_SetPitch (pitch - 0.5)
		TEC9 C 0 A_AlertMonsters
		TEC9 C 0 A_GunFlash
		TEC9 C 1 A_FireBullets (4.5, 5.5, -1, Random (3, 4), "S7_BulletPuff", 1)
		TEC9 D 1 A_SetPitch (pitch - 0.5)
		TEC9 C 0 A_PlaySound ("Weapons/TEC-9/Fire", CHAN_WEAPON)
		TEC9 C 0 A_SetPitch (pitch - 0.5)
		TEC9 C 0 A_AlertMonsters
		TEC9 C 0 A_GunFlash
		TEC9 C 1 A_FireBullets (4.5, 5.5, -1, Random (3, 4), "S7_BulletPuff", 1)
		TEC9 D 1 A_SetPitch (pitch - 0.5)
		TEC9 B 1
		TEC9 A 0 A_TakeInventory ("S7_TEC9_Flash", 999999)
		TEC9 A 7
		Goto Ready2
	ChangeFireMode:
		TEC9 A 0 A_PlaySound ("Weapons/ModeChange", CHAN_WEAPON)
		TEC9 A 0 A_TakeInventory ("S7_ChangeFireMode", 99999)
		TEC9 A 0 A_JumpIfInventory ("S7_TEC9_Burst", 1, "ToFullAuto")
		TEC9 A 0 A_GiveInventory ("S7_TEC9_Burst", 1)
		TEC9 A 5
		Goto Ready2
	ToFullAuto:
		TEC9 A 0 A_TakeInventory ("S7_TEC9_Burst", 99999)
		TEC9 A 5
		Goto Ready2
	Reload:
		TECR A 0 A_TakeInventory ("S7_Reloading", 1)
		TECR A 0 A_JumpIfInventory ("S7_TEC9Clip", 0, "Ready2")
		TECR A 0 A_JumpIfInventory ("S7_Clip", 1, "TrueReload")
		Goto Ready2
	TrueReload:
		TECR A 0 A_JumpIfInventory ("S7_TEC9Clip", 0, "ReloadFinish")
		TECR A 0 A_JumpIfInventory ("S7_Clip", 1, 1)
		Goto ReloadFinish
		TECR A 0 A_GiveInventory ("S7_TEC9Clip", 1)
		TECR A 0 A_TakeInventory ("S7_Clip", 1)
		Loop
	ReloadFinish:
		TECR A 0 A_PlaySound ("Weapons/TEC-9/Out", CHAN_WEAPON)
		TECR ABCDEEEEE 1
		TECR FGH 2
		TECR I 0 A_PlaySound ("Weapons/TEC-9/In", CHAN_WEAPON)
		TECR IJKLMNO 2
		TECR P 0 A_PlaySound ("Weapons/TEC-9/Tap", CHAN_WEAPON)
		TECR PQRRRSTU 2
		Goto Ready2
	
	Kick.Start:
		TECK BCD 1
		TECK A 0 A_Jump (256, "Kicking.Mid")
		Wait
	Kick.End:
		TECK H 4
		TECK IGFEDCBA 1
		Goto Ready2
	
	// Muzzle flashes
	Flash:
		TECF A 0 A_JumpIfInventory ("S7_TEC9_Flash", 2, "FlashSet2")
		TECF A 0 A_JumpIfInventory ("S7_TEC9_Flash", 1, "FlashSet1")
		TECF A 0 A_GiveInventory ("S7_TEC9_Flash", Random (1, 2))
		Loop
	
	FlashSet1:
		TECF A 0 Bright A_Jump (256, "FlashSet1.Flash1", "FlashSet1.Flash2")
	FlashSet1.Flash1:
		TECF A 0 A_JumpIf (ACS_ExecuteWithResult (S7_RunningInZDoom) == 0, "FlashSet1.Flash1.Zandronum")
		TECF A 1 Bright
		Stop
	FlashSet1.Flash2:
		TECF B 0 A_JumpIf (ACS_ExecuteWithResult (S7_RunningInZDoom) == 0, "FlashSet1.Flash2.Zandronum")
		TECF B 1 Bright
		Stop
	
	FlashSet2:
		TECF A 0 Bright A_Jump (256, "FlashSet2.Flash1", "FlashSet2.Flash2")
	FlashSet2.Flash1:
		TECF C 0 A_JumpIf (ACS_ExecuteWithResult (S7_RunningInZDoom) == 0, "FlashSet2.Flash1.Zandronum")
		TECF C 1 Bright
		Stop
	FlashSet2.Flash2:
		TECF D 0 A_JumpIf (ACS_ExecuteWithResult (S7_RunningInZDoom) == 0, "FlashSet2.Flash2.Zandronum")
		TECF D 1 Bright
		Stop
	
	FlashSet1.Flash1.Zandronum:
		TECF A 2 Bright
		Stop
	FlashSet1.Flash2.Zandronum:
		TECF B 2 Bright
		Stop
	
	FlashSet2.Flash1.Zandronum:
		TECF C 2 Bright
		Stop
	FlashSet2.Flash2.Zandronum:
		TECF D 2 Bright
		Stop
	}
}