actor S7_TEC9Clip : ammo {
    inventory.MaxAmount 32
    ammo.BackpackMaxAmount 32
    +ignoreSkill
}

actor S7_TEC9_Flash : counter { inventory.MaxAmount 2 }
actor S7_TEC9_Burst : boolean { }

actor S7_TEC9 : S7_BaseWeapon {
    inventory.PickupMessage "$TEC9"
    tag "$TEC9"
    weapon.AmmoType1 "S7_TEC9Clip"
    weapon.AmmoType2 "S7_9mmCartridges"
    weapon.AmmoUse 1
    weapon.AmmoGive 12
    states {
    Spawn:
        RPTP A -1
        loop
    Ready:
    Ready2:
        TNT1 A 0 a_jumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 a_jumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 a_jumpIfInventory ("S7_ChangeFireMode", 1, "ChangeFireMode")
        TEC9 A 1 a_weaponReady
        loop
    /*Deselect:
        TNT1 A 0 a_jumpIfHealthLower (1, "Deselect.DEAD")
        TNT1 A 0 a_lower
        wait
    Select:
        TNT1 A 0 a_raise
        wait*/
    Fire:
        TNT1 A 0 a_jumpIfInventory ("S7_TEC9_Burst", 1, "Fire.Burst")
        TNT1 A 0 a_jumpIfInventory ("S7_TEC9Clip", 1, 1)
        goto DryFire
        TEC9 B 1
    Hold:
        TNT1 A 0 a_jumpIfNoAmmo ("DryFire")
        TNT1 A 0 a_playSound ("Weapons/TEC-9/Fire", CHAN_Weapon)
        TNT1 A 0 a_alertMonsters
        TNT1 A 0 a_gunFlash
        TNT1 A 0 a_fireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 3, 5)
        TNT1 A 0 a_fireCustomMissile ("S7_9mmCasingSpawn", 0, 0, 9, -2)
        TNT1 A 0 a_fireCustomMissile ("S7_TEC9Tracer", fRandom (-5.5, 5.5), 1, 3, 2, 0, fRandom (-4.5, 4.5))
        TEC9 C 1 a_setPitch (pitch - 0.5)
        TEC9 D 1 a_setPitch (pitch - 0.5)
        TEC9 B 1 a_refire
        TNT1 A 0 a_takeInventory ("S7_TEC9_Flash", 999999)
        TEC9 A 7
        goto Ready2
    Fire.Burst:
        TNT1 A 0 a_jumpIfNoAmmo ("DryFire")
        TEC9 B 1
        TNT1 A 0 a_playSound ("Weapons/TEC-9/Fire", CHAN_Weapon)
        TNT1 A 0 a_alertMonsters
        TNT1 A 0 a_gunFlash
        TNT1 A 0 a_fireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 3, 5)
        TNT1 A 0 a_fireCustomMissile ("S7_9mmCasingSpawn", 0, 0, 9, -2)
        TNT1 A 0 a_fireCustomMissile ("S7_TEC9Tracer", fRandom (-5.5, 5.5), 1, 3, 2, 0, fRandom (-4.5, 4.5))
        TEC9 C 1 a_setPitch (pitch - 0.5)
        TEC9 D 1 a_setPitch (pitch - 0.5)
        TNT1 A 0 a_jumpIfNoAmmo ("DryFire")
        TNT1 A 0 a_playSound ("Weapons/TEC-9/Fire", CHAN_Weapon)
        TNT1 A 0 a_alertMonsters
        TNT1 A 0 a_gunFlash
        TNT1 A 0 a_fireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 3, 5)
        TNT1 A 0 a_fireCustomMissile ("S7_9mmCasingSpawn", 0, 0, 9, -2)
        TNT1 A 0 a_fireCustomMissile ("S7_TEC9Tracer", fRandom (-5.5, 5.5), 1, 3, 2, 0, fRandom (-4.5, 4.5))
        TEC9 C 1 a_setPitch (pitch - 0.5)
        TEC9 D 1 a_setPitch (pitch - 0.5)
        TNT1 A 0 a_jumpIfNoAmmo ("DryFire")
        TNT1 A 0 a_playSound ("Weapons/TEC-9/Fire", CHAN_Weapon)
        TNT1 A 0 a_alertMonsters
        TNT1 A 0 a_gunFlash
        TNT1 A 0 a_fireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 3, 5)
        TNT1 A 0 a_fireCustomMissile ("S7_9mmCasingSpawn", 0, 0, 9, -2)
        TNT1 A 0 a_fireCustomMissile ("S7_TEC9Tracer", fRandom (-5.5, 5.5), 1, 3, 2, 0, fRandom (-4.5, 4.5))
        TEC9 C 1 a_setPitch (pitch - 0.5)
        TEC9 D 1 a_setPitch (pitch - 0.5)
        TEC9 B 1
        TNT1 A 0 a_takeInventory ("S7_TEC9_Flash", 999999)
        TEC9 A 7
        goto Ready2
    ChangeFireMode:
        TNT1 A 0 a_playSound ("Weapons/ModeChange", CHAN_Weapon)
        TNT1 A 0 a_takeInventory ("S7_ChangeFireMode", 99999)
        TNT1 A 0 a_jumpIfInventory ("S7_TEC9_Burst", 1, "ToFullAuto")
        TNT1 A 0 a_giveInventory ("S7_TEC9_Burst", 1)
        TEC9 A 5
        goto Ready2
    ToFullAuto:
        TNT1 A 0 a_takeInventory ("S7_TEC9_Burst", 99999)
        TEC9 A 5
        goto Ready2
    DryFire:
        TNT1 A 0 a_playSound ("Weapons/Misc/Dryfire", 7)
        TEC9 A 3
        goto Reload
    Reload:
        TNT1 A 0 a_clearRefire
        TNT1 A 0 a_takeInventory ("S7_Reloading", 1)
        TNT1 A 0 a_jumpIfInventory ("S7_TEC9Clip", 0, "Ready2")
        TNT1 A 0 a_jumpIfInventory ("S7_9mmCartridges", 1, "TrueReload")
        goto Ready2
    TrueReload:
        TNT1 A 0 a_jumpIfInventory ("S7_TEC9Clip", 0, "ReloadFinish")
        TNT1 A 0 a_jumpIfInventory ("S7_9mmCartridges", 1, 1)
        goto ReloadFinish
        TNT1 A 0 a_giveInventory ("S7_TEC9Clip", 1)
        TNT1 A 0 a_takeInventory ("S7_9mmCartridges", 1)
        loop
    ReloadFinish:
        TNT1 A 0 a_playSound ("Weapons/TEC-9/Out", CHAN_Weapon)
        TECR ABCDEEEEE 1
        TECR FGH 2
        TNT1 A 0 a_playSound ("Weapons/TEC-9/In", CHAN_Weapon)
        TECR IJKLMNO 2
        TNT1 A 0 a_playSound ("Weapons/TEC-9/Tap", CHAN_Weapon)
        TECR PQRRRSTU 2
        goto Ready2
    
    // Muzzle flashes
    Flash:
        TNT1 A 0 a_jumpIfInventory ("S7_TEC9_Flash", 2, "FlashSet2")
        TNT1 A 0 a_jumpIfInventory ("S7_TEC9_Flash", 1, "FlashSet1")
        TNT1 A 0 a_giveInventory ("S7_TEC9_Flash", Random (1, 2))
        loop
    
    FlashSet1:
        TNT1 A 0 bright a_jump (256, "FlashSet1.Flash1", "FlashSet1.Flash2")
    FlashSet1.Flash1:
        TECF A 1 Bright
        stop
    FlashSet1.Flash2:
        TECF B 1 Bright
        stop
    
    FlashSet2:
        TNT1 A 0 bright a_jump (256, "FlashSet2.Flash1", "FlashSet2.Flash2")
    FlashSet2.Flash1:
        TECF C 1 Bright
        stop
    FlashSet2.Flash2:
        TECF D 1 Bright
        stop
    }
}