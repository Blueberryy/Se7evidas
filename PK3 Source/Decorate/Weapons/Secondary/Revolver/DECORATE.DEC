Actor S7_RevolverClip : Ammo {
    Inventory.MaxAmount 6
    Ammo.BackpackMaxAmount 6
    +IgnoreSkill
}

Actor S7_Revolver : S7_BaseWeapon {
    Inventory.PickupMessage "$REVOLVER"
    Tag "$REVOLVER"
    Weapon.AmmoType1 "S7_RevolverClip"
    Weapon.AmmoType2 "S7_Clip"
    Weapon.AmmoUse 1
    Weapon.AmmoGive 6
    States {
    Spawn:
        RPTP A -1
        Loop
    Ready:
    Ready2:
        RVLV A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        RVLV A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        RVLV A 0 A_TakeInventory ("S7_ChangeFireMode", 1)
        RVLV A 1 A_WeaponReady
        Loop
    Deselect:
        RVLV A 0 A_JumpIfHealthLower (1, "Deselect.DEAD")
        RVLV A 0 A_Lower
        Wait
    Select:
        RVLV A 0 A_Raise
        Wait
    Fire:
        RVLV B 0 A_JumpIfInventory ("S7_RevolverClip", 1, 1)
        Goto DryFire
        RVLV B 0 A_PlaySound ("Weapons/Revolver/Fire", CHAN_WEAPON)
        RVLV B 0 A_SetPitch (pitch - 0.8)
        RVLV B 0 A_AlertMonsters
        RVLV B 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 5, 4)
        RVLV B 1 Bright A_FireCustomMissile ("S7_RevolverTracer", frandom (-1.8, 1.8), 1, 5, 2, 0, frandom (-2.7, 2.7))
        RVLV C 0 A_SetPitch (pitch - 0.8)
        RVLV CDEF 1
        RVLV A 15
        Goto Ready2
    DryFire:
        RVLV A 3 A_PlaySound ("Weapons/Revolver/Dryfire", 7)
        Goto Ready2
    Reload:
        REVR A 0 A_TakeInventory ("S7_Reloading", 1)
        REVR A 0 A_JumpIfInventory ("S7_RevolverClip", 0, "Ready2")
        REVR A 0 A_JumpIfInventory ("S7_Clip", 1, "TrueReload")
        Goto Ready2
    TrueReload:
        REVR A 0 A_JumpIfInventory ("S7_RevolverClip", 0, "ReloadFinish")
        REVR A 0 A_JumpIfInventory ("S7_Clip", 1, 1)
        Goto ReloadFinish
        REVR A 0 A_GiveInventory ("S7_RevolverClip", 1)
        REVR A 0 A_TakeInventory ("S7_Clip", 1)
        Loop
    ReloadFinish:
        RVLV A 1
        REVR A 0 A_PlaySound ("Weapons/Revolver/Open", CHAN_WEAPON)
        REVR ABCDEFG 1
        REVR H 0 A_PlaySound ("Weapons/Revolver/Eject", CHAN_WEAPON)
        REVR HIJKLMNOPQR 1
        REVR S 0 A_PlaySound ("Weapons/Revolver/In", CHAN_WEAPON)
        REVR STUV 1
        REVR W 0 A_PlaySound ("Weapons/Revolver/Close", CHAN_WEAPON)
        REVR WXYZ 1
        RVR2 AB 1
        RVR2 C 0 A_PlaySound ("Weapons/Revolver/Spin", CHAN_WEAPON)
        RVR2 CDEFGHGHGHGHGGHHGGHHGGGHHHGGGHHHH 1
        Goto Ready2
    }
}