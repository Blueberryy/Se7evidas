//-------------------------------------------------------------------------------------------
//
// Ω-Σ "Deathbringer" Revolver
//
//-------------------------------------------------------------------------------------------
actor S7_RevolverClip : ammo {
    inventory.maxAmount 6
    ammo.backpackMaxAmount 6

    +ignoreSkill
}

actor S7_Revolver : S7_BaseWeapon {
    tag "$REVOLVER"
    inventory.pickupMessage "$REVOLVER"
    weapon.ammoType1 "S7_RevolverClip"
    weapon.ammoType2 "S7_44MCartridges"
    weapon.ammoUse 1
    weapon.ammoGive 0

    states {
    Spawn:
        RVLV Z -1
        loop

    Ready:
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 1)
        RVLV A 1 A_WeaponReady
        loop

    Fire:
        TNT1 A   0 A_JumpIfInventory ("S7_RevolverClip", 1, 1)
        goto DryFire
        TNT1 A   0 A_PlaySound ("Weapons/Revolver/Fire", CHAN_Weapon)
        TNT1 A   0 A_AlertMonsters
        TNT1 A   0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 4, 5)
        TNT1 A   0 A_FireCustomMissile ("S7_RevolverTracer", fRandom [weaponSpread] (-1.0, 1.0), 1, 7, 2, 0, fRandom [weaponSpread] (-1.9, 1.9))
        RVLV AB  1 A_SetPitch (pitch - 1.75, SPF_Interpolate)
        RVLV C   1
        RVLV DE  1
        RVLV A  15
        goto Ready2

    DryFire:
        RVLV A 3 A_PlaySound ("Weapons/Revolver/Dryfire", CHAN_7)
        TNT1 A 0 A_JumpIfInventory ("S7_AutoReloading", 1, "Reload")
        goto Ready2

    Reload:
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_RevolverClip", 0, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("S7_44MCartridges", 1, "TrueReload")
        goto Ready2
    TrueReload:
        TNT1 A 0 A_JumpIfInventory ("S7_RevolverClip", 0, "ReloadFinish")
        TNT1 A 0 A_JumpIfInventory ("S7_44MCartridges", 1, 1)
        goto ReloadFinish
        TNT1 A 0 A_GiveInventory ("S7_RevolverClip", 1)
        TNT1 A 0 A_TakeInventory ("S7_44MCartridges", 1)
        loop
    ReloadFinish:
        RVLV A                             1
        TNT1 A                             0 A_PlaySound ("Weapons/Revolver/Open", CHAN_Weapon)
        REVR ABCDEFG                       1
        TNT1 A                             0 A_PlaySound ("Weapons/Revolver/Eject", CHAN_Weapon)
        TNT1 A                             0 A_FireCustomMissile ("S7_RevolverCasingSpawn", 0, 0, 8, -2)
        TNT1 A                             0 A_FireCustomMissile ("S7_RevolverCasingSpawn", 0, 0, 9, -2)
        TNT1 A                             0 A_FireCustomMissile ("S7_RevolverCasingSpawn", 0, 0, 10, -2)
        TNT1 A                             0 A_FireCustomMissile ("S7_RevolverCasingSpawn", 0, 0, 8, -3)
        TNT1 A                             0 A_FireCustomMissile ("S7_RevolverCasingSpawn", 0, 0, 9, -3)
        TNT1 A                             0 A_FireCustomMissile ("S7_RevolverCasingSpawn", 0, 0, 10, -3)
        REVR HIJKLMNOPQR                   1
        TNT1 A 0 A_PlaySound ("Weapons/Revolver/In", CHAN_Weapon)
        REVR STUV                          1
        TNT1 A 0 A_PlaySound ("Weapons/Revolver/Close", CHAN_Weapon)
        REVR WXYZ                          1
        RVR2 AB                            1
        TNT1 A 0 A_PlaySound ("Weapons/Revolver/Spin", CHAN_Weapon)
        RVR2 CDEF                          1
        RVLV BABABABABBAABBAABBBAAABBBAAAA 1
        goto Ready2
    }
}