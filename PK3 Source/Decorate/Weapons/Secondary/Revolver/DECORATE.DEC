actor S7_RevolverClip : ammo {
    inventory.MaxAmount 6
    ammo.BackpackMaxAmount 6
    +ignoreSkill
}

actor S7_Revolver : S7_BaseWeapon {
    inventory.PickupMessage "$REVOLVER"
    tag "$REVOLVER"
    weapon.AmmoType1 "S7_RevolverClip"
    weapon.AmmoType2 "S7_Clip"
    weapon.AmmoUse 1
    weapon.AmmoGive 6
    states {
    Spawn:
        RPTP A -1
        loop
    Ready:
    Ready2:
        TNT1 A 0 a_jumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 a_jumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 a_takeInventory ("S7_ChangeFireMode", 1)
        RVLV A 1 a_weaponReady
        loop
    /*Deselect:
        TNT1 A 0 a_jumpIfHealthLower (1, "Deselect.DEAD")
        TNT1 A 0 a_lower
        wait
    Select:
        TNT1 A 0 a_raise
        wait*/
    Fire:
        TNT1 A 0 a_jumpIfInventory ("S7_RevolverClip", 1, 1)
        goto DryFire
        TNT1 A 0 a_playSound ("Weapons/Revolver/Fire", CHAN_Weapon)
        TNT1 A 0 a_setPitch (pitch - 0.8)
        TNT1 A 0 a_alertMonsters
        TNT1 A 0 a_fireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 4, 5)
        RVLV B 1 bright a_fireCustomMissile ("S7_RevolverTracer", fRandom (-1.8, 1.8), 1, 5, 3, 0, fRandom (-2.7, 2.7))
        TNT1 A 0 a_setPitch (pitch - 0.8)
        RVLV CDEF 1
        RVLV A 15
        goto Ready2
    DryFire:
        RVLV A 3 a_playSound ("Weapons/Revolver/Dryfire", 7)
        goto Ready2
    Reload:
        TNT1 A 0 a_takeInventory ("S7_Reloading", 1)
        TNT1 A 0 a_jumpIfInventory ("S7_RevolverClip", 0, "Ready2")
        TNT1 A 0 a_jumpIfInventory ("S7_Clip", 1, "TrueReload")
        goto Ready2
    TrueReload:
        TNT1 A 0 a_jumpIfInventory ("S7_RevolverClip", 0, "ReloadFinish")
        TNT1 A 0 a_jumpIfInventory ("S7_Clip", 1, 1)
        goto ReloadFinish
        TNT1 A 0 a_giveInventory ("S7_RevolverClip", 1)
        TNT1 A 0 a_takeInventory ("S7_Clip", 1)
        loop
    ReloadFinish:
        RVLV A 1
        TNT1 A 0 a_playSound ("Weapons/Revolver/Open", CHAN_Weapon)
        REVR ABCDEFG 1
        TNT1 A 0 a_playSound ("Weapons/Revolver/Eject", CHAN_Weapon)
        REVR HIJKLMNOPQR 1
        TNT1 A 0 a_playSound ("Weapons/Revolver/In", CHAN_Weapon)
        REVR STUV 1
        TNT1 A 0 a_playSound ("Weapons/Revolver/Close", CHAN_Weapon)
        REVR WXYZ 1
        RVR2 AB 1
        TNT1 A 0 a_playSound ("Weapons/Revolver/Spin", CHAN_Weapon)
        RVR2 CDEFGHGHGHGHGGHHGGHHGGGHHHGGGHHHH 1
        goto Ready2
    }
}