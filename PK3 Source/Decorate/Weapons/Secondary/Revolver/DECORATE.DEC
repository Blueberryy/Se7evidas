actor S7_RevolverClip : ammo {
    inventory.MaxAmount 6
    ammo.BackpackMaxAmount 6
    +ignoreSkill
}

actor S7_Revolver : S7_BaseWeapon {
    inventory.PickupMessage "$REVOLVER"
    tag "$REVOLVER"
    weapon.AmmoType1 "S7_RevolverClip"
    weapon.AmmoType2 "S7_44MCartridges"
    weapon.AmmoUse 1
    weapon.AmmoGive 6
    states {
    Spawn:
        RPTP A -1
        loop
    Ready:
    Ready2:
        TNT1 A 0 A_JumpIfInventory ("S7_DoMelee", 1, "QuickMelee")
        TNT1 A 0 A_JumpIfInventory ("S7_Reloading", 1, "Reload")
        TNT1 A 0 A_TakeInventory ("S7_ChangeFireMode", 1)
        RVLV A 1 A_WeaponReady
        loop
    /*Deselect:
        TNT1 A 0 A_JumpIfHealthLower (1, "Deselect.DEAD")
        TNT1 A 0 A_Lower
        wait
    Select:
        TNT1 A 0 A_Raise
        wait*/
    Fire:
        TNT1 A 0 A_JumpIfInventory ("S7_RevolverClip", 1, 1)
        goto DryFire
        TNT1 A 0 A_PlaySound ("Weapons/Revolver/Fire", CHAN_Weapon)
        TNT1 A 0 A_AlertMonsters
        TNT1 A 0 A_FireCustomMissile ("S7_ShotSmoke_2_Spawner", 0, 0, 4, 5)
        TNT1 A 0 A_FireCustomMissile ("S7_RevolverTracer", fRandom [weaponSpread] (-1.8, 1.8), 1, 5, 3, 0, fRandom [weaponSpread] (-2.7, 2.7))
        RVLV BC 1 bright acs_namedExecuteAlways ("S7_RecoilPitch", 0, 0.8 * 65536)
        RVLV DEF 1
        RVLV A 15
        goto Ready2
    DryFire:
        RVLV A 3 A_PlaySound ("Weapons/Revolver/Dryfire", 7)
        TNT1 A 0 A_JumpIf (callACS ("S7_GetAutoReloading"), "Reload")
        goto Ready2
    
    Reload:
        TNT1 A 0 A_TakeInventory ("S7_Reloading", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_RevolverClip", 0, "Ready2")
        TNT1 A 0 A_JumpIfInventory ("S7_44MCartridges", 1, "TrueReload")
        goto Ready2
    TrueReload:
        TNT1 A 0 A_JumpIfInventory ("S7_RevolverClip", 0, "ReloadFinish")
        TNT1 A 0 A_JumpIfInventory ("S7_44MCartridges", 1, 1)
        goto ReloadFinish
        TNT1 A 0 A_GiveInventory ("S7_RevolverClip", 1)
        TNT1 A 0 A_TakeInventory ("S7_44MCartridges", 1)
        loop
    ReloadFinish:
        RVLV A 1
        TNT1 A 0 A_PlaySound ("Weapons/Revolver/Open", CHAN_Weapon)
        REVR ABCDEFG 1
        TNT1 A 0 A_PlaySound ("Weapons/Revolver/Eject", CHAN_Weapon)
        TNT1 A 0 A_FireCustomMissile ("S7_RevolverCasingSpawn", 0, 0, 8, -2)
        TNT1 A 0 A_FireCustomMissile ("S7_RevolverCasingSpawn", 0, 0, 9, -2)
        TNT1 A 0 A_FireCustomMissile ("S7_RevolverCasingSpawn", 0, 0, 10, -2)
        TNT1 A 0 A_FireCustomMissile ("S7_RevolverCasingSpawn", 0, 0, 8, -3)
        TNT1 A 0 A_FireCustomMissile ("S7_RevolverCasingSpawn", 0, 0, 9, -3)
        TNT1 A 0 A_FireCustomMissile ("S7_RevolverCasingSpawn", 0, 0, 10, -3)
        REVR HIJKLMNOPQR 1
        TNT1 A 0 A_PlaySound ("Weapons/Revolver/In", CHAN_Weapon)
        REVR STUV 1
        TNT1 A 0 A_PlaySound ("Weapons/Revolver/Close", CHAN_Weapon)
        REVR WXYZ 1
        RVR2 AB 1
        TNT1 A 0 A_PlaySound ("Weapons/Revolver/Spin", CHAN_Weapon)
        RVR2 CDEFGHGHGHGHGGHHGGHHGGGHHHGGGHHHH 1
        goto Ready2
    }
}