//-------------------------------------------------------------------------------------------
//
// Projectile bases
//
//-------------------------------------------------------------------------------------------
actor S7_FireballBase {
    radius 6
    height 16
    speed 15
    fastSpeed 20
    damage 8
    projectile
    renderStyle add
    alpha 1.0
    seeSound "baron/attack"
    deathSound "baron/shotx"
    decal "BaronScorch"

    +randomize
}

//-------------------------------------------------------------------------------------------
//
// Monster bases
//
//-------------------------------------------------------------------------------------------
actor S7_MonsterBase {
    health 60
    radius 20
    height 56
    mass 100
    speed 8

    /** Damage factors **/
    // Plasma/Laser damage bonus
    damageFactor "Plasma", 1.5
    damageFactor "LaserTracer", 1.25

    monster
    +floorClip +dontHurtSpecies

    var int user_deathType;
}

actor S7_DemonBase : S7_MonsterBase {
    // Demon flesh damage reduction
    damageFactor "PistolTracer", 0.7
    damageFactor "RevolverTracer", 0.8
    damageFactor "ShotgunTracer", 0.86
    damageFactor "RifleTracer", 0.9
}

actor S7_HellNobleBase : S7_DemonBase {
    health 1000
    radius 24
    height 64
    mass 1000
    painChance 50

    /** Damage factors **/
    // Demon flesh damage reduction
    damageFactor "PistolTracer", 0.7
    damageFactor "RevolverTracer", 0.8
    damageFactor "RifleTracer", 0.9
    // Hell Noble buckshot damage bonus
    damageFactor "ShotgunTracer", 1.2
}

actor S7_SuccubusBase : S7_DemonBase {
    health 400
    radius 18
    height 50
    mass 200
    painChance 90

    /** Damage factors **/
    // Demon flesh damage reduction
    // Succubi get lower reduction because they're mostly human
    damageFactor "PistolTracer", 0.75
    damageFactor "RevolverTracer", 0.85
    damageFactor "ShotgunTracer", 0.91
    damageFactor "RifleTracer", 0.95
}

actor S7_ScorpBase : S7_MonsterBase {
    radius 16
    height 56
    activeSound "Monsters/Nova/RadioChatter"
    seeSound "Monsters/Nova/RadioSight"

    /** Damage factors **/
    // Reanimated flesh damage reduction
    damageFactor "PistolTracer", 0.75
    damageFactor "RevolverTracer", 0.85
    damageFactor "ShotgunTracer", 0.91
    damageFactor "RifleTracer", 0.95
    // Plasma/Laser damage bonus
    damageFactor "Plasma", 1.6
    damageFactor "LaserTracer", 1.35

    // Default user vars
    var int user_fireMode;
    var int user_turn;
    var int user_spreadX; var int user_spreadY;
    var int user_ammo;
}

//-------------------------------------------------------------------------------------------
//
// Misc bases
//
//-------------------------------------------------------------------------------------------
/** Monster spawner base
*** Usage: You must change the Spawn state in spawners inheriting from this actor like this:
*** Spawn:
***     TNT1 A 0 noDelay A_SpawnItemEx ("<monster>", 0, 0, 0, 0, 0, 0, 0, SXF_MSpawner, 0, tid)
***     goto Prepare
*** Spawned actors must call A_KillMaster in their Death states, and must call A_RaiseMaster in their Raise states.
**/
const int SXF_MSpawner = SXF_TRANSFERPOINTERS | SXF_SETMASTER | SXF_NOCHECKPOSITION | SXF_TRANSFERAMBUSHFLAG | SXF_TRANSFERPITCH;
actor S7_MonsterSpawnerBase {
    health 0x7FFFFFFF
    radius 0
    height 0
    mass 0x7FFFFFFF
    speed 0
    damageFactor 0.0

    monster
    +floorClip +dontHurtSpecies +noBlockmap +noGravity
    +noTeleport +noInteraction +cannotPush +noTelestomp
    -shootable -vulnerable -solid -moveWithSector

    states {
        Spawn:
            TNT1 A 0 noDelay
            goto Prepare
        Prepare:
            TNT1 A 0 
        Idle:
            TNT1 A 1
            loop
        Death:
        Death.Ice:
            TNT1 A 0 A_NoBlocking
            TNT1 A -1
            stop
        Raise:
            TNT1 A 1
            goto Idle
    }
}
/// Same as the monster spawner
actor S7_BossSpawnerBase : S7_MonsterSpawnerBase {
    +boss +bossDeath

    states {
        Death:
        Death.Ice:
            TNT1 A 0 A_NoBlocking
            TNT1 A 0 A_BossDeath
            TNT1 A -1
            stop
    }
}

actor S7_FastProjTrailBase {
    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +clientsideOnly
}