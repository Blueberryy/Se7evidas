// Credits:
// Decorate: WildWeasel, Chronos "phantombeta" Ouroboros
// Sprites: ???
// Weapons: A_FireCustomMissile (tracerName, fRandom [weaponSpread] (-, horizontalSpread), 1, horizontalOffset, verticalOffset, 0, fRandom [weaponSpread] (-, verticalSpread))
// Monsters: A_CustomMissile (tracerName, verticalOffset, horizontalOffset, fRandom [monsterSpread] (-, horizontalSpread), 0, fRandom [monsterSpread] (-, verticalSpread))

//-------------------------------------------------------------------------------------------
//
// Base tracer
//
//-------------------------------------------------------------------------------------------
actor S7_TracerEffectsBase : S7_FastProjTrailBase {
    height 2
    radius 2
    scale 0.02120
    renderStyle add

    +forceXYBillboard
}

actor S7_BaseTracer : fastProjectile {
    alpha 1.0
    renderStyle add
    radius 2
    height 2
    speed 25
    health 75
    scale 0.02120
    damage (fRandom [weaponDamage] (1.0, 3.0) * 4)
    missileType "S7_TracerTrail"
    missileHeight 8
    decal BulletChip

    +bloodSplatter +noExtremeDeath +noDamageThrust +puffGetsOwner
    +forceXYBillboard

    states {
    Spawn:
        TNT1 AA 0 A_ScaleVelocity (1.0 / 25)
        TNT1 A  0 A_ScaleVelocity (health * 1.0)
    Idle:
        SPRK A 1 bright
        wait

    Death:
    Crash:
        TNT1 AAAA 0        A_SpawnItemEx ("S7_BulletPuffSparks", 0.0, 0.0, 0.0, fRandom [sfx] (-0.5, -2.0), fRandom [sfx] (-2.0, 2.0), fRandom [sfx] (1.5, 4.0), 0, SXF_Clientside)
        TNT1 AAA  0        A_SpawnItemEx ("S7_BulletPuffSmoke", 0.0, 0.0, 0.0, 0.0, fRandom [sfx] (-1.0, 1.0), fRandom [sfx] (-0.8, 0.8), 0, SXF_Clientside)
        SPRK A    2 bright
        SPRK A    2 bright A_SetScale (scaleX * 1.5, scaleY * 1.5)
        stop
    XDeath:
        TNT1 A 3 A_PlaySound ("Tracers/HitFlesh", CHAN_Body)
        stop
    }
}

actor S7_TracerTrail : S7_TracerEffectsBase {
    states {
    Spawn:
        SPRK AAA 1 bright A_FadeOut (0.4)
        stop
    }
}

//-------------------------------------------------------------------------------------------
//
// Bullet puff
//
//-------------------------------------------------------------------------------------------
actor S7_BulletPuff : S7_TracerEffectsBase replaces BulletPuff {
    scale 0.05

    -clientsideOnly

    states {
    Spawn:
        TNT1 A 0 noDelay A_PlaySound ("Tracers/HitWall", CHAN_Body)
    NoSound:
        TNT1 AAAA 0        A_SpawnItemEx ("S7_BulletPuffSparks", 0.0, 0.0, 0.0, fRandom [sfx] (-0.5, -2.0), fRandom [sfx] (-2.0, 2.0), fRandom [sfx] (1.5, 4.0), 0, SXF_Clientside)
        TNT1 AAA  0        A_SpawnItemEx ("S7_BulletPuffSmoke", 0.0, 0.0, 0.0, 0.0, fRandom [sfx] (-1.0, 1.0), fRandom [sfx] (-0.8, 0.8), 0, SXF_Clientside)
        SPRK A    2 bright
        SPRK A    2 bright A_SetScale (scaleX * 1.5, scaleY * 1.5)
        stop
    }
}

actor S7_BulletPuffSmoke : S7_BlackSmoke {
    alpha 0.2
    states {
    Spawn:
        SMOK A 8
        SMOK A 1 A_FadeOut (0.03)
        wait
    }
}

actor S7_BulletPuffSparks : S7_TracerEffectsBase {
    mass 1
    gravity 1.0

    +dontSplash
    -noGravity -noInteraction

    states {
    Spawn:
        SPRK A 1 bright
    Loople:
        SPRK A 1 bright A_FadeOut (0.05)
        loop
    Death:
        TNT1 A 0
        stop
    }
}

//-------------------------------------------------------------------------------------------
//
// Tracers
//
//-------------------------------------------------------------------------------------------
/*** Primary ***/
actor S7_AMGTracer              : S7_BaseTracer {
    damage (fRandom [weaponDamage] (1.0, 3.0) * 9)
    health 130
    damageType RifleTracer
}

actor S7_SSGTracer              : S7_BaseTracer { // 259 max DMG
    damage (fRandom [weaponDamage] (1.0, 3.0) * 2.3981)
    health 125
    damageType ShotgunTracer
    missileType "S7_SSGTracerTrail"

    states {
    Death:
    Crash:
        TNT1 AAA 0        A_SpawnItemEx ("S7_BulletPuffSparks", 0.0, 0.0, 0.0, fRandom [sfx] (-0.5, -2.0), fRandom [sfx] (-2.0, 2.0), fRandom [sfx] (1.5, 4.0), 0, SXF_Clientside)
        TNT1 AA  0        A_SpawnItemEx ("S7_BulletPuffSmoke", 0.0, 0.0, 0.0, 0.0, fRandom [sfx] (-1.0, 1.0), fRandom [sfx] (-0.8, 0.8), 0, SXF_Clientside)
        SPRK A   2 bright
        SPRK A   2 bright A_SetScale (scaleX * 1.5, scaleY * 1.5)
        stop
    }
}
actor S7_SSGTracerTrail : S7_TracerTrail {
    states {
    Spawn:
        SPRK AA 1 bright A_FadeOut (0.45)
        stop
    }
}

actor S7_12gaTracer             : S7_BaseTracer {
    damage (fRandom [weaponDamage] (1.0, 3.0) * 6)
    health 125
    damageType ShotgunTracer
    missileType "S7_SSGTracerTrail"
}
actor S7_ManxCarbineTracer      : S7_BaseTracer {
    damage (fRandom [weaponDamage] (1.0, 3.0) * 7)
    health 95
    damageType PistolTracer
}
actor S7_HitterSMGTracer        : S7_BaseTracer {
    damage (fRandom [weaponDamage] (1.0, 3.0) * 6)
    health 85
    damageType PistolTracer
}
actor S7_ConquerorRifle_Tracer  : S7_BaseTracer {
    damage (fRandom [weaponDamage] (1.0, 2.5) * 70)
    health 275 // So you need to compensate less when firing at long range
    damageType SniperRifleTracer
}

/*** Secondary ***/
actor S7_RaptorTracer           : S7_BaseTracer {
    damage (fRandom [weaponDamage] (1.5, 3.0) * 5)
    health 80
    damageType PistolTracer
}
actor S7_RevolverTracer         : S7_BaseTracer {
    damage (fRandom [weaponDamage] (1.75, 2.5) * 50)
    health 105
    damageType RevolverTracer
}
actor S7_TEC9Tracer             : S7_BaseTracer {
    damage (fRandom [weaponDamage] (1.5, 3.0) * 5)
    damageType PistolTracer
}