// Credits:
// Decorate: WildWeasel, Alex_Mercer
// Sprites: ???
// Weapons: a_fireCustomMissile (tracerName, fRandom (-, horizontalSpread), 1, horizontalOffset, verticalOffset, 0, fRandom (-, verticalSpread))
// Monsters: a_customMissile (tracerName, verticalOffset, horizontalOffset, fRandom (-, horizontalSpread), 0, fRandom (-, verticalSpread))

/*** Base ***/
actor S7_TracerEffectsBase {
	height 2
	radius 2
    alpha 1.0
    scale 0.025
    renderStyle add
    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +clientsideOnly +forceXYBillboard
}

actor S7_BaseTracer : fastProjectile {
    alpha 1.0
    renderStyle add
    radius 2
    height 2
    speed 25
    Health 75
    projectile
    +bloodSplatter +noExtremeDeath +noDamageThrust +puffGetsOwner
    +forceXYBillboard
    scale 0.025
    damage (random (1, 3) * 4)
    missileType "S7_TracerSpawner"
    decal Bullet
    states {
    Spawn:
        TNT1 AA 0 a_scaleVelocity (1.0 / (25 * 1.0))
        TNT1 A 0 a_scaleVelocity (health * 1.0)
        SPRK A 1 bright
        wait
    Death:
    Crash:
        TNT1 A 0 a_faceTarget
        TNT1 A 0 a_spawnItemEx ("S7_BulletPuff")
        stop
    XDeath:
        TNT1 A 3 a_playSound ("Bullet/HitFlesh", CHAN_Body, 0.5)
        stop
    }
}

actor S7_TracerSpawner {
    +noBlockmap +noGravity +noTeleport
    states {
    Spawn:
        TNT1 A 0
        TNT1 A 1 a_spawnItemEx ("S7_TracerTrail", 0.0, 0.0, 8.0, 0.0, 0.0, 0.0, 0, SXF_Clientside)
        stop
    }
}

actor S7_TracerTrail : S7_TracerEffectsBase {
    states {
    Spawn:
        SPRK A 1 bright a_fadeOut (0.3)
        wait
    }
}

actor S7_BulletPuff : S7_TracerEffectsBase replaces BulletPuff {
    -clientsideOnly
    states {
    Spawn:
        TNT1 A 0
        TNT1 AAAAA 0 a_spawnItemEx ("S7_BulletPuffSparks", 0.0, 0.0, 0.0, frandom (0.0, 2.0), frandom (-2.0, 2.0), frandom (0.0, 3.0), 0, SXF_Clientside)
        SPRK A 1 bright a_spawnItemEx ("S7_BulletPuffSparks", 0.0, 0.0, 0.0, frandom (0.0, 2.0), frandom (-2.0, 2.0), frandom (0.0, 3.0), 0, SXF_Clientside)
        stop
    }
}

actor S7_BulletPuffSparks : S7_TracerEffectsBase {
    mass 1
	damage 0
	+missile +dropOff +ghost +dontSplash
    -doomBounce -noGravity -noInteraction
	speed 10
	gravity 1.0
    states {
    Spawn:
        SPRK A 1 bright
        SPRK A 1 bright a_fadeOut (0.05)
        wait
    }
}

/*** Tracers ***/
actor S7_RaptorTracer : S7_BaseTracer { damage (random (1, 3) * 10) }
actor S7_RevolverTracer : S7_BaseTracer { damage (random (1, 3) * 40) health 95 }
actor S7_TEC9Tracer : S7_BaseTracer { damage (random (1, 3) * 9) health 80}
actor S7_SSGTracer : S7_BaseTracer { damage (random (1, 3) * 6) health 85}