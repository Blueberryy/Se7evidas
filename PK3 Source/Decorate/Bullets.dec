// Credits:
// Decorate: WildWeasel, Alex_Mercer
// Sprites: ???
// Weapons: A_FireCustomMissile (tracerName, fRandom [weaponSpread] (-, horizontalSpread), 1, horizontalOffset, verticalOffset, 0, fRandom [weaponSpread] (-, verticalSpread))
// Monsters: A_CustomMissile (tracerName, verticalOffset, horizontalOffset, fRandom [monsterSpread] (-, horizontalSpread), 0, fRandom [monsterSpread] (-, verticalSpread))

/*** Base ***/
actor S7_TracerEffectsBase {
    height 2
    radius 2
    alpha 1.0
    scale 0.02125
    renderStyle add

    +noBlockmap +noGravity +noTeleport +cannotPush
    +noInteraction +clientsideOnly +forceXYBillboard
}

actor S7_BaseTracer : fastProjectile {
    alpha 1.0
    renderStyle add
    radius 2
    height 2
    speed 25
    health 75
    projectile
    scale 0.02125
    damage (random (1, 3) * 4)
    missileType "S7_TracerSpawner"
    decal Bullet

    +bloodSplatter +noExtremeDeath +noDamageThrust +puffGetsOwner
    +forceXYBillboard

    states {
    Spawn:
        TNT1 AA 0 A_ScaleVelocity (1.0 / (25 * 1.0))
        TNT1 A 0 A_ScaleVelocity (health * 1.0)
        SPRK A 1 bright
        wait
    Death:
    Crash:
        TNT1 A 0 A_FaceTarget
        TNT1 A 0 A_SpawnItemEx ("S7_BulletPuff")
        stop
    XDeath:
        TNT1 A 3 A_PlaySound ("Bullet/HitFlesh", CHAN_Body, 0.5)
        stop
    }
}

actor S7_TracerSpawner {
    +noBlockmap +noGravity +noTeleport

    states {
    Spawn:
        TNT1 A 1 noDelay A_SpawnItemEx ("S7_TracerTrail", 0.0, 0.0, 8.0, 0.0, 0.0, 0.0, 0, SXF_Clientside)
        stop
    }
}

actor S7_TracerTrail : S7_TracerEffectsBase {
    states {
    Spawn:
        SPRK A 1 bright A_FadeOut (0.3)
        wait
    }
}

actor S7_BulletPuff : S7_TracerEffectsBase replaces BulletPuff {
    scale 0.05

    -clientsideOnly

    states {
    Spawn:
        TNT1 AAAAA 0 noDelay A_SpawnItemEx ("S7_BulletPuffSparks", 0.0, 0.0, 0.0, fRandom [sfx] (0.0, 2.0), fRandom [sfx] (-2.0, 2.0), fRandom [sfx] (1.5, 4.0), 0, SXF_Clientside)
        SPRK A 2 bright A_SpawnItemEx ("S7_BulletPuffSparks", 0.0, 0.0, 0.0, fRandom [sfx] (0.0, 2.0), fRandom [sfx] (-2.0, 2.0), fRandom [sfx] (1.5, 4.0), 0, SXF_Clientside)
        SPRK A 2 bright A_SetScale (scaleX * 1.5, scaleY * 1.5)
        stop
    }
}

actor S7_BulletPuffSparks : S7_TracerEffectsBase {
    mass 1
    gravity 1.0
    scale 0.02125

    +dontSplash
    -noGravity -noInteraction
    
    states {
    Spawn:
        SPRK A 1 bright
    Loople:
        TNT1 A 0 A_JumpIf (z == floorZ, "Death")
        SPRK A 1 bright A_FadeOut (0.05)
        loop
    Death:
        TNT1 A 0
        stop
    }
}

/*** Tracers ***/
/*** Primary ***/
actor S7_AMGTracer              : S7_BaseTracer {
    damage (random [weaponDamage] (1, 3) * 15)
    health 130
}
actor S7_SSGTracer              : S7_BaseTracer {
    damage (random [weaponDamage] (1, 3) * 6)
    health 125
}

/*** Secondary ***/
actor S7_RaptorTracer           : S7_BaseTracer {
    damage (random [weaponDamage] (1, 3) * 10)
    health 80
}
actor S7_RevolverTracer         : S7_BaseTracer {
    damage (random [weaponDamage] (1, 3) * 40)
    health 95
}
actor S7_TEC9Tracer             : S7_BaseTracer {
    damage (random [weaponDamage] (1, 3) * 10)
}