actor S7_Empress_FXBase {
    renderStyle add

    +noTeleport +noBlockmap +thruActors +clientsideOnly
    +noGravity +noInteraction
}

//-------------------------------------------------------------------------------------------
//
// Smoke
//
//-------------------------------------------------------------------------------------------
actor S7_EmpressSmoke : S7_ShotSmoke_2 {
    renderStyle subtract
    alpha 0.6
    scale 0.8

    +noInteraction +forceXYBillboard +clientsideOnly
    
    states {
    Spawn:
        ESPS ABCDEFGHIJKLMNOPQRSTUVW 1
        stop
    }
}

//-------------------------------------------------------------------------------------------
//
// Sparkle
//
//-------------------------------------------------------------------------------------------
actor S7_EmpressSparkle : S7_Empress_FXBase {
    renderStyle add

    var int user_loopCount;

    states {
    Spawn:
        TNT1 A 0 noDelay A_SetScale (fRandom (0.25, 0.4))
        TNT1 A 0 A_SetUserVar ("user_loopCount", random (4, 7))
        TNT1 A 1 A_Jump (256, "SpawnA", "SpawnB", "SpawnC", "SpawnD", "SpawnE", "SpawnF", "SpawnG", "SpawnH")
        wait

    SpawnA:
        HRSS A 0
        goto FadeOut
    SpawnB:
        HRSS B 0
        goto FadeOut
    SpawnC:
        HRSS C 0
        goto FadeOut
    SpawnD:
        HRSS D 0
        goto FadeOut

    SpawnE:
        HRSS E 0
        goto FadeOut
    SpawnF:
        HRSS F 0
        goto FadeOut
    SpawnG:
        HRSS G 0
        goto FadeOut
    SpawnH:
        HRSS H 0
        goto FadeOut

    FadeOut:
        "----" A 0 A_JumpIf (user_loopCount < 1, "ReallyFadeOutNow")
        "----" A 1 bright
        "----" A 1 A_SetUserVar ("user_loopCount", user_loopCount - 1)
        loop
    ReallyFadeOutNow:
        "----" A 1 A_FadeOut (0.1)
        wait
    }
}

//-------------------------------------------------------------------------------------------
//
// Pentagram
//
//-------------------------------------------------------------------------------------------
/** Corner **/
actor S7_EmpressPentagramCorner : S7_Empress_FXBase {
    states {
    Spawn:
        TNT1 A 0 noDelay A_CustomMissile ("S7_EmpressPentagramLine", 0.0, 0, -198, CMF_AimDirection)
        TNT1 A 0         A_CustomMissile ("S7_EmpressPentagramLine", 0.0, 0,  198, CMF_AimDirection)
        stop
    }
}

/** Line **/
actor S7_EmpressPentagramLine : S7_Empress_FXBase {
    radius 1
    height 1
    damage (0)
    speed 18

    projectile
    +noTarget +noBlood +painless +bloodlessImpact
    +noClip +ripper

    states {
    Spawn:
        TNT1 AAAAAAAAAA 1 A_SpawnItemEx ("S7_EmpressPentagramFire", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_Clientside | SXF_NoCheckPosition)
        stop
    }
}

/** Fire **/
actor S7_EmpressPentagramFire : S7_Empress_FXBase {
    radius 1
    height 1
    reactionTime 4

    states {
    Spawn:
        EPTF ABCDEFGHIJKLM 3 bright
        TNT1 A 0 A_CountDown
        loop

    Death:
        EPTF NOP 3
        stop
    }
}