#include "Decorate/Monsters/Empress/Effects.DEC"
#include "Decorate/Monsters/Empress/Spawner.DEC"
#include "Decorate/Monsters/Empress/Invuln.DEC"

//-------------------------------------------------------------------------------------------
//
// Empress (Female Heresiarch)
//
//-------------------------------------------------------------------------------------------
/** Constants **/
const int S7_EmpressMass = 12000;

/** Inventory tokens **/
// Booleans
actor S7_IsEmpress            : boolean { }
actor S7_EmpressCanCastInvuln : boolean { }
actor S7_EmpressNormalDeath   : boolean { }
// Counters
actor S7_EmpressBalls : counter { inventory.maxAmount 4 }

/** Main actor **/
actor S7_Empress {
    health 0x7FFFFFFF
    radius 40
    height 100
    speed 0//20
    painChance 15
    painChance Empress_BallExploded, 256
    painChance S7_SoulLance_Beam, 256
    minMissileChance 160
    bloodColor "B2 11 DE"
    seeSound "cyber/sight"
    painSound "cyber/pain"
    deathSound "cyber/death"
    activeSound "cyber/active"
    species S7Empress
    tag "$EMPRESSTAG"

    // Demon/Reanimated flesh damage reduction
    damageFactor "PistolTracer", 0.7
    damageFactor "RevolverTracer", 0.8
    damageFactor "RifleTracer", 0.9
    // Plasma/Laser damage bonus
    damageFactor "Plasma", 1.5
    damageFactor "LaserTracer", 1.25
    
    monster
    +boss +missileMore +floorClip +noRadiusDMG
    +noTarget +dontMorph +bossDeath

    var int user_trueHealth;
    var int user_setPain;

    states {
    Spawn:
        TNT1 A 0 noDelay A_GiveInventory ("S7_IsEmpress", 1)
        TNT1 A 0 A_SetUserVar ("user_trueHealth", 4500)
        TNT1 A 0 A_SetMass (S7_EmpressMass)
        HRFR A 1 ACS_NamedExecuteAlways ("S7_EmpressScript", 0)
        TNT1 A 0 A_TakeInventory ("S7_IsDead", 0x7FFFFFFF)
    Idle:
        TNT1 A   0 A_TakeInventory ("S7_IsDead", 0x7FFFFFFF)
        TNT1 A   0 A_JumpIf (user_trueHealth < 1, "RunAway")
        HRFR A   1 A_Look
        HRFR AAA 3 A_JumpIf (user_trueHealth < 1, "RunAway")
        loop

    See:
        TNT1 A  0 A_TakeInventory ("S7_IsDead", 0x7FFFFFFF)

        TNT1 A  0 A_PlaySound ("Footsteps/Large", CHAN_5)
        TNT1 A  0 A_JumpIf (user_trueHealth < 1, "RunAway")
        HRFR AA 4 A_Chase

        TNT1 A  0 A_JumpIf (user_trueHealth < 1, "RunAway")
        HRFR BB 4 A_Chase

        TNT1 A  0 A_PlaySound ("Footsteps/Large", CHAN_6)
        TNT1 A  0 A_JumpIf (user_trueHealth < 1, "RunAway")
        HRFR CC 4 A_Chase

        TNT1 A  0 A_JumpIf (user_trueHealth < 1, "RunAway")
        HRFR DD 4 A_Chase
        loop

    Missile:
        TNT1 A 0 A_JumpIfInventory ("S7_EmpressCanCastInvuln", 1, 1)
        goto Missile.NoShields
        TNT1 A 0 A_JumpIfInventory ("S7_EmpressBalls", 1, "Missile.NoShields")
        HRFR A 1 A_Jump (64, "ShieldsUp")
    Missile.NoShields:
        TNT1 A 0
        goto MissileAttack
    ShieldsUp:
        TNT1 A               0 A_JumpIfInventory ("S7_EmpressBalls", 1, "See")
        TNT1 A               0 A_ChangeFlag ("noPain", TRUE)
        HRFR EEEEEFFFFFFFFFF 1 A_FaceTarget
        TNT1 A               0 A_ChangeFlag ("noBlood", TRUE)
        TNT1 A               0 A_SetInvulnerable
        //TNT1 A               0 A_GiveInventory ("S7_EmpressProtection", 1)
        HRFR FFFF            5 A_SpawnItemEx ("S7_EmpressInvulnOrbiter", 64.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, SXF_NoCheckPosition | SXF_SetMaster)
        goto See

    MissileAttack:
        TNT1 A 0 A_Jump (256, "Attack1")
        goto See

    Attack1:
        HRFR A    2 A_FaceTarget
        HRFR H    3 A_FaceTarget
        TNT1 A    0 A_FaceTarget
        HRFR HHH  5 A_CustomMissile ("S7_EmpressMissile1", 105.0)
        HRFR HHHH 5
        goto See

    Pain:
        HRFR G 8 A_Pain
        goto See
    Pain.Empress_BallExploded:
        HRFR G 13 A_Pain
        goto See

    Pain.S7_SoulLance_Beam:
        TNT1 A 0 A_JumpIfInTargetInventory ("S7_SoulLance_BeamGrabbed", 1, "Pain")
        TNT1 A 0 A_Jump (4, "Pain")
        HRFR G 3 A_JumpIfInTargetLOS ("SLance_BeamGrab", 360.0, JLOSF_AllyNoJump)
        goto See
    SLance_BeamGrab:
        TNT1 A 0 A_SetMass (0x7FFFFFFF)
        TNT1 A 0 A_ChangeFlag ("noPain", TRUE)
        TNT1 A 0 A_GiveToTarget ("S7_SoulLance_BeamGrabbed", 1)
        TNT1 A 0 A_GiveInventory ("S7_SoulLance_BeamGrabbed", 1)
        HRFR E 2 ACS_NamedExecuteAlways ("S7_SLanceBeamGrab", 0)
    SLance_BeamHold:
        TNT1 A 0 A_JumpIfInventory ("S7_SoulLance_BeamGrabbed", 1, 1)
        goto SLance_BeamLost
        TNT1 A 0 A_JumpIfInTargetLOS ("SLance_BeamHold2", 360.0, JLOSF_AllyNoJump)
        goto SLance_BeamLost
    SLance_BeamHold2:
        HRFR E 1 A_FaceTarget
        TNT1 A 0 A_JumpIfInventory ("S7_SoulLance_BeamGrabbed", 1, 1)
        goto SLance_BeamLost
        TNT1 A 0 A_JumpIfInTargetLOS ("SLance_BeamHold3", 360.0, JLOSF_AllyNoJump)
        goto SLance_BeamLost
    SLance_BeamHold3:
        HRFR E 1
        TNT1 A 0 A_JumpIfInventory ("S7_SoulLance_BeamGrabbed", 1, 1)
        goto SLance_BeamLost
        TNT1 A 0 A_JumpIfInTargetLOS ("SLance_BeamHold4", 360.0, JLOSF_AllyNoJump)
        goto SLance_BeamLost
    SLance_BeamHold4:
        HRFR E 1
        TNT1 A 0 A_JumpIfInventory ("S7_SoulLance_BeamGrabbed", 1, 1)
        goto SLance_BeamLost
        TNT1 A 0 A_JumpIf (user_trueHealth < 1, "SLance_BeamLost")
        goto SLance_BeamHold
    SLance_BeamLost:
        TNT1 A 0 A_SetMass (S7_EmpressMass)
        TNT1 A 0 A_ChangeFlag ("noPain", FALSE)
        TNT1 A 0 A_TakeFromTarget ("S7_SoulLance_BeamGrabbed", 0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory ("S7_SoulLance_BeamGrabbed", 0x7FFFFFFF)
        HRFR E 3 A_FaceTarget
        TNT1 A 0 A_JumpIf (user_trueHealth < 1, "RunAway")
        goto See

    RunAway:
        TNT1 AAAAAAAAAAAA 0 A_GiveInventory ("S7_EmpressNormalDeath", 1)
        TNT1 AAAAAAAAAAAA 0 A_Die
        goto Death

    Death:
        TNT1 A 0 A_GiveInventory ("S7_IsDead", 1)
        TNT1 A 0 A_JumpIfInventory ("S7_SoulLance_BeamGrabbed", 1, "SLance_BeamLost")
    Death2:
        HRFR GGG    13 A_Pain
        HRFR AAAAAA  1 A_SpawnItemEx ("S7_EmpressDeathSmokeSpawner", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_Clientside | SXF_NoCheckPosition)
        HRFR A       2
        TNT1 A       0 A_SpawnItemEx ("S7_EmpressFlash", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_Clientside | SXF_NoCheckPosition)
        TNT1 A       0 A_NoBlocking
        TNT1 A       0 A_DamageChildren (0x7FFFFFFF, "NonKilledDeath")
        TNT1 A       0 A_KillChildren
        TNT1 A      -1 A_KillMaster
        stop
    }
}

actor S7_EmpressDeathSmokeSpawner {
    +noTeleport +noGravity +noInteraction +noBlockmap
    +clientsideOnly

    states {
    Spawn:
        TNT1 AAAAAAAA 0 A_SpawnItemEx ("S7_EmpressSmoke", fRandom (-23.0, 23.0), fRandom (-23.0, 23.0), 15.0 + fRandom (-0.0, 85.0), 0.0, 0.0, 0.0, 0, SXF_Clientside | SXF_NoCheckPosition)
        stop
    }
}

actor S7_EmpressMissile1 : fastProjectile {
    damage (random [monsterDMG] (2, 4) * 7)
    speed 45
    missileType "S7_EmpressMissile1Trail"
    missileHeight 8
    renderStyle add
    deathSound "imp/shotx"
    obituary "$OB_EMPRESSFB"

    +forceXYBillboard +SeekerMissile

    var int user_angel;
    var int user_loopCount;
    var int user_savedVelX; var int user_savedVelY; var int user_savedVelZ;

    states {
    Spawn:
        TNT1 A 0 noDelay A_SetUserVar ("user_angel", angle)
        TNT1 A 0 A_SetUserVar ("user_loopCount", 15)
        TNT1 A 0 A_SetUserVar ("user_savedVelX", velX * 65535.0) TNT1 A 0 A_SetUserVar ("user_savedVelY", velY * 65535.0) TNT1 A 0 A_SetUserVar ("user_savedVelZ", velZ * 65535.0)
        TNT1 A 0 A_ChangeVelocity (0.0, 0.0, 0.0, CVF_Replace)
    Loople:
        TNT1 A 0 A_JumpIfInTargetInventory ("S7_IsDead", 1, "DieOut")
        TNT1 A 0 A_JumpIf (user_loopCount < 1, "Flight")
        TNT1 A 0 A_Warp (AAPTR_Target, 25.0, 0.0, 105.0, user_angel, WARPF_AbsoluteAngle | WARPF_NoCheckPosition | WARPF_Interpolate)
        TNT1 A 0 A_SetUserVar ("user_angel", user_angel + 24)
        BAL1 A 1 A_SetUserVar ("user_loopCount", user_loopCount - 1)
        loop

    Flight:
        BAL1 A    1 A_Warp (AAPTR_Target, 25.0, 0.0, 105.0, 360, WARPF_AbsoluteAngle | WARPF_NoCheckPosition | WARPF_Interpolate)
        TNT1 A    0 A_ChangeVelocity (user_savedVelX / 65535.0, user_savedVelY / 65535.0, user_savedVelZ / 65535.0, CVF_Replace)
        TNT1 AAAA 0 A_SeekerMissile (360, 360, SMF_Precise | SMF_CurSpeed)
        BAL1 A    1
        wait

    DieOut:
        TNT1 AAAAAAAAAAAAAA 0 A_Die
    Death:
        BAL1 CDE 3 bright
        stop
    }
}

actor S7_EmpressMissile1Trail : S7_FastProjTrailBase {
    renderStyle add

    +forceXYBillboard

    states {
    Spawn:
        BAL1 AAA 1 A_FadeOut (0.35)
        stop
    }
}