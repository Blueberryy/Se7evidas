#include "Decorate/Monsters/Empress/Effects.DEC"
#include "Decorate/Monsters/Empress/Spawner.DEC"

//-------------------------------------------------------------------------------------------
//
// Empress (Female Heresiarch)
//
//-------------------------------------------------------------------------------------------
const int S7_EmpressMass = 12000;

actor S7_IsEmpress : boolean { }
actor S7_EmpressBalls : counter { inventory.maxAmount 4 }
actor S7_Empress {
    health 4000
    radius 40
    height 100
    speed 0//20
    painChance 15
    painChance Empress_BallExploded, 255
    painChance S7_SoulLance_Beam, 255
    monster
    minMissileChance 160
    obituary "$OB_EMPRESS"
    seeSound "cyber/sight"
    painSound "cyber/pain"
    deathSound "cyber/death"
    activeSound "cyber/active"
    
    +boss +missileMore +floorClip +noRadiusDMG
    +noTarget +dontMorph +bossDeath +quickToRetaliate

    states {
    Spawn:
        TNT1 A 0 noDelay A_GiveInventory ("S7_IsEmpress", 1)
        TNT1 A 0 A_SetMass (S7_EmpressMass)
    Idle:
        HRFR A 10 A_Look
        loop

    See:
        TNT1 A 0 A_Chase
        HRFR A 4 A_PlaySound ("Footsteps/Large", CHAN_5)
        HRFR ABB 4 A_Chase
        TNT1 A 0 A_PlaySound ("Footsteps/Large", CHAN_6)
        HRFR CCDD 4 A_Chase
        loop

    Missile:
        TNT1 A 0 A_Jump (64, "ShieldsUp") //"MissileAttack", "ShieldsUp")
        wait
    ShieldsUp:
        TNT1 A 0 A_JumpIfInventory ("S7_EmpressBalls", 1, "See")
        TNT1 A 0 A_ChangeFlag ("noPain", 1)
        HRFR EEEEEFFFFFFFFFF 1 A_FaceTarget
        TNT1 A 0 A_ChangeFlag ("noBlood", 1)
        TNT1 A 0 A_SetInvulnerable
        //TNT1 A 0 A_GiveInventory ("S7_EmpressProtection", 1)
        HRFR FFFF 5 A_SpawnItemEx ("S7_EmpressInvulnOrbiter", 64.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, SXF_NoCheckPosition | SXF_SetMaster)
        goto See

    Pain:
        HRFR G 8 A_Pain
        goto See
    Pain.Empress_BallExploded:
        HRFR G 13 A_Pain
        goto See

    Pain.S7_SoulLance_Beam:
        TNT1 A 0 A_Jump (4, "Pain")
        TNT1 A 0 A_JumpIfInTargetLOS ("SLance_BeamGrab", 90.0, JLOSF_AllyNoJump)
        goto See

    SLance_BeamGrab:
        TNT1 A 0 A_SetMass (0x7FFFFFFF)
        TNT1 A 0 A_ChangeFlag ("noPain", TRUE)
        TNT1 A 0 A_ChangeFlag ("quickToRetaliate", FALSE)
        TNT1 A 0 A_GiveToTarget ("S7_SoulLance_BeamGrabbed", 1)
        TNT1 A 0 A_GiveInventory ("S7_SoulLance_BeamGrabbed", 1)
        TNT1 A 0 ACS_NamedExecuteAlways ("S7_SLanceBeamGrab", 0)
    SLance_BeamHold:
        TNT1 A 0 A_JumpIfInTargetLOS ("SLance_BeamHold2", 90.0, JLOSF_AllyNoJump)
        goto SLance_BeamLost
    SLance_BeamHold2:
        HRFR E 1 A_FaceTarget
        TNT1 A 0 A_JumpIfInTargetLOS ("SLance_BeamHold3", 90.0, JLOSF_AllyNoJump)
        goto SLance_BeamLost
    SLance_BeamHold3:
        HRFR E 1
        TNT1 A 0 A_JumpIfInTargetLOS ("SLance_BeamHold4", 90.0, JLOSF_AllyNoJump)
        goto SLance_BeamLost
    SLance_BeamHold4:
        HRFR E 1
        goto SLance_BeamHold
    SLance_BeamLost:
        TNT1 A 0 A_SetMass (S7_EmpressMass)
        TNT1 A 0 A_ChangeFlag ("noPain", FALSE)
        TNT1 A 0 A_ChangeFlag ("quickToRetaliate", TRUE)
        TNT1 A 0 A_TakeFromTarget ("S7_SoulLance_BeamGrabbed", 0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory ("S7_SoulLance_BeamGrabbed", 0x7FFFFFFF)
        HRFR E 3 A_FaceTarget
        goto See
    SLance_BeamLost_ByDeath:
        TNT1 A 0 A_SetMass (S7_EmpressMass)
        TNT1 A 0 A_ChangeFlag ("noPain", FALSE)
        TNT1 A 0 A_ChangeFlag ("quickToRetaliate", TRUE)
        TNT1 A 0 A_TakeFromTarget ("S7_SoulLance_BeamGrabbed", 0x7FFFFFFF)
        TNT1 A 0 A_TakeInventory ("S7_SoulLance_BeamGrabbed", 0x7FFFFFFF)
        goto Death2

    Death:
        TNT1 A 0 A_JumpIfInventory ("S7_SoulLance_BeamGrabbed", 1, "SLance_BeamLost_ByDeath")
    Death2:
        TNT1 A 0 A_DamageChildren (0x7FFFFFFF, "NonKilledDeath")
        TNT1 A 0 A_KillChildren
        stop
    }
}

damageType NonKilledDeath {
    factor 0.0
    noArmor
}

//-------------------------------------------------------------------------------------------
//
// Empress' invulnerability orbiter
//
//-------------------------------------------------------------------------------------------
actor S7_EmpressInvulnOrbiter {
    health 100
    radius 20
    height 100
    renderStyle add
    damageFactor "NonKilledDeath", 1.0

    +isMonster +shootable +noTeleport +noBlood
    +noTargetSwitch +noGravity +forceXYBillboard +forcePain
    +foilInvul
    
    var int user_angle;
    var int user_countdown;
    var int user_loop;
    
    states {
    Spawn:
        TNT1 A 0 noDelay A_RearrangePointers (AAPTR_Master, AAPTR_Default, AAPTR_Master)
        TNT1 A 0 A_JumpIfInventory ("S7_IsEmpress", 1, "Initialize", AAPTR_Master)
        TNT1 A 0 A_Die
        goto Death
    
    Initialize:
        TNT1 A 0 A_GiveInventory ("S7_EmpressBalls", 1, AAPTR_Master)
        TNT1 A 0 A_SetUserVar ("user_countdown", 35 * 12)
        TNT1 A 0 A_ChangeFlag ("isMonster", 0)
    
    Idle:
        BAL1 A 1 bright A_Warp (AAPTR_Master, 64.0, 0.0, 0.0, user_angle, WARPF_AbsoluteAngle | WARPF_NoCheckPosition | WARPF_Interpolate)
        TNT1 A 0 A_SetUserVar ("user_countdown", user_countdown - 1)
        TNT1 A 0 A_SetUserVar ("user_angle", user_angle + 18)
    CountdownChecks:
        TNT1 A 0 A_JumpIf (user_countdown > 0, "Idle")
        TNT1 A 0 A_Die ("NonKilledDeath")
        TNT1 A 0 A_Jump (256, "Death.NonKilledDeath")
        wait

    Death:
        TNT1 A 1 A_GiveInventory ("S7_EmpressInvulnOrbiterDeath", 1, AAPTR_Master)
        TNT1 A 0 A_PrintBold ("S7_EmpressInvulnOrbiter: Line 98")
        TNT1 A 0 A_DamageMaster (random [miscDamage] (1, 8) * 8, "Empress_BallExploded") // That's gotta hurt.
        // Explosion here
        stop
    Death.NonKilledDeath:
        BAL1 A 1 bright A_Warp (AAPTR_Master, 64.0, 0.0, 0.0, user_angle, WARPF_AbsoluteAngle | WARPF_NoCheckPosition | WARPF_Interpolate)
        TNT1 A 0 A_SetUserVar ("user_angle", user_angle + 18)
        TNT1 A 0 A_FadeOut (0.03, 0)
        TNT1 A 0 A_JumpIf (alpha <= 0, "KILLKILLKILL")
        loop
    Death.EmpressDeath:
        BAL1 A 1 bright A_Stop
        TNT1 A 0 A_FadeOut (0.03, 0)
        TNT1 A 0 A_JumpIf (alpha <= 0, "KILLKILLKILL")
        loop
    KILLKILLKILL:
        TNT1 A 1 A_GiveInventory ("S7_EmpressInvulnOrbiterDeath", 1, AAPTR_Master)
        stop
    }
}

actor S7_EmpressInvulnOrbiterDeath : S7_ActionRunnerBase {
    inventory.maxAmount 4
    
    states {
    Pickup:
        TNT1 A 0 A_JumpIfInventory ("S7_IsEmpress", 1, "IsEmpress")
        goto Finish
    IsEmpress:
        TNT1 A 0 A_PrintBold ("S7_EmpressInvulnOrbiterDeath: Line 121")
        TNT1 A 0 A_TakeInventory ("S7_EmpressBalls", 1) // Ouch.
        TNT1 A 0 A_JumpIfInventory ("S7_EmpressBalls", 1, "Finish")
        TNT1 A 0 A_ChangeFlag ("noBlood", 0)
        TNT1 A 0 A_ChangeFlag ("noPain", 0)
        TNT1 A 0 A_UnsetInvulnerable
    Use:
    Finish:
        TNT1 A 0 A_RailWait
        stop
    }
}