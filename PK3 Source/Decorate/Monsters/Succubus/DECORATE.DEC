//-------------------------------------------------------------------------------------------
//
// Succubus
//
//-------------------------------------------------------------------------------------------
actor S7_Succubus : S7_SuccubusBase replaces cacodemon 32506 {
    health 350
    speed 8
    painChance 90
    seeSound "Succubus/Sight"
    activeSound "Succubus/Active"
    painSound "Succubus/Pain"
    deathSound "Succubus/Death"
    obituary "$OBSUCCUBUS"
    species "Succubus"
    tag "$SUCCUBUSTAG"

    var int user_Flying;

    states {
    Spawn:
        SUCC A 0 noDelay ACS_NamedExecuteAlways ("S7_SuccubusScript", 0)
        SUCC A 0 A_JumpIf (z - floorZ > 0, "StartFlying")
        SUCC A 1 A_Jump (256, "Idle")
        wait
    Idle:
        SUCC A 10 A_Look
        loop
    StartFlying:
        TNT1 A 0 A_ChangeFlag ("noGravity", TRUE)
        TNT1 A 0 A_ChangeFlag ("dropOff", TRUE)
        TNT1 A 0 A_ChangeFlag ("float", TRUE)
        TNT1 A 0 A_SetUserVar ("user_Flying", TRUE)
        TNT1 A 1 A_Jump (256, "IdleFlying")
        wait

    IdleFlying:
        TNT1 A    0 A_Look
        SUCF ABCB 3
        loop
    See:
        TNT1 A  0 A_JumpIf (user_Flying, "StartFlight")
        SUCC AA 4 A_Chase
        TNT1 A  0 A_JumpIf (user_Flying, "StartFlight")
        SUCC BB 4 A_Chase
        TNT1 A  0 A_JumpIf (user_Flying, "StartFlight")
        SUCC CC 4 A_Chase
        TNT1 A  0 A_JumpIf (user_Flying, "StartFlight")
        SUCC DD 4 A_Chase
        loop

    SeeFlying:
        TNT1 A  0 A_JumpIf (!user_Flying, "EndFlight")
        SUCF AA 2 A_Chase
        TNT1 A  0 A_JumpIf (!user_Flying, "EndFlight")
        SUCF BB 2 A_Chase
        TNT1 A  0 A_JumpIf (!user_Flying, "EndFlight")
        SUCF CC 2 A_Chase
        TNT1 A  0 A_JumpIf (!user_Flying, "EndFlight")
        SUCF BB 2 A_Chase
        loop

    StartFlight:
        TNT1 A   0 A_ChangeFlag ("noGravity", TRUE)
        TNT1 A   0 A_ChangeFlag ("dropOff", TRUE)
        TNT1 A   0 A_ChangeFlag ("float", TRUE)
        TNT1 A   0 ThrustThingZ (0, 3.5, FALSE, FALSE)
        SUCF ABCB 3
        goto SeeFlying

    EndFlight:
        TNT1 A   0 A_CheckFloor ("EndFlight.EndLoop")
        TNT1 A   0 ThrustThingZ (0, 3.5, TRUE, FALSE)
        SUCF ABCB 3
        loop
    EndFlight.EndLoop:
        TNT1 A   0 A_ChangeFlag ("noGravity", TRUE)
        TNT1 A   0 A_ChangeFlag ("dropOff", TRUE)
        TNT1 A   0 A_ChangeFlag ("float", TRUE)
        goto See

    Missile:
        SUCC AAAEEE 1 A_FaceTarget (60, 30)

        TNT1 A 0 A_FaceTarget (360, 180)
        SUCC F 3 A_CustomMissile ("S7_HellionBall", 0.0, 0, angle, CMF_AbsolutePitch | CMF_AbsoluteAngle, pitch)
        SUCC G 4
        TNT1 A 0 A_JumpIf (user_Flying, "StartFlight")
        goto See

    Pain:
        SUCC H 2
        SUCC H 2 A_Pain
        TNT1 A 0 A_JumpIf (user_Flying, "StartFlight")
        goto See

    Death:
        SUCC I 3 A_SetUserVar ("user_deathType", 0)
        SUCC J 3 A_Scream
        SUCC K 3
        TNT1 A 0 A_NoBlocking
    Death_AirLoop:
        SUCC L 3
        TNT1 A 0 A_CheckFloor ("Death_HitGround")
        loop
    Death_HitGround:
        SUCC MNOP 3
        SUCC Q   -1
        stop

    Death.Fire:
        TNT1 A 0 A_JumpIfHealthLower (-15, "RealDeath.Fire")
        goto Death
    Death.Fire2:
    RealDeath.Fire:
        BURN ABC        5 bright A_Pain
        BURN D          5 bright A_Pain
        TNT1 A          0        A_NoBlocking
        BURN EFGHIJKLMN 5 bright
        BURN O          5 bright
        BURN PQRSTU     5 bright
        BURN V          1
        wait

    Raise:
        SUCC Q        6
        SUCC PONMLKJI 6
        TNT1 A        0 A_JumpIf (user_Flying, "StartFlight")
        goto See
    }
}