//-------------------------------------------------------------------------------------------
//
// Hellion
//
//-------------------------------------------------------------------------------------------
actor S7_Hellion : S7_DemonBase replaces doomImp 33004 {
    health 60
    gibHealth -30
    radius 20
    height 56
    speed 8
    painChance 200
    seeSound "Hellion/Sight"
    activeSound "Hellion/Active"
    painSound "Hellion/Pain"
    deathSound "Hellion/Death"
    obituary "$OBHELLION"
    species "Hellion"
    tag "$HELLIONTAG"

    // Fire damage reduction
    damageFactor "Fire", 0.75

    states {
    Spawn:
        HELN A 0 noDelay ACS_NamedExecuteAlways ("S7_GenericMonsterScript", 0)
    Idle:
        HELN A 10 A_Look
        loop
    See:
        HELN AABBCCDDEEFF 2 A_Chase
        loop

    Missile:
        HELN GGGGGGHHIIJJKK 1 A_FaceTarget (26, 13)

        TNT1 A 0 A_FaceTarget (360, 180)
        HELN L 6 A_CustomMissile ("S7_HellionBall", 35.0, 0, angle, CMF_AbsolutePitch | CMF_AbsoluteAngle, pitch)
        goto See

    Pain:
        HELN M 2
        HELN M 2 A_Pain
        goto See

    Death:
        HELN N  5 A_SetUserVar ("user_deathType", 0)
        HELN O  5 A_Scream
        HELN P  5
        HELN Q  5 A_NoBlocking
        HELN RS 5
        HELN T -1
        stop
    Death.Fire:
        TNT1 A 0 A_JumpIfHealthLower (-15, "RealDeath.Fire")
        goto Death
    Death.Fire2:
    RealDeath.Fire:
        BURN ABC        5 bright A_Pain
        BURN D          5 bright A_Pain
        TNT1 A          0        A_NoBlocking
        BURN EFGHIJKLMN 5 bright
        BURN O          5 bright
        BURN PQRSTU     5 bright
        BURN V          1
        wait
    XDeath:
        HELN  U      6 A_SetUserVar ("user_deathType", 1)
        HELN  V      6 A_XScream
        HELN  W      6
        HELN  X      6 A_NoBlocking
        HELN "YZ[\"  6
        HELN "]"    -1
        stop

    Raise:
        HELN T      6 A_JumpIf (user_deathType == 1, "XRaise")
        HELN SRQPON 6
        goto See
    XRaise:
        HELN "]"        6
        HELN "\[ZYXWVU" 6
        goto See
    }
}

//-------------------------------------------------------------------------------------------
//
// Hellion fireball
//
//-------------------------------------------------------------------------------------------
actor S7_HellionBall : S7_FireballBase {
    damage (random [monsterDamage] (1, 4) * 6)
    damageType "Fire"
    speed 12
    fastSpeed 24

    states {
    Spawn:
        CP00 AABBCC 1 bright A_SpawnItemEx ("S7_HellionBallFire", 0.0, 0.0, -1.0, 0.0, 0.0, 0.0, 0, SXF_NoCheckPosition | SXF_Clientside)
        loop
    Death:
        CP00 DEFGH 2 bright
        stop
    }
}

actor S7_HellionBallFire : S7_FastProjTrailBase {
    states {
    Spawn:
        TNT1 A 0 noDelay A_SpawnItemEx ("S7_FireballSmoke",  0.0, fRandom (-3.0, 3.0), fRandom (-3.0, 3.0), fRandom (-1.8, -2.2), 0.0, 0.0, 0, SXF_NoCheckPosition | SXF_Clientside)
        TNT1 A 1         A_SpawnItemEx ("S7_FireballSmoke", -2.5, fRandom (-3.0, 3.0), fRandom (-3.0, 3.0), fRandom (-1.8, -2.2), 0.0, 0.0, 0, SXF_NoCheckPosition | SXF_Clientside)
        stop
    }
}