//-------------------------------------------------------------------------------------------
//
// Terminator
//
//-------------------------------------------------------------------------------------------
actor S7_Terminator : S7_DemonBase 32507 {
    health 5000
    radius 24
    height 64
    speed 12
    painChance 15
    mass 12000
    species S7Terminator
    tag "$TERMTAG"

    seeSound "Terminator/sight"
    painSound "Terminator/pain"
    deathSound "Terminator/death"
    activeSound "Terminator/active"

    // Demon/Reanimated flesh damage reduction
    damageFactor "PistolTracer", 0.7
    damageFactor "RevolverTracer", 0.8
    damageFactor "RifleTracer", 0.9
    // Plasma/Laser damage bonus
    damageFactor "Plasma", 1.5
    damageFactor "LaserTracer", 1.25

    monster
    +missileMore +missileEvenMore
    +floorClip +dontMorph +boss +bossDeath

    var int user_ReqGrenCheck;
    var int user_GrenType;

    states {
    Spawn:
        TERM A 1 noDelay ACS_NamedExecuteAlways ("S7_TerminatorScript", 0)
    Idle:
        TERM A 10 A_Look
        loop

    See:
        TERM A   4 A_PlaySound ("Footsteps/Large", CHAN_5)
        TERM ABB 4 A_Chase
        TERM C   4 A_PlaySound ("Footsteps/Large", CHAN_6)
        TERM CDD 4 A_Chase
        loop

    Missile:
        goto Missile.Grenade

    Missile.Plasma:
        TNT1 A 0 A_PlaySound ("Terminator/PlasmaStart", CHAN_WEAPON) // Play plasma attack begin sound

        TERM EEEEEE 1 A_FaceTarget (60.0, 30.0) // Spin to face target

        TNT1 A  0 A_FaceTarget (360, 180) // Make sure we're facing the target
        TNT1 A  0 A_PlaySound ("Terminator/PlasmaFire", CHAN_Weapon) // Play the firing sound
        TERM F  1 bright A_CustomMissile ("S7_TerminatorPlasma", 35.0, 18, angle + fRandom [monsterSpread] (-3.5, 3.5), CMF_AbsolutePitch | CMF_AbsoluteAngle, pitch) // Fire a S7_TerminatorPlasma at the current direction
        TERM FF 1 bright

        TERM EEE 1 A_FaceTarget (60.0, 30.0)
        TNT1 A   0 A_FaceTarget (360, 180)
        TERM F   1 bright A_CustomMissile ("S7_TerminatorPlasma", 35.0, 18, angle + fRandom [monsterSpread] (-3.5, 3.5), CMF_AbsolutePitch | CMF_AbsoluteAngle, pitch) // Fire a S7_TerminatorPlasma at the current direction
        TERM FF  1 bright

        TERM EEE 1 A_FaceTarget (60.0, 30.0)
        TNT1 A   0 A_FaceTarget (360, 180)
        TERM F   1 bright A_CustomMissile ("S7_TerminatorPlasma", 35.0, 18, angle + fRandom [monsterSpread] (-3.5, 3.5), CMF_AbsolutePitch | CMF_AbsoluteAngle, pitch) // Fire a S7_TerminatorPlasma at the current direction
        TERM FF  1 bright

        TERM EEEEE 1
        goto Missile.End

    Missile.Grenade:
        TNT1 A 0 A_SetUserVar ("user_ReqGrenCheck", 1)
        TNT1 A 0 A_PlaySound ("Terminator/GrenadeStart", CHAN_WEAPON) // Play grenade attack begin sound

        TERM JJJJJJ 1 A_FaceTarget (60.0, 30.0) // Spin to face target

        TERM J 2 A_FaceTarget (360, 180) // Make sure we're facing the target
        goto Missile.Nail
        TNT1 A 0 A_SetUserVar ("user_ReqGrenCheck", 2)
        TNT1 A 0 A_JumpIf (user_GrenType == 0, "Missile.Nail")
        TNT1 A 0 A_JumpIf (user_GrenType == 2, "See")

        TNT1 A  0 A_PlaySound ("Terminator/GrenadeFire", CHAN_Weapon) // Play the firing sound
        TERM K  1 bright A_CustomMissile ("S7_TerminatorGrenade", 35.0, 18, angle + fRandom [monsterSpread] (-2.0, 2.0), CMF_AbsolutePitch | CMF_AbsoluteAngle, pitch) // Fire a S7_TerminatorGrenade at the current direction
        TERM KK 1 bright

        TERM JJJJJ 1
        goto Missile.End
    Missile.Nail:
        TNT1 A  0 A_PlaySound ("Terminator/NailFire", CHAN_Weapon) // Play the firing sound
        TERM K  1 bright A_CustomMissile ("S7_TerminatorNail", 35.0, 18, angle + fRandom [monsterSpread] (-1.0, 1.0), CMF_AbsolutePitch | CMF_AbsoluteAngle, pitch) // Fire a S7_TerminatorNail at the current direction
        TERM KK 1 bright

        TERM JJJJJ 1
        goto Missile.End

    Missile.End:
        TNT1 A 0 A_SetUserVar ("user_ReqGrenCheck", 0)
        TNT1 A 0 A_SetUserVar ("user_GrenType", 0)
        goto See

    /*Melee:
        goto See*/

    Pain:
        TERM L 6
        TERM L 6 A_Pain
        goto See
    Death: //A_SpawnItemEx ("S7_Blood", 0.0, 0.0, 0.0, 0.0, 0.0, 0.0, 0, SXF_UseBloodColor)
        FAT1 J      2
        FAT1 J     10 A_Pain
        FAT1 L      4 A_Scream
        FAT1 M      4 A_NoBlocking
        FAT1 NOPQRS 4
        FAT1 T     -1 A_BossDeath
        stop
    }
}

actor S7_TerminatorGrenade {
    damage (fRandom [monsterDMG] (1.0, 3.8) * 10)
    speed 40
    deathSound "Explosions/Explosion1"
    gravity 0.2

    projectile
    -noGravity

    states {
    Spawn:
        GRN2 BA 1
        loop
    Death:
        TNT1 A 0 A_ChangeFlag ("noGravity", TRUE)
        TNT1 AAAAAAAAA 0 A_SpawnItemEx ("S7_Shrapnel", 0.2, fRandom (-0.025, 0.025), 0.25, fRandom (0.5, 4.8) * 4, 0.0, fRandom (3.2, 8.8), fRandom (0, 359), SXF_TransferPointers | SXF_NoCheckPosition)
        XPL1 A 3 A_Explode (96, 128)
        XPL1 BCDE 3
        stop
    }
}

actor S7_TerminatorGrenadeStronger : S7_TerminatorGrenade {
    damage (fRandom [monsterDMG] (1.0, 6.0) * 14.166666666666666666666666666667)
    speed 60
    deathSound "Explosions/Explosion2"
    /*missileType "S7_TerminatorGrenadeShockwave"
    missileHeight 8*/

    states {
    Spawn:
        GRN2 BB 1 A_SpawnItemEx ("S7_TerminatorGrenadeShockwave")
        GRN2 AA 1 A_SpawnItemEx ("S7_TerminatorGrenadeShockwave")
        loop
    Death:
        TNT1 AAAAA 0 A_SpawnItemEx ("S7_Shrapnel", 0.2, fRandom (-0.025, 0.025), 0.25, fRandom (0.5, 4.8) * 4, 0.0, fRandom (3.2, 8.8), fRandom (0, 359), SXF_TransferPointers | SXF_NoCheckPosition)
        XPL2 A 3 A_Explode (72, 104)
        XPL2 BCDEF 3
        stop
    }
}
actor S7_TerminatorGrenadeShockwave : S7_FastProjTrailBase {
    scale 0.25
    renderStyle add

    states {
    Spawn:
        SSHK ABCDEFGHIJKLMNOPQR 1 A_FadeOut (0.03)
        stop
    }
}

actor S7_TerminatorNail : fastProjectile {
    radius 2
    height 2
    damage (fRandom [monsterDMG] (1.0, 4.0) * 15)
    speed 1
    missileType "S7_HeatSmokeStatic"
    missileHeight 8
    gravity 0.25

    projectile
    +noGravity +bloodSplatter +noExtremeDeath +noDamageThrust

    states {
    Spawn:
        TNT1 AA 0 A_ScaleVelocity (90 * 1.0)
        NAIL A 5
        TNT1 A 0 A_ChangeFlag ("noGravity", FALSE)
    Flight:
        NAIL A 1
        loop
    Death:
        NAIL A 500
    Loople:
        NAIL A 1 A_FadeOut (0.03)
        wait
    XDeath:
        TNT1 A 3 A_PlaySound ("Bullet/HitFlesh", CHAN_Body, 0.5)
        stop
    }
}

actor S7_TerminatorPlasma : fastProjectile {
    damage (fRandom [monsterDMG] (1.0, 8.0) * 22)
    speed 55
    renderStyle add
    scale 0.08
    missileType "S7_TerminatorPlasma_Trail"
    missileHeight 8

    states {
    Spawn:
        FLR1 A 1 bright
        loop
    Death:
        
        stop
    }
}

actor S7_TerminatorPlasma_Trail : S7_FastProjTrailBase {
    scale 0.08
    renderStyle add

    states {
    Spawn:
        FLR1 AAA 1 bright A_FadeOut (0.25)
        stop
    }
}