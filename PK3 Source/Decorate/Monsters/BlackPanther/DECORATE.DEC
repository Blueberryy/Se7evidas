//-------------------------------------------------------------------------------------------
//
// Black Panther
//
//-------------------------------------------------------------------------------------------
actor S7_BlackPanther : S7_HellNobleBase replaces hellKnight {
    health 750
    speed 8
    bloodColor "00 67 67"
    seeSound "BlackPanther/Sight"
    activeSound "BlackPanther/Active"
    painSound "BlackPanther/Pain"
    deathSound "knight/death"
    hitObituary "$OBBPANTHER_MELEE"
    obituary "$OBBPANTHER"

    /** Damage factors **/
    // Demon/Reanimated flesh damage reduction
    damageFactor "PistolTracer", 0.7
    damageFactor "RevolverTracer", 0.8
    damageFactor "RifleTracer", 0.9
    // Plasma/Laser damage bonus
    damageFactor "Plasma", 1.5
    damageFactor "LaserTracer", 1.25
    // Hell Noble buckshot damage bonus
    damageFactor "ShotgunTracer", 1.2
    
    states {
    Spawn:
        BPNT A 10 A_Look 
        loop
    See:
        BPNT AABBCCDD 2 A_Chase
        loop
    Melee:
    Missile:
        BPNT EF 6 A_FaceTarget
        BPNT G  6 A_CustomComboAttack ("S7_BlackPantherBall", 32, random [monsterDamage] (1, 4) * 8, "baron/melee")
        goto See
    Pain:
        BPNT H 2
        BPNT H 2 A_Pain
        goto See
    Death:
        BPNT I  6
        BPNT J  6 A_Scream
        BPNT K  6
        BPNT L  6 A_NoBlocking
        BPNT MN 6
        BPNT O -1
        stop
    Raise:
        BPNT O      6
        BPNT NMLKJI 6
        goto See
    }
}

//-------------------------------------------------------------------------------------------
//
// Black Panther ball
//
//-------------------------------------------------------------------------------------------
actor S7_BlackPantherBall : S7_FireballBase {
    damage (random [monsterDamage] (1, 4) * 16)
    speed 18
    
    states {
    Spawn:
        PNTB AB 2
        loop
    Death:
        TNT1 A      0 A_Explode (16, 16, 0)
        PNTB CDEFGH 2
        stop
    }
}